import Lean
import Compiler.Modules.ERC20
import Compiler.Modules.Calls
import Compiler.Modules.Precompiles
import Compiler.Selectors
import Compiler.CompilationModel.InternalNaming
import Compiler.Keccak.Sponge
import Verity.Macro.Syntax
import Verity.Core.Intrinsics

namespace Verity.Macro

open Lean
open Lean.Elab
open Lean.Elab.Command

set_option hygiene false

abbrev Term := TSyntax `term
abbrev Cmd := TSyntax `command
abbrev Ident := TSyntax `ident
abbrev DoSeq := TSyntax `Lean.Parser.Term.doSeq

/-! Minimal session-local registry for intrinsics declared via `verity_intrinsic`.
   Sufficient for same-module declaration-before-use.
   Cross-module requires attribute-based collection (future). -/
private initialize intrinsicDeclRegistry : IO.Ref (Array Verity.Core.Intrinsics.IntrinsicDecl) ← IO.mkRef #[]

def getRegisteredIntrinsics : IO (Array Verity.Core.Intrinsics.IntrinsicDecl) :=
  intrinsicDeclRegistry.get

def registerIntrinsic (d : Verity.Core.Intrinsics.IntrinsicDecl) : IO Unit :=
  intrinsicDeclRegistry.modify (·.push d)

private def hardForkTermFromParsed (fork : Verity.Core.Intrinsics.HardFork) : CommandElabM Term := do
  match fork with
  | .cancun => `(Verity.Core.Intrinsics.HardFork.cancun)
  | .prague => `(Verity.Core.Intrinsics.HardFork.prague)
  | .osaka => `(Verity.Core.Intrinsics.HardFork.osaka)

private def hardForkTermFromIdent (fork : TSyntax `ident) : CommandElabM Term := do
  match Verity.Core.Intrinsics.HardFork.parse? (toString fork.getId) with
  | some parsed => hardForkTermFromParsed parsed
  | none =>
      throwErrorAt fork
        s!"unknown fork '{toString fork.getId}' (expected cancun, prague, osaka, or fusaka alias)"

inductive ValueType where
  | uint256
  | int256
  | uint8
  | uint16
  | address
  | bytes32
  | bool
  | string
  | bytes
  | array (elemTy : ValueType)
  | fixedArray (elemTy : ValueType) (size : Nat)
  | tuple (elemTys : List ValueType)
  | unit
  | newtype (name : String) (baseType : ValueType)  -- Semantic newtype; erased to baseType (#1727 Steps 3b/3c)
  | struct (name : String) (fields : List (String × ValueType)) -- Named ABI tuple with executable field access (#1750)
  | adt (name : String) (maxFields : Nat)  -- User-defined ADT (tagged union); maxFields = max variant field count (#1727 Step 5b)
  deriving Repr, BEq

inductive MappingKeyType where
  | address
  | uint256
  | bytes32
  deriving BEq

structure StructMemberDecl where
  name : String
  ty : ValueType := .uint256
  wordOffset : Nat
  packed : Option (Nat × Nat) := none
  deriving BEq

inductive StorageType where
  | scalar (ty : ValueType)
  | dynamicArray (elemTy : Compiler.CompilationModel.StorageArrayElemType)
  | mappingAddressToUint256
  | mapping2AddressToAddressToUint256
  | mappingUintToUint256
  | mappingChain (keyTypes : List MappingKeyType)
  | mappingStruct (keyType : MappingKeyType) (members : List StructMemberDecl)
  | mappingStruct2 (outerKey : MappingKeyType) (innerKey : MappingKeyType) (members : List StructMemberDecl)
  deriving BEq

structure StorageFieldDecl where
  ident : Ident
  name : String
  ty : StorageType
  slotNum : Nat
  adtInfo? : Option (String × Nat) := none
  packedBits : Option (Nat × Nat) := none
  emitDef : Bool := true
  aliases : List String := []

inductive StorageAccessorTree where
  | leaf (name : String) (ty : StorageType) (slotNum : Nat)
  | node (name : String) (children : Array StorageAccessorTree)

structure StorageStructAccessorDecl where
  ident : Ident
  name : String
  tree : StorageAccessorTree

/-- The arrow signature of a function-pointer parameter (#1747).
    Recorded only so that good diagnostics are available; the monomorphization
    pre-pass removes every function-pointer parameter before any model/IR
    lowering, so this signature is never lowered to the CompilationModel. -/
structure FuncPtrSig where
  paramTys : Array ValueType
  returnTy : ValueType

structure ParamDecl where
  ident : Ident
  name : String
  ty : ValueType
  interfaceName? : Option String := none
  /-- When `some sig`, this parameter is a higher-order function pointer
      (e.g. `op : (Uint256) -> Uint256`).  The CompilationModel has no
      first-class function values, so such parameters exist only transiently:
      the monomorphization pre-pass (#1747) specializes every call site to a
      concrete internal helper and drops the parameter.  `ty` is set to `.unit`
      as an inert placeholder that must never reach lowering. -/
  funcPtr? : Option FuncPtrSig := none

structure ErrorDecl where
  ident : Ident
  name : String
  params : Array ValueType

structure EventParamDecl where
  ident : Ident
  name : String
  ty : ValueType
  isIndexed : Bool

structure EventDecl where
  ident : Ident
  name : String
  params : Array EventParamDecl

structure ConstantDecl where
  ident : Ident
  name : String
  ty : ValueType
  body : Term

structure ImmutableDecl where
  ident : Ident
  name : String
  ty : ValueType
  body : Term

structure ExternalDecl where
  ident : Ident
  name : String
  params : Array ValueType
  returnTys : Array ValueType
  linkMode : Compiler.CompilationModel.ForeignLinkMode := .objectLinked
  interfaceName? : Option String := none
  isView : Bool := false

/-- A user-defined semantic newtype declared in the `types` section.
    At the language level the type is distinct from its base type; at the
    EVM/Yul level it is erased to the base type (zero overhead).
    (#1727, Axis 1 Step 3a) -/
structure NewtypeDecl where
  ident : Ident
  name : String
  baseType : ValueType

/-- A named ABI struct declared in the `verity_contract` body.
    It elaborates to a Lean `structure` for executable tests, while the compiler
    lowers it as an ABI tuple with named projection support. -/
structure StructDecl where
  ident : Ident
  name : String
  fields : Array ParamDecl

/-- A single variant (constructor) of a user-defined algebraic data type.
    E.g. `| Ok(value : Uint256)` or `| None`.
    (#1727, Axis 1 Step 5a) -/
structure AdtVariantDecl where
  ident : Ident
  name : String
  fields : Array ParamDecl

/-- A user-defined algebraic data type (tagged union) declared in the `inductive` section.
    E.g. `Result := | Ok(value : Uint256) | Err(code : Uint256)`.
    At the EVM level, ADTs use max-width tagged union encoding.
    (#1727, Axis 1 Step 5a) -/
structure AdtDecl where
  ident : Ident
  name : String
  variants : Array AdtVariantDecl

structure LocalObligationDecl where
  ident : Ident
  name : String
  obligation : String
  proofStatus : Compiler.ProofStatus

inductive InitGuardDecl where
  | initializer (fieldIdent : Ident) (fieldName : String)
  | reinitializer (fieldIdent : Ident) (fieldName : String) (version : Nat)

structure FunctionDecl where
  ident : Ident
  name : String
  params : Array ParamDecl
  returnTy : ValueType
  isPayable : Bool := false
  isView : Bool := false
  isPure : Bool := false
  isInternal : Bool := false
  noExternalCalls : Bool := false
  /-- When true, the function is annotated `allow_post_interaction_writes` and
      CEI (Checks-Effects-Interactions) enforcement is bypassed.  This is the
      explicit trust-surface opt-out in the escalation ladder (#1728, Axis 2 Step 2a). -/
  allowPostInteractionWrites : Bool := false
  /-- When `some fieldIdent`, the function is annotated `nonreentrant(field)`.
      The named storage field is used as a reentrancy lock.  CEI enforcement is
      bypassed because the lock prevents reentrant state corruption.
      (#1728, Axis 2 Step 2b — known-safe guard rung) -/
  nonReentrantLock : Option Ident := none
  /-- When true, the function is annotated `cei_safe` — the user asserts CEI
      safety via a machine-checked proof obligation.  CEI enforcement is bypassed
      and a proof obligation is generated.
      (#1728, Axis 2 Step 2b — Lean proof rung) -/
  ceiSafe : Bool := false
  /-- When `some fieldIdent`, the function is annotated `requires(field)`.
      The named Address-typed storage field is an access-control role.
      A `require(caller == roleHolder)` check is auto-injected at the start
      of the function body.  (#1728, Axis 2 Step 2c) -/
  requiresRole : Option Ident := none
  initGuard? : Option InitGuardDecl := none
  /-- Storage field names declared via `modifies(field1, field2)`.
      When non-empty, the compiler validates that the function body only
      writes to fields in this set and auto-generates a `_frame` theorem. -/
  modifies : Array Ident := #[]
  localObligations : Array LocalObligationDecl := #[]
  modifiers : Array Ident := #[]
  body : Term

structure InterfaceFunctionDecl where
  ident : Ident
  name : String
  params : Array ParamDecl
  returnTys : Array ValueType
  isView : Bool := false

structure InterfaceDecl where
  ident : Ident
  name : String
  functions : Array InterfaceFunctionDecl

structure ModifierDecl where
  ident : Ident
  name : String
  body : Term

private partial def valueTypeSignatureComponent : ValueType → String
  | .uint256 => "scalar_uint256"
  | .int256 => "scalar_int256"
  | .uint8 => "scalar_uint8"
  | .uint16 => "scalar_uint16"
  | .address => "scalar_address"
  | .bool => "scalar_bool"
  | .bytes32 => "scalar_bytes32"
  | .string => "scalar_string"
  | .bytes => "scalar_bytes"
  | .unit => "scalar_unit"
  | .array ty => "array_" ++ valueTypeSignatureComponent ty
  | .fixedArray ty size => "fixed_array_" ++ toString size ++ "_" ++ valueTypeSignatureComponent ty
  | .tuple tys => "tuple" ++ toString tys.length ++ "_" ++ String.intercalate "__" (tys.map valueTypeSignatureComponent)
  | .newtype name baseType => "newtype_" ++ name ++ "_" ++ valueTypeSignatureComponent baseType
  | .struct name fields =>
      "struct_" ++ name ++ "_" ++
        String.intercalate "__" (fields.map (fun field => field.fst ++ "_" ++ valueTypeSignatureComponent field.snd))
  | .adt name _ => "adt_" ++ name

private def functionSignatureKey (fn : FunctionDecl) : String :=
  fn.name ++ "(" ++ String.intercalate "," (fn.params.toList.map (fun p => valueTypeSignatureComponent p.ty)) ++ ")"

private partial def valueTypeAbiSignatureComponent : ValueType → String
  | .newtype _ baseType => valueTypeAbiSignatureComponent baseType
  | .array ty => "array_" ++ valueTypeAbiSignatureComponent ty
  | .fixedArray ty size => "fixed_array_" ++ toString size ++ "_" ++ valueTypeAbiSignatureComponent ty
  | .tuple tys => "tuple" ++ toString tys.length ++ "_" ++ String.intercalate "__" (tys.map valueTypeAbiSignatureComponent)
  | .struct _ fields =>
      "tuple" ++ toString fields.length ++ "_" ++ String.intercalate "__" (fields.map (fun field => valueTypeAbiSignatureComponent field.snd))
  | .adt _ maxFields =>
      "tuple" ++ toString (maxFields + 1) ++ "_" ++
        String.intercalate "__" ("scalar_uint8" :: List.replicate maxFields "scalar_uint256")
  | ty => valueTypeSignatureComponent ty

private def functionAbiSignatureKey (fn : FunctionDecl) : String :=
  fn.name ++ "(" ++ String.intercalate "," (fn.params.toList.map (fun p => valueTypeAbiSignatureComponent p.ty)) ++ ")"

private def nameComponents : Name → List String
  | .anonymous => []
  | .str parent part => nameComponents parent ++ [part]
  | .num parent n => nameComponents parent ++ [toString n]

private def mapNameLastComponent (f : String → String) : Name → Name
  | .anonymous => .anonymous
  | .str parent part => .str parent (f part)
  | .num parent n => .str parent (f (toString n))

private def isQualifiedFunctionName (name : Name) : Bool :=
  (nameComponents name).length == 2

private def startsWithLowercaseAscii (s : String) : Bool :=
  match s.data with
  | c :: _ => 'a' ≤ c && c ≤ 'z'
  | [] => false

private def qualifiedFunctionModelName (name : Name) : Name :=
  mapNameLastComponent (fun part => part ++ "_model") name

private def qualifiedFunctionDisplayName (name : Name) : String :=
  String.intercalate "." (nameComponents name)

private def internalHelperSpecNameFor (fn : FunctionDecl) : String :=
  Compiler.CompilationModel.internalFunctionPrefix ++ toString fn.ident.getId

private def qualifiedInternalHelperBaseName (name : Name) : String :=
  let encodedComponents :=
    nameComponents name |>.map fun component =>
      s!"{component.length}_{component}"
  Compiler.CompilationModel.internalFunctionPrefix ++
    "qualified_" ++ String.intercalate "_" encodedComponents

private def qualifiedInternalHelperName (functions : Array FunctionDecl) (name : Name) : String :=
  Compiler.CompilationModel.pickFreshName
    (qualifiedInternalHelperBaseName name)
    ((functions.map internalHelperSpecNameFor).toList)

private def qualifiedInternalHelperNameFromUsed
    (usedNames : List String)
    (name : Name) : String :=
  Compiler.CompilationModel.pickFreshName (qualifiedInternalHelperBaseName name) usedNames

private partial def qualifiedFunctionAppSyntax? (stx : Term) : Option (Name × Array Term) :=
  match stx.raw with
  | .node _ `Lean.Parser.Term.doExpr args =>
      match args.getD 0 Syntax.missing with
      | inner => qualifiedFunctionAppSyntax? ⟨inner⟩
  | .node _ `Lean.Parser.Term.paren args =>
      match args.getD 1 Syntax.missing with
      | inner => qualifiedFunctionAppSyntax? ⟨inner⟩
  | .node _ `Lean.Parser.Term.app args =>
      match args.getD 0 Syntax.missing with
      | .ident _ _ raw _ =>
          if isQualifiedFunctionName raw then
            let argTerms := (args.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
            some (raw, argTerms)
          else
            none
      | _ => none
  | _ => none

private def overloadedFunctionIdentName (fn : FunctionDecl) : String :=
  -- Length-prefix each component so the suffix encoding is injective.
  -- Component strings can contain `_` (e.g. `newtype_Foo_scalar_uint256`,
  -- `tuple2_scalar_uint256__scalar_bool`), so a plain `_` join is ambiguous
  -- and distinct overloads can collapse to the same identifier.
  let suffix :=
    match fn.params.toList.map (fun p => valueTypeSignatureComponent p.ty) with
    | [] => "0"
    | parts => String.join (parts.map fun p => toString p.length ++ "x" ++ p)
  fn.name ++ "__" ++ suffix

private def assignOverloadInternalIdents (functions : Array FunctionDecl) :
    Array FunctionDecl :=
  functions.map fun fn =>
    if functions.any (fun other => other.name == fn.name && functionSignatureKey other != functionSignatureKey fn) then
      { fn with ident := mkIdentFrom fn.ident (Name.mkSimple (overloadedFunctionIdentName fn)) }
    else
      fn

structure ConstructorDecl where
  params : Array ParamDecl
  isPayable : Bool := false
  localObligations : Array LocalObligationDecl := #[]
  body : Term

private def strTerm (s : String) : Term := ⟨Syntax.mkStrLit s⟩
private def natTerm (n : Nat) : Term := ⟨Syntax.mkNumLit (toString n)⟩

def yulLoweringTerm (lowering : Verity.Core.Intrinsics.YulLowering) : CommandElabM Term := do
  match lowering with
  | .verbatim inArity outArity opcodeHex =>
      `(Verity.Core.Intrinsics.YulLowering.verbatim
          $(natTerm inArity) $(natTerm outArity) $(strTerm opcodeHex))
  | .builtin name =>
      `(Verity.Core.Intrinsics.YulLowering.builtin $(strTerm name))

private partial def expectTermListLiteral (stx : Term) : CommandElabM (Array Term) := do
  match stx with
  | `(term| [ $[$xs],* ]) => pure xs
  | `(term| ($inner:term)) => expectTermListLiteral inner
  | _ => throwErrorAt stx "expected list literal [..]"

private partial def collectTupleElems (stx : Syntax) : Array Syntax :=
  if stx.isAtom then
    #[]
  else if stx.getKind == `null then
    stx.getArgs.foldl (fun acc child => acc ++ collectTupleElems child) #[]
  else
    #[stx]

private def tupleElemsFromSyntax? (stx : Syntax) : Option (Array Syntax) :=
  if stx.getKind == `Lean.Parser.Term.tuple then
    some (collectTupleElems stx[1])
  else
    none

private partial def expectMappingKeyTerms (stx : Term) : CommandElabM (Array Term) := do
  expectTermListLiteral stx

private partial def collectArrowChainTypes (stx : Term) : CommandElabM (List Term × Term) := do
  match stx with
  | `(term| $lhs:term → $rhs:term) =>
      let (rest, resultTy) ← collectArrowChainTypes rhs
      pure (lhs :: rest, resultTy)
  | _ => pure ([], stx)

private def natFromSyntax (stx : Syntax) : CommandElabM Nat :=
  match stx.isNatLit? with
  | some n => pure n
  | none => throwErrorAt stx "expected natural literal"

private partial def stripParens (stx : Term) : Term :=
  match stx with
  | `(term| ($inner)) => stripParens inner
  | _ => stx

private def structValueTypeFields (decl : StructDecl) : List (String × ValueType) :=
  decl.fields.toList.map fun field => (field.name, field.ty)

private partial def valueTypeFromSyntax
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (ty : Term) : CommandElabM ValueType := do
  let ty := stripParens ty
  let (arrowArgs, _arrowResult) ← collectArrowChainTypes ty
  if !arrowArgs.isEmpty then
    throwErrorAt ty
      "unsupported function type in verity_contract boundary (#1747); internal function-pointer parameters are not first-class in the CompilationModel yet. Pass an explicit mode/enum and dispatch to direct internal helper calls, or inline the helper call at each call site."
  match ty with
  | `(term| Uint256) => pure .uint256
  | `(term| Int256) => pure .int256
  | `(term| Uint8) => pure .uint8
  | `(term| Uint16) => pure .uint16
  | `(term| Address) => pure .address
  | `(term| Bytes32) => pure .bytes32
  | `(term| Bool) => pure .bool
  | `(term| String) => pure .string
  | `(term| Bytes) => pure .bytes
  | `(term| Array $elemTy:term) =>
      let elem ← valueTypeFromSyntax newtypes structDecls adtDecls elemTy
      match elem with
      | .unit => throwErrorAt ty "unsupported type '{ty}'; Array Unit is not allowed"
      | .array _ => throwErrorAt ty "unsupported type '{ty}'; nested arrays are not supported"
      | _ => pure (.array elem)
  | `(term| FixedArray $elemTy:term $size:num) =>
      let elem ← valueTypeFromSyntax newtypes structDecls adtDecls elemTy
      let n ← natFromSyntax size
      match elem with
      | .unit => throwErrorAt ty "unsupported type '{ty}'; FixedArray Unit is not allowed"
      | .array _ => throwErrorAt ty "unsupported type '{ty}'; FixedArray of dynamic Array is not supported"
      | _ => pure (.fixedArray elem n)
  | `(term| Tuple [ $[$elemTys:term],* ]) =>
      let elems ← elemTys.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
      if elems.size < 2 then
        throwErrorAt ty "tuple types must have at least 2 elements"
      pure (.tuple elems.toList)
  | `(term| Unit) => pure .unit
  | `(term| $id:ident) =>
      let tyName := toString id.getId
      -- Try resolving as a user-defined newtype (#1727, Axis 1 Steps 3a/3b)
      match newtypes.find? (fun nt => nt.name == tyName) with
      | some nt => pure (.newtype nt.name nt.baseType)
      | none =>
        -- Try resolving as a user-defined ADT (#1727, Axis 1 Step 5b)
        match structDecls.find? (fun s => s.name == tyName) with
        | some decl => pure (.struct decl.name (structValueTypeFields decl))
        | none =>
          match adtDecls.find? (fun a => a.name == tyName) with
          | some decl =>
              let maxFields := decl.variants.foldl (fun acc v => max acc v.fields.size) 0
              pure (.adt decl.name maxFields)
          | none => throwErrorAt ty "unsupported type '{ty}'; expected Uint256, Int256, Uint8, Uint16, Address, Bytes32, Bool, String, Bytes, Array <type>, FixedArray <type> <size>, Tuple [...], Unit, a user-defined struct, or a user-defined type from the `types` or `inductive` section"
  | _ =>
      throwErrorAt ty "unsupported type '{ty}'; expected Uint256, Int256, Uint8, Uint16, Address, Bytes32, Bool, String, Bytes, Array <type>, FixedArray <type> <size>, Tuple [...], Unit, a user-defined struct, or a user-defined type from the `types` or `inductive` section"

private def storageTypeFromSyntax (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl := #[]) (adtDecls : Array AdtDecl := #[]) (ty : Term) : CommandElabM StorageType := do
  let keyTypeFromSyntax (stx : Term) : CommandElabM MappingKeyType := do
    match stx with
    | `(term| Address) => pure .address
    | `(term| Uint256) => pure .uint256
    | `(term| Bytes32) => pure .bytes32
    | _ => throwErrorAt stx "unsupported mapping key type; expected Address, Uint256, or Bytes32"

  let structMemberFromSyntax (stx : TSyntax `verityStructMember) : CommandElabM StructMemberDecl := do
    match stx with
    | `(verityStructMember| $name:ident @word $wordOffset:num) =>
        pure {
          name := toString name.getId
          wordOffset := ← natFromSyntax wordOffset
        }
    | `(verityStructMember| $name:ident : $memberTy:term @word $wordOffset:num) =>
        pure {
          name := toString name.getId
          ty := ← valueTypeFromSyntax newtypes structDecls adtDecls memberTy
          wordOffset := ← natFromSyntax wordOffset
        }
    | `(verityStructMember| $name:ident @word $wordOffset:num packed($offset:num,$width:num)) =>
        pure {
          name := toString name.getId
          wordOffset := ← natFromSyntax wordOffset
          packed := some (← natFromSyntax offset, ← natFromSyntax width)
        }
    | `(verityStructMember| $name:ident : $memberTy:term @word $wordOffset:num packed($offset:num,$width:num)) =>
        pure {
          name := toString name.getId
          ty := ← valueTypeFromSyntax newtypes structDecls adtDecls memberTy
          wordOffset := ← natFromSyntax wordOffset
          packed := some (← natFromSyntax offset, ← natFromSyntax width)
        }
    | _ => throwErrorAt stx "invalid struct member declaration"

  let rec storageStructMemberElementWords (memberName : String) : ValueType → CommandElabM Nat
    | .uint256 | .int256 | .uint16 | .address | .bool | .bytes32 => pure 1
    | .newtype _ baseType => storageStructMemberElementWords memberName baseType
    | .fixedArray elemTy size => do
        let elemWords ← storageStructMemberElementWords memberName elemTy
        pure (elemWords * size)
    | other =>
        throwErrorAt ty
          s!"mapping struct member '{memberName}' has unsupported storage type {repr other}; expected a word-like type or FixedArray of word-like types"

  let rec expandStructMemberDecl (memberPrefix : String) (baseOffset : Nat) (memberTy : ValueType)
      (packed : Option (Nat × Nat)) : CommandElabM (List StructMemberDecl) := do
    match memberTy with
    | .newtype _ baseType =>
        expandStructMemberDecl memberPrefix baseOffset baseType packed
    | .fixedArray elemTy size => do
        if packed.isSome then
          throwErrorAt ty s!"mapping struct fixed-array member '{memberPrefix}' cannot be packed"
        let elemWords ← storageStructMemberElementWords memberPrefix elemTy
        let nested ← (List.range size).mapM fun idx =>
          expandStructMemberDecl s!"{memberPrefix}[{idx}]" (baseOffset + idx * elemWords) elemTy none
        pure nested.flatten
    | .uint256 | .int256 | .uint16 | .address | .bool | .bytes32 =>
        pure [{ name := memberPrefix, ty := memberTy, wordOffset := baseOffset, packed := packed }]
    | other =>
        throwErrorAt ty
          s!"mapping struct member '{memberPrefix}' has unsupported storage type {repr other}; expected a word-like type or FixedArray of word-like types"

  let expandStructMembers (members : Array StructMemberDecl) : CommandElabM (List StructMemberDecl) := do
    let expanded ← members.mapM fun member =>
      expandStructMemberDecl member.name member.wordOffset member.ty member.packed
    pure expanded.toList.flatten

  let storageArrayElemTypeFromValueType (elemTy : ValueType) : CommandElabM Compiler.CompilationModel.StorageArrayElemType :=
    match elemTy with
    | .uint256 => pure .uint256
    | .address => pure .address
    | .bool => pure .bool
    | .bytes32 => pure .bytes32
    | _ =>
        throwErrorAt ty
          s!"storage dynamic arrays currently support only one-word elements (Uint256, Address, Bool, Bytes32) on the macro path, got {reprStr (ValueType.array elemTy)}"

  let (arrowArgs, arrowResult) ← collectArrowChainTypes ty
  if !arrowArgs.isEmpty then
    match arrowResult with
    | `(term| Uint256) =>
        let keyTypes ← arrowArgs.mapM keyTypeFromSyntax
        match keyTypes with
        | [.address] => pure .mappingAddressToUint256
        | [.uint256] => pure .mappingUintToUint256
        | [.address, .address] => pure .mapping2AddressToAddressToUint256
        | _ => pure (.mappingChain keyTypes)
    | _ =>
        throwErrorAt ty "unsupported mapping value type; expected Uint256"
  else
    match ty with
  | `(term| MappingStruct($keyTy:term,[ $[$members:verityStructMember],* ])) =>
      pure <| .mappingStruct
        (← keyTypeFromSyntax keyTy)
        (← expandStructMembers (← members.mapM structMemberFromSyntax))
  | `(term| MappingStruct2($outerKey:term,$innerKey:term,[ $[$members:verityStructMember],* ])) =>
      pure <| .mappingStruct2
        (← keyTypeFromSyntax outerKey)
        (← keyTypeFromSyntax innerKey)
        (← expandStructMembers (← members.mapM structMemberFromSyntax))
  | _ => do
      let vt ← valueTypeFromSyntax newtypes structDecls adtDecls ty
      match vt with
      | .array elemTy => pure (.dynamicArray (← storageArrayElemTypeFromValueType elemTy))
      | .tuple _ => throwErrorAt ty "storage fields cannot be Tuple; use mapping encodings"
      | .struct _ _ =>
          throwErrorAt ty
            "top-level named struct storage fields are not supported yet (#1758); flatten the struct into explicit scalar storage fields with fixed slots, or use MappingStruct/MappingStruct2 for struct-valued mappings"
      | _ => pure (.scalar vt)

private def modelMappingKeyTypeTerm : MappingKeyType → CommandElabM Term
  | .address => `(Compiler.CompilationModel.MappingKeyType.address)
  | .uint256 => `(Compiler.CompilationModel.MappingKeyType.uint256)
  | .bytes32 => `(Compiler.CompilationModel.MappingKeyType.bytes32)

private def storageTypeMappingKeyTypes? : StorageType → Option (List MappingKeyType)
  | .mappingAddressToUint256 => some [.address]
  | .mapping2AddressToAddressToUint256 => some [.address, .address]
  | .mappingUintToUint256 => some [.uint256]
  | .mappingChain keyTypes => some keyTypes
  | _ => none

private def storageTypeMappingDepth? (ty : StorageType) : Option Nat :=
  storageTypeMappingKeyTypes? ty |>.map List.length

private def storageKeyTypeContractTerm : MappingKeyType → CommandElabM Term
  | .address => `(Address)
  | .uint256 => `(Uint256)
  | .bytes32 => `(Bytes32)

private def modelStructMemberTerm (member : StructMemberDecl) : CommandElabM Term := do
  let packedTerm ←
    match member.packed with
    | none => `(none)
    | some (offset, width) =>
        `(some { offset := $(natTerm offset), width := $(natTerm width) })
  let memberTypeTerm ←
    match member.ty with
    | .uint256 | .int256 | .uint8 =>
        `(Compiler.CompilationModel.StructMemberType.uint256)
    | .uint16 =>
        `(Compiler.CompilationModel.StructMemberType.uint16)
    | .address =>
        `(Compiler.CompilationModel.StructMemberType.address)
    | .bool =>
        `(Compiler.CompilationModel.StructMemberType.bool)
    | .bytes32 =>
        `(Compiler.CompilationModel.StructMemberType.bytes32)
    | _ =>
        throwError "mapping struct member '{member.name}' has unsupported type {repr member.ty}; expected Uint256, Uint16, Address, Bool, or Bytes32"
  `(Compiler.CompilationModel.StructMember.mk
      $(strTerm member.name)
      $memberTypeTerm
      $(natTerm member.wordOffset)
      $packedTerm)

private def modelFieldTypeTerm (ty : StorageType) : CommandElabM Term :=
  match ty with
  | .scalar .uint256 => `(Compiler.CompilationModel.FieldType.uint256)
  | .scalar .int256 => `(Compiler.CompilationModel.FieldType.uint256)
  | .scalar .uint8 => throwError "storage fields cannot be Uint8; use Uint256 encoding"
  | .scalar .uint16 => throwError "storage fields cannot be Uint16; use Uint256 encoding"
  | .scalar .address => `(Compiler.CompilationModel.FieldType.address)
  | .scalar .bytes32 => throwError "storage fields cannot be Bytes32; use Uint256 encoding"
  | .scalar .bool => throwError "storage fields cannot be Bool; use Uint256 (0/1) encoding"
  | .scalar .string => throwError "storage fields cannot be String; use Uint256 encoding"
  | .scalar .bytes => throwError "storage fields cannot be Bytes; use Uint256 encoding"
  | .scalar (.array _) => throwError "storage fields cannot be Array; use mapping encodings"
  | .scalar (.fixedArray _ _) => throwError "storage fields cannot be FixedArray; use mapping encodings"
  | .scalar (.tuple _) => throwError "storage fields cannot be Tuple; use mapping encodings"
  | .scalar (.struct _ _) =>
      throwError
        "top-level named struct storage fields are not supported yet (#1758); flatten the struct into explicit scalar storage fields with fixed slots, or use MappingStruct/MappingStruct2 for struct-valued mappings"
  | .scalar .unit => throwError "storage fields cannot be Unit"
  | .scalar (.newtype _ baseType) => modelFieldTypeTerm (.scalar baseType)  -- Erased to base type
  | .scalar (.adt name maxFields) =>
      `(Compiler.CompilationModel.FieldType.adt $(Lean.quote name) $(Lean.quote maxFields))
  | .dynamicArray .uint256 => `(Compiler.CompilationModel.FieldType.dynamicArray Compiler.CompilationModel.StorageArrayElemType.uint256)
  | .dynamicArray .address => `(Compiler.CompilationModel.FieldType.dynamicArray Compiler.CompilationModel.StorageArrayElemType.address)
  | .dynamicArray .bool => `(Compiler.CompilationModel.FieldType.dynamicArray Compiler.CompilationModel.StorageArrayElemType.bool)
  | .dynamicArray .uint8 => `(Compiler.CompilationModel.FieldType.dynamicArray Compiler.CompilationModel.StorageArrayElemType.uint8)
  | .dynamicArray .bytes32 => `(Compiler.CompilationModel.FieldType.dynamicArray Compiler.CompilationModel.StorageArrayElemType.bytes32)
  | .mappingAddressToUint256 =>
      `(Compiler.CompilationModel.FieldType.mappingTyped
          (Compiler.CompilationModel.MappingType.simple Compiler.CompilationModel.MappingKeyType.address))
  | .mapping2AddressToAddressToUint256 =>
      `(Compiler.CompilationModel.FieldType.mappingTyped
          (Compiler.CompilationModel.MappingType.nested
            Compiler.CompilationModel.MappingKeyType.address
            Compiler.CompilationModel.MappingKeyType.address))
  | .mappingUintToUint256 =>
      `(Compiler.CompilationModel.FieldType.mappingTyped
          (Compiler.CompilationModel.MappingType.simple Compiler.CompilationModel.MappingKeyType.uint256))
  | .mappingChain keyTypes => do
      let keyTypeTerms := (← keyTypes.mapM modelMappingKeyTypeTerm).toArray
      `(Compiler.CompilationModel.FieldType.mappingTyped
          (Compiler.CompilationModel.MappingType.chain [ $[$keyTypeTerms],* ]))
  | .mappingStruct keyType members => do
      let keyTypeTerm ← modelMappingKeyTypeTerm keyType
      let memberTerms := (← members.mapM modelStructMemberTerm).toArray
      `(Compiler.CompilationModel.FieldType.mappingStruct $keyTypeTerm [ $[$memberTerms],* ])
  | .mappingStruct2 outerKey innerKey members => do
      let outerKeyTerm ← modelMappingKeyTypeTerm outerKey
      let innerKeyTerm ← modelMappingKeyTypeTerm innerKey
      let memberTerms := (← members.mapM modelStructMemberTerm).toArray
      `(Compiler.CompilationModel.FieldType.mappingStruct2 $outerKeyTerm $innerKeyTerm [ $[$memberTerms],* ])

private partial def modelParamTypeTerm (ty : ValueType) : CommandElabM Term :=
  match ty with
  | .uint256 => `(Compiler.CompilationModel.ParamType.uint256)
  | .int256 => `(Compiler.CompilationModel.ParamType.int256)
  | .uint8 => `(Compiler.CompilationModel.ParamType.uint8)
  | .uint16 => `(Compiler.CompilationModel.ParamType.uint16)
  | .address => `(Compiler.CompilationModel.ParamType.address)
  | .bytes32 => `(Compiler.CompilationModel.ParamType.bytes32)
  | .bool => `(Compiler.CompilationModel.ParamType.bool)
  | .string => `(Compiler.CompilationModel.ParamType.string)
  | .bytes => `(Compiler.CompilationModel.ParamType.bytes)
  | .array elemTy => do
      `(Compiler.CompilationModel.ParamType.array $(← modelParamTypeTerm elemTy))
  | .fixedArray elemTy size => do
      `(Compiler.CompilationModel.ParamType.fixedArray $(← modelParamTypeTerm elemTy) $(natTerm size))
  | .tuple elemTys => do
      let elemTerms ← elemTys.mapM modelParamTypeTerm
      `(Compiler.CompilationModel.ParamType.tuple [ $[$elemTerms.toArray],* ])
  | .struct _ fields => do
      let elemTerms ← fields.mapM (fun field => modelParamTypeTerm field.snd)
      `(Compiler.CompilationModel.ParamType.tuple [ $[$elemTerms.toArray],* ])
  | .unit => throwError "function parameters cannot be Unit"
  | .newtype name baseType => do
      let baseTerm ← modelParamTypeTerm baseType
      `(Compiler.CompilationModel.ParamType.newtypeOf $(Lean.quote name) $baseTerm)
  | .adt name maxFields => do
      `(Compiler.CompilationModel.ParamType.adt $(Lean.quote name) $(Lean.quote maxFields))

private partial def valueTypeToSolidityString : ValueType → String
  | .uint256 => "uint256"
  | .int256 => "int256"
  | .uint8 => "uint8"
  | .uint16 => "uint16"
  | .address => "address"
  | .bytes32 => "bytes32"
  | .bool => "bool"
  | .string => "string"
  | .bytes => "bytes"
  | .array elemTy => valueTypeToSolidityString elemTy ++ "[]"
  | .fixedArray elemTy size => valueTypeToSolidityString elemTy ++ "[" ++ toString size ++ "]"
  | .tuple elemTys =>
      "(" ++ String.intercalate "," (elemTys.map valueTypeToSolidityString) ++ ")"
  | .struct _ fields =>
      "(" ++ String.intercalate "," (fields.map (fun field => valueTypeToSolidityString field.snd)) ++ ")"
  | .newtype _ baseType => valueTypeToSolidityString baseType
  | .adt name _ => name
  | .unit => "()"

private def interfaceFunctionSignature (methodName : String) (params : Array ValueType) : String :=
  methodName ++ "(" ++
    String.intercalate "," (params.toList.map valueTypeToSolidityString) ++
    ")"

/-- A value type that ABI-encodes to exactly one 32-byte word. The no-return ECM
    (`Compiler.Modules.Calls.noReturnModule`) lays calldata out as
    `selector ++ numArgs*32`, one word per argument, so it is only sound for
    arguments of these types. Dynamic (`bytes`/`string`/`array`) or composite
    (`tuple`/`struct`/`fixedArray`/`adt`) params need head/tail ABI framing and
    are rejected at the call site rather than silently mis-encoded.
    `newtype` erases to its base, so it is single-word iff its base is. -/
private def isStaticSingleWordValueType : ValueType → Bool
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => true
  | .newtype _ baseType => isStaticSingleWordValueType baseType
  | _ => false

private def modelReturnTypeTerm (ty : ValueType) : CommandElabM Term :=
  match ty with
  | .unit => `(none)
  | .uint256 => `(some Compiler.CompilationModel.FieldType.uint256)
  | .int256 => `(none)
  | .uint8 => `(none)
  | .uint16 => `(none)
  | .address => `(some Compiler.CompilationModel.FieldType.address)
  | .bytes32 => `(none)
  | .bool => `(none)
  | .string => `(none)
  | .bytes => `(none)
  | .array _ => `(none)
  | .fixedArray _ _ => `(none)
  | .tuple _ => `(none)
  | .struct _ _ => `(none)
  | .newtype _ baseType => modelReturnTypeTerm baseType
  | .adt _ _ => `(none)  -- ADTs are not directly returnable as single FieldType

private partial def modelReturnsTerm (ty : ValueType) : CommandElabM Term :=
  match ty with
  | .unit => `([])
  | .uint256 => `([Compiler.CompilationModel.ParamType.uint256])
  | .int256 => `([Compiler.CompilationModel.ParamType.int256])
  | .uint8 => `([Compiler.CompilationModel.ParamType.uint8])
  | .uint16 => `([Compiler.CompilationModel.ParamType.uint16])
  | .address => `([Compiler.CompilationModel.ParamType.address])
  | .bytes32 => `([Compiler.CompilationModel.ParamType.bytes32])
  | .bool => `([Compiler.CompilationModel.ParamType.bool])
  | .string => `([Compiler.CompilationModel.ParamType.string])
  | .bytes => `([Compiler.CompilationModel.ParamType.bytes])
  | .array elemTy => do
      `([Compiler.CompilationModel.ParamType.array $(← modelParamTypeTerm elemTy)])
  | .fixedArray elemTy size => do
      `([Compiler.CompilationModel.ParamType.fixedArray $(← modelParamTypeTerm elemTy) $(natTerm size)])
  | .tuple elemTys => do
      let elemTerms ← elemTys.mapM modelParamTypeTerm
      `([ $[$elemTerms.toArray],* ])
  | .struct _ fields => do
      let elemTerms ← fields.mapM (fun field => modelParamTypeTerm field.snd)
      `([ $[$elemTerms.toArray],* ])
  | .newtype name baseType => do
      let baseTerm ← modelParamTypeTerm baseType
      `([Compiler.CompilationModel.ParamType.newtypeOf $(Lean.quote name) $baseTerm])
  | .adt name maxFields => do
      `([Compiler.CompilationModel.ParamType.adt $(Lean.quote name) $(Lean.quote maxFields)])

private partial def valueTypeFromModelParamType? : Compiler.CompilationModel.ParamType → Option ValueType
  | .uint256 => some .uint256
  | .int256 => some .int256
  | .uint8 => some .uint8
  | .uint16 => some .uint16
  | .address => some .address
  | .bool => some .bool
  | .bytes32 => some .bytes32
  | .string => some .string
  | .bytes => some .bytes
  | .array elemTy => do
      let elem ← valueTypeFromModelParamType? elemTy
      some (.array elem)
  | .fixedArray elemTy size => do
      let elem ← valueTypeFromModelParamType? elemTy
      some (.fixedArray elem size)
  | .tuple elemTys => do
      let elems ← elemTys.mapM valueTypeFromModelParamType?
      some (.tuple elems)
  | .newtypeOf name baseType => do
      let base ← valueTypeFromModelParamType? baseType
      some (.newtype name base)
  | .adt name maxFields => some (.adt name maxFields)

private unsafe def evalQualifiedFunctionSpec
    (fnName : Name) : CommandElabM Compiler.CompilationModel.FunctionSpec := do
  let modelTerm : Term := ⟨(mkIdent (qualifiedFunctionModelName fnName)).raw⟩
  liftTermElabM do
    let expectedType := mkConst ``Compiler.CompilationModel.FunctionSpec
    let expr ← Lean.Elab.Term.elabTermEnsuringType modelTerm expectedType
    Lean.Meta.evalExpr Compiler.CompilationModel.FunctionSpec expectedType expr .unsafe

private unsafe def qualifiedFunctionReturnTypes
    (stx : Syntax)
    (fnName : Name) : CommandElabM (Array ValueType) := do
  let spec ←
    try
      unsafe evalQualifiedFunctionSpec fnName
    catch _ =>
      throwErrorAt stx
        s!"unable to inspect qualified helper '{qualifiedFunctionDisplayName fnName}'; expected generated model '{qualifiedFunctionModelName fnName}' to be in scope"
  let mut result := #[]
  for returnTy in spec.returns do
    match valueTypeFromModelParamType? returnTy with
    | some ty => result := result.push ty
    | none =>
        throwErrorAt stx
          s!"qualified helper '{qualifiedFunctionDisplayName fnName}' returns unsupported value type {repr returnTy}"
  pure result

mutual
private partial def mkTupleContractType (elemTys : List ValueType) : CommandElabM Term := do
  let rec go : List ValueType → CommandElabM Term
    | [] => throwError "tuple types must have at least 2 elements"
    | [ty] => contractValueTypeTerm ty
    | ty :: rest => do
        `(($(← contractValueTypeTerm ty) × $(← go rest)))
  go elemTys

private partial def contractValueTypeTerm (ty : ValueType) : CommandElabM Term :=
  match ty with
  | .uint256 => `(Uint256)
  | .int256 => `(Int256)
  | .uint8 => `(Uint256)
  | .uint16 => `(Uint16)
  | .address => `(Address)
  | .bytes32 => `(Bytes32)
  | .bool => `(Bool)
  | .string => `(String)
  | .bytes => `(ByteArray)
  | .array elemTy => do
      `(Array $(← contractValueTypeTerm elemTy))
  | .fixedArray elemTy _size => do
      `(Array $(← contractValueTypeTerm elemTy))
  | .tuple elemTys => mkTupleContractType elemTys
  | .unit => `(Unit)
  | .newtype _ baseType => contractValueTypeTerm baseType  -- Erased to base type at contract level
  | .struct name _ => pure (mkIdent (Name.mkSimple name))
  | .adt _ _ => `(Uint256)  -- ADTs represented as tag value at contract level
end

private def parseStorageField (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl := #[]) (adtDecls : Array AdtDecl := #[]) (stx : Syntax) : CommandElabM StorageFieldDecl := do
  match stx with
  | `(verityStorageField| $name:ident : $ty:term := slot $slotNum:num) =>
      let parsedTy ← storageTypeFromSyntax newtypes structDecls adtDecls ty
      let adtInfo? :=
        match parsedTy with
        | .scalar (.adt adtName maxFields) => some (adtName, maxFields)
        | _ => none
      pure {
        ident := name
        name := toString name.getId
        ty := parsedTy
        slotNum := ← natFromSyntax slotNum
        adtInfo? := adtInfo?
      }
  | _ => throwErrorAt stx "invalid storage field declaration"

private def pathFieldName (parts : List String) : String :=
  String.intercalate "." parts

private def pathFieldModelName (parts : List String) : String :=
  String.intercalate "_" parts

private def pathFieldIdent (root : Ident) (parts : List String) : Ident :=
  mkIdentFrom root (Name.mkSimple (pathFieldModelName parts))

private partial def parseStorageStructMember
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl := #[])
    (adtDecls : Array AdtDecl := #[])
    (rootIdent : Ident)
    (pathPrefix : List String)
    (baseSlot : Nat)
    (stx : TSyntax `verityStorageStructMember) :
    CommandElabM (Array StorageFieldDecl × StorageAccessorTree) := do
  let leafDecl (name : Ident) (ty : Term) (wordOffset : TSyntax `num)
      (packed : Option (TSyntax `num × TSyntax `num)) := do
    let localName := toString name.getId
    let parts := pathPrefix ++ [localName]
    let parsedTy ← storageTypeFromSyntax newtypes structDecls adtDecls ty
    let packedBits ←
      match packed with
      | none => pure none
      | some (offset, width) => pure (some (← natFromSyntax offset, ← natFromSyntax width))
    let slotNum := baseSlot + (← natFromSyntax wordOffset)
    let field : StorageFieldDecl := {
      ident := pathFieldIdent rootIdent parts
      name := pathFieldModelName parts
      ty := parsedTy
      slotNum := slotNum
      adtInfo? :=
        match parsedTy with
        | .scalar (.adt adtName maxFields) => some (adtName, maxFields)
        | _ => none
      packedBits := packedBits
      emitDef := false
      aliases := [pathFieldName parts]
    }
    pure (#[field], StorageAccessorTree.leaf localName parsedTy slotNum)
  match stx with
  | `(verityStorageStructMember| $name:ident : $ty:term @word $wordOffset:num) =>
      leafDecl name ty wordOffset none
  | `(verityStorageStructMember| $name:ident : $ty:term @word $wordOffset:num packed($offset:num,$width:num)) =>
      leafDecl name ty wordOffset (some (offset, width))
  | `(verityStorageStructMember| $name:ident : StorageStruct [ $[$members:verityStorageStructMember],* ] @word $wordOffset:num) =>
      let localName := toString name.getId
      let childBase := baseSlot + (← natFromSyntax wordOffset)
      let childPrefix := pathPrefix ++ [localName]
      let mut fields : Array StorageFieldDecl := #[]
      let mut children : Array StorageAccessorTree := #[]
      for member in members do
        let (memberFields, memberTree) ← parseStorageStructMember newtypes structDecls adtDecls rootIdent childPrefix childBase member
        fields := fields ++ memberFields
        children := children.push memberTree
      pure (fields, StorageAccessorTree.node localName children)
  | _ => throwErrorAt stx "invalid storage struct member declaration"

private def parseStorageStructItem
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl := #[])
    (adtDecls : Array AdtDecl := #[])
    (stx : TSyntax `verityStorageItem) :
    CommandElabM (Option (Array StorageFieldDecl × StorageStructAccessorDecl)) := do
  match stx with
  | `(verityStorageItem| $name:ident : StorageStruct [ $[$members:verityStorageStructMember],* ] := slot $slotNum:num) =>
      let rootName := toString name.getId
      let baseSlot ← natFromSyntax slotNum
      let mut fields : Array StorageFieldDecl := #[]
      let mut children : Array StorageAccessorTree := #[]
      for member in members do
        let (memberFields, memberTree) ← parseStorageStructMember newtypes structDecls adtDecls name [rootName] baseSlot member
        fields := fields ++ memberFields
        children := children.push memberTree
      pure (some (fields, {
        ident := name
        name := rootName
        tree := StorageAccessorTree.node rootName children
      }))
  | _ => pure none

private def parseParam (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl) (adtDecls : Array AdtDecl) (stx : Syntax) : CommandElabM ParamDecl := do
  match stx with
  | `(verityParam| $name:ident : $ty:term) =>
      pure {
        ident := name
        name := toString name.getId
        ty := ← valueTypeFromSyntax newtypes structDecls adtDecls ty
      }
  | _ => throwErrorAt stx "invalid parameter declaration"

private def localDeclName (name : String) : String :=
  (name.splitOn ".").getLastD name

/-- Like `parseParam`, but additionally recognizes a higher-order
    function-pointer parameter type (e.g. `op : (Uint256) -> Uint256`) and
    records it as `ParamDecl.funcPtr?` instead of rejecting it (#1747).  Used
    only for regular `function` helpers; struct/ADT/event/constructor parameters
    still go through `parseParam`, which rejects function types at the boundary. -/
private def parseFunctionParam (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl) (adtDecls : Array AdtDecl) (stx : Syntax) : CommandElabM ParamDecl := do
  match stx with
  | `(verityParam| $name:ident : $ty:term) => do
      let (arrowArgs, arrowResult) ← collectArrowChainTypes (stripParens ty)
      if arrowArgs.isEmpty then
        pure {
          ident := name
          name := toString name.getId
          ty := ← valueTypeFromSyntax newtypes structDecls adtDecls ty
        }
      else
        -- Higher-order function-pointer parameter; validate the component types
        -- for diagnostics, then mark it for the monomorphization pre-pass.
        let paramTys ← arrowArgs.mapM (fun a =>
          valueTypeFromSyntax newtypes structDecls adtDecls (stripParens a))
        let returnTy ← valueTypeFromSyntax newtypes structDecls adtDecls arrowResult
        pure {
          ident := name
          name := toString name.getId
          ty := .unit
          funcPtr? := some { paramTys := paramTys.toArray, returnTy := returnTy }
        }
  | _ => throwErrorAt stx "invalid parameter declaration"

private def parseFunctionParamWithInterfaces
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (interfaceNames : Array String)
    (stx : Syntax) : CommandElabM ParamDecl := do
  match stx with
  | `(verityParam| $name:ident : $ty:term) =>
      match stripParens ty with
      | `(term| $interfaceIdent:ident) =>
          let interfaceName := toString interfaceIdent.getId
          if interfaceNames.contains interfaceName then
            let typeLocalNames :=
              (newtypes.map (fun decl => localDeclName decl.name)) ++
              (structDecls.map (fun decl => localDeclName decl.name)) ++
              (adtDecls.map (fun decl => localDeclName decl.name))
            if typeLocalNames.contains (localDeclName interfaceName) then
              throwErrorAt interfaceIdent
                s!"interface name '{localDeclName interfaceName}' conflicts with an existing type name"
            pure {
              ident := name
              name := toString name.getId
              ty := .address
              interfaceName? := some interfaceName
            }
          else
            parseFunctionParam newtypes structDecls adtDecls stx
      | _ => parseFunctionParam newtypes structDecls adtDecls stx
  | _ => throwErrorAt stx "invalid parameter declaration"

private def parseInterfaceParam
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (stx : Syntax) : CommandElabM ParamDecl := do
  match stx with
  | `(verityInterfaceParam| $name:ident : $ty:term) => do
      let parsedTy ← valueTypeFromSyntax newtypes structDecls adtDecls ty
      pure {
        ident := name
        name := toString name.getId
        ty := parsedTy
      }
  | _ => throwErrorAt stx "invalid interface parameter declaration"

private def parseNewtype (stx : Syntax) : CommandElabM NewtypeDecl := do
  match stx with
  | `(verityNewtype| $name:ident : $ty:term) =>
      let baseType ← valueTypeFromSyntax #[] #[] #[] ty
      -- Validate: newtypes must be based on scalar types (not arrays, tuples, or unit)
      match baseType with
      | .array _ => throwErrorAt ty "newtype base type must be a scalar type, not an array"
      | .tuple _ => throwErrorAt ty "newtype base type must be a scalar type, not a tuple"
      | .unit => throwErrorAt ty "newtype base type must be a scalar type, not Unit"
      | _ => pure ()
      pure {
        ident := name
        name := toString name.getId
        baseType := baseType
      }
  | _ => throwErrorAt stx "invalid type declaration"

private def parseStructDecl (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl) (stx : Syntax) : CommandElabM StructDecl := do
  match stx with
  | `(verityStructDecl| struct $name:ident where $[$fields:verityParam],*) =>
      let parsedFields ← fields.mapM (parseParam newtypes structDecls #[])
      if parsedFields.isEmpty then
        throwErrorAt name s!"struct '{toString name.getId}' must have at least one field"
      let mut seenFieldNames : Array String := #[]
      for field in parsedFields do
        if seenFieldNames.contains field.name then
          throwErrorAt field.ident s!"duplicate field '{field.name}' in struct '{toString name.getId}'"
        seenFieldNames := seenFieldNames.push field.name
      pure { ident := name, name := toString name.getId, fields := parsedFields }
  | _ => throwErrorAt stx "invalid struct declaration"

/-- Parse a single ADT variant: `| Name(field1 : Type1, field2 : Type2)` or `| Name`.
    (#1727, Axis 1 Step 5a) -/
private def parseAdtVariant (newtypes : Array NewtypeDecl) (stx : Syntax) : CommandElabM AdtVariantDecl := do
  match stx with
  | `(verityAdtVariant| | $name:ident ($[$params:verityParam],*)) =>
      let parsedParams ← params.mapM (parseParam newtypes #[] #[])
      pure { ident := name, name := toString name.getId, fields := parsedParams }
  | `(verityAdtVariant| | $name:ident) =>
      pure { ident := name, name := toString name.getId, fields := #[] }
  | _ => throwErrorAt stx "invalid ADT variant declaration"

/-- Parse a full ADT declaration: `Name := | Variant1(...) | Variant2(...)`.
    (#1727, Axis 1 Step 5a) -/
private def parseAdtDecl (newtypes : Array NewtypeDecl) (stx : Syntax) : CommandElabM AdtDecl := do
  match stx with
  | `(verityAdtDecl| $name:ident := $[$variants:verityAdtVariant]*) =>
      let parsedVariants ← variants.mapM (parseAdtVariant newtypes)
      if parsedVariants.isEmpty then
        throwErrorAt name s!"ADT '{toString name.getId}' must have at least one variant"
      if parsedVariants.size > 256 then
        throwErrorAt name
          s!"ADT '{toString name.getId}' has {parsedVariants.size} variants, but ADT tags are encoded as Uint8 and support at most 256 variants"
      -- Validate: no duplicate variant names within this ADT
      let mut seenVariantNames : Array String := #[]
      for v in parsedVariants do
        if seenVariantNames.contains v.name then
          throwErrorAt v.ident s!"duplicate variant name '{v.name}' in ADT '{toString name.getId}'"
        seenVariantNames := seenVariantNames.push v.name
      pure { ident := name, name := toString name.getId, variants := parsedVariants }
  | _ => throwErrorAt stx "invalid ADT declaration"

private def parseError (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl) (adtDecls : Array AdtDecl) (stx : Syntax) : CommandElabM ErrorDecl := do
  match stx with
  | `(verityError| error $name:ident ($[$params:term],*)) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
      }
  | _ => throwErrorAt stx "invalid custom error declaration"

private def parseEventParam
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (stx : Syntax) : CommandElabM EventParamDecl := do
  match stx with
  | `(verityEventParam| $name:ident : $ty:term) =>
      pure {
        ident := name
        name := toString name.getId
        ty := ← valueTypeFromSyntax newtypes structDecls adtDecls ty
        isIndexed := false
      }
  | `(verityEventParam| @indexed $name:ident : $ty:term) =>
      pure {
        ident := name
        name := toString name.getId
        ty := ← valueTypeFromSyntax newtypes structDecls adtDecls ty
        isIndexed := true
      }
  | _ => throwErrorAt stx "invalid event parameter declaration"

private def parseEvent
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (stx : Syntax) : CommandElabM EventDecl := do
  match stx with
  | `(verityEvent| event $name:ident ($[$params:verityEventParam],*)) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (parseEventParam newtypes structDecls adtDecls)
      }
  | _ => throwErrorAt stx "invalid event declaration"

private def parseConstant (newtypes : Array NewtypeDecl) (stx : Syntax) : CommandElabM ConstantDecl := do
  match stx with
  | `(verityConstant| $name:ident : $ty:term := $body:term) =>
      pure {
        ident := name
        name := toString name.getId
        ty := ← valueTypeFromSyntax newtypes #[] #[] ty
        body := body
      }
  | _ => throwErrorAt stx "invalid constant declaration"

private def parseImmutable (newtypes : Array NewtypeDecl) (stx : Syntax) : CommandElabM ImmutableDecl := do
  match stx with
  | `(verityImmutable| $name:ident : $ty:term := $body:term) =>
      pure {
        ident := name
        name := toString name.getId
        ty := ← valueTypeFromSyntax newtypes #[] #[] ty
        body := body
      }
  | _ => throwErrorAt stx "invalid immutable declaration"

private def parseExternalLinkMode (stx : Syntax) : CommandElabM Compiler.CompilationModel.ForeignLinkMode := do
  match stx with
  | `(verityExternalLinkMode| external) => pure .external
  | `(verityExternalLinkMode| internal_yul) => pure .objectLinked
  | `(verityExternalLinkMode| object_linked) => pure .objectLinked
  | `(verityExternalLinkMode| inline) => pure .inline
  | `(verityExternalLinkMode| compiler_runtime) => pure .compilerRuntime
  | _ =>
      throwErrorAt stx
        "unsupported linked_as mode; expected external, internal_yul, object_linked, inline, or compiler_runtime"

private def parseExternal (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl) (adtDecls : Array AdtDecl) (stx : Syntax) : CommandElabM ExternalDecl := do
  match stx with
  | `(verityExternal| external $name:ident ($[$params:term],*) -> ($[$returnTys:term],*) linked_as := $mode:verityExternalLinkMode) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
        returnTys := ← returnTys.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
        linkMode := ← parseExternalLinkMode mode
      }
  | `(verityExternal| external $name:ident ($[$params:term],*) linked_as := $mode:verityExternalLinkMode) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
        returnTys := #[]
        linkMode := ← parseExternalLinkMode mode
      }
  | `(verityExternal| external $name:ident ($[$params:term],*) -> ($[$returnTys:term],*)) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
        returnTys := ← returnTys.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
      }
  | `(verityExternal| external $name:ident ($[$params:term],*)) =>
      pure {
        ident := name
        name := toString name.getId
        params := ← params.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
        returnTys := #[]
      }
  | _ => throwErrorAt stx "invalid external declaration"

private def parseInterfaceFunction
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (stx : Syntax) : CommandElabM InterfaceFunctionDecl := do
  let expectReturnsMarker (marker : Ident) : CommandElabM Unit := do
    unless toString marker.getId == "returns" do
      throwErrorAt marker "expected 'returns'"
  -- `returnTys?` is `none` for a void interface method (no returns clause).
  let parseParts (name : Ident) (params : Array ParamDecl)
      (mods : TSyntaxArray `verityMutability) (returnTys? : Option (TSyntaxArray `term)) := do
    let mut isView := false
    for mod in mods do
      match mod with
      | `(verityMutability| view) =>
          if isView then
            throwErrorAt mod "duplicate 'view' modifier"
          isView := true
      | _ => throwErrorAt mod "interface functions currently support only the optional `view` modifier"
    let parsedReturns ←
      match returnTys? with
      | none => pure #[]
      | some returnTys => do
          let parsed ← returnTys.mapM (valueTypeFromSyntax newtypes structDecls adtDecls)
          if parsed.isEmpty then
            throwErrorAt stx "interface function returns clause must contain at least one return type"
          if parsed.size > 1 then
            throwErrorAt stx "typed interface calls currently support exactly one return value"
          pure parsed
    pure {
      ident := name
      name := toString name.getId
      params := params
      returnTys := parsedReturns
      isView := isView
    }
  -- positional (`(term, ...)`) params lower to synthetic arg0/arg1/... names.
  let parsePositionalParams (paramTys : TSyntaxArray `term) : CommandElabM (Array ParamDecl) :=
    paramTys.mapIdxM fun idx ty => do
      pure {
        ident := mkIdent (Name.mkSimple s!"arg{idx}")
        name := s!"arg{idx}"
        ty := ← valueTypeFromSyntax newtypes structDecls adtDecls ty
      }
  match stx with
  | `(verityInterfaceFunction| function $name:ident ($[$params:verityInterfaceParam],*) $[$mods:verityMutability]* $returnsMarker:ident ($[$returnTys:term],*)) => do
      expectReturnsMarker returnsMarker
      parseParts name (← params.mapM (parseInterfaceParam newtypes structDecls adtDecls)) mods (some returnTys)
  | `(verityInterfaceFunction| function $name:ident ($[$paramTys:term],*) $[$mods:verityMutability]* $returnsMarker:ident ($[$returnTys:term],*)) => do
      expectReturnsMarker returnsMarker
      parseParts name (← parsePositionalParams paramTys) mods (some returnTys)
  -- void forms: no returns clause
  | `(verityInterfaceFunction| function $name:ident ($[$params:verityInterfaceParam],*) $[$mods:verityMutability]*) => do
      parseParts name (← params.mapM (parseInterfaceParam newtypes structDecls adtDecls)) mods none
  | `(verityInterfaceFunction| function $name:ident ($[$paramTys:term],*) $[$mods:verityMutability]*) => do
      parseParts name (← parsePositionalParams paramTys) mods none
  | _ => throwErrorAt stx "invalid interface function declaration"

private def parseInterface
    (newtypes : Array NewtypeDecl)
    (structDecls : Array StructDecl)
    (adtDecls : Array AdtDecl)
    (stx : Syntax) : CommandElabM InterfaceDecl := do
  match stx with
  | `(verityInterface| interface $name:ident where $[$fns:verityInterfaceFunction]* end) =>
      let parsedFns ← fns.mapM (parseInterfaceFunction newtypes structDecls adtDecls)
      if parsedFns.isEmpty then
        throwErrorAt name s!"interface '{toString name.getId}' must declare at least one function"
      pure { ident := name, name := toString name.getId, functions := parsedFns }
  | _ => throwErrorAt stx "invalid interface declaration"

private def interfaceExternalName (interfaceName methodName : String) : String :=
  s!"{interfaceName}.{methodName}"

private def interfaceExternals (ifaces : Array InterfaceDecl) : Array ExternalDecl :=
  ifaces.foldl
    (fun acc iface =>
      acc ++ iface.functions.map (fun fn => {
        ident := fn.ident
        name := interfaceExternalName iface.name fn.name
        params := fn.params.map (·.ty)
        returnTys := fn.returnTys
        linkMode := Compiler.CompilationModel.ForeignLinkMode.external
        interfaceName? := some iface.name
        isView := fn.isView
      }))
    #[]

private def parseProofStatusIdent (stx : Syntax) : CommandElabM Compiler.ProofStatus := do
  match stx with
  | .ident _ raw _ _ =>
      match raw.toString with
      | "proved" => pure .proved
      | "assumed" => pure .assumed
      | "unchecked" => pure .unchecked
      | other =>
          throwErrorAt stx s!"unsupported proof status '{other}'; expected proved, assumed, or unchecked"
  | _ => throwErrorAt stx "expected proof status identifier"

private def parseLocalObligation (stx : Syntax) : CommandElabM LocalObligationDecl := do
  match stx with
  | `(verityLocalObligation| $name:ident := $status:ident $obligation:str) =>
      pure {
        ident := name
        name := toString name.getId
        obligation := obligation.getString
        proofStatus := ← parseProofStatusIdent status
      }
  | _ => throwErrorAt stx "invalid local obligation declaration"

private structure ParsedMutability where
  isPayable : Bool := false
  isView : Bool := false
  isPure : Bool := false
  isInternal : Bool := false
  noExternalCalls : Bool := false
  allowPostInteractionWrites : Bool := false
  nonReentrantLock : Option Ident := none
  ceiSafe : Bool := false

private def parseMutabilityModifiers
    (mods : Array (TSyntax `verityMutability))
    (stx : Syntax) : CommandElabM ParsedMutability := do
  let mut result : ParsedMutability := {}
  for mod in mods do
    match mod with
    | `(verityMutability| payable) =>
        if result.isPayable then
          throwErrorAt mod "duplicate 'payable' modifier"
        result := { result with isPayable := true }
    | `(verityMutability| view) =>
        if result.isView then
          throwErrorAt mod "duplicate 'view' modifier"
        result := { result with isView := true }
    | `(verityMutability| internal) =>
        if result.isInternal then
          throwErrorAt mod "duplicate 'internal' modifier"
        result := { result with isInternal := true }
    | `(verityMutability| no_external_calls) =>
        if result.noExternalCalls then
          throwErrorAt mod "duplicate 'no_external_calls' modifier"
        result := { result with noExternalCalls := true }
    | `(verityMutability| allow_post_interaction_writes) =>
        if result.allowPostInteractionWrites then
          throwErrorAt mod "duplicate 'allow_post_interaction_writes' modifier"
        result := { result with allowPostInteractionWrites := true }
    | `(verityMutability| nonreentrant($field:ident)) =>
        if result.nonReentrantLock.isSome then
          throwErrorAt mod "duplicate 'nonreentrant' modifier"
        result := { result with nonReentrantLock := some field }
    | `(verityMutability| cei_safe) =>
        if result.ceiSafe then
          throwErrorAt mod "duplicate 'cei_safe' modifier"
        result := { result with ceiSafe := true }
    | _ => throwErrorAt stx "invalid function mutability modifier"
  pure result

private def parseModifies (stx : TSyntax `verityModifies) : CommandElabM (Array Ident) := do
  match stx with
  | `(verityModifies| modifies($[$fields:ident],*)) =>
      let result := fields
      -- Check for duplicates
      let mut seen : Array String := #[]
      for f in result do
        let s := toString f.getId
        if seen.contains s then
          throwErrorAt f s!"duplicate field '{s}' in modifies annotation"
        seen := seen.push s
      pure result
  | _ => throwErrorAt stx "invalid modifies annotation"

private def parseInitGuard (stx : TSyntax `verityInitGuard) : CommandElabM InitGuardDecl := do
  match stx with
  | `(verityInitGuard| initializer($field:ident)) =>
      pure (.initializer field (toString field.getId))
  | `(verityInitGuard| reinitializer($field:ident, $version:num)) => do
      let versionNat ← natFromSyntax version
      if versionNat == 0 then
        throwErrorAt version "reinitializer version must be greater than 0"
      pure (.reinitializer field (toString field.getId) versionNat)
  | _ => throwErrorAt stx "invalid initializer guard"

private def parseLocalObligations
    (stx : TSyntax `verityLocalObligations) : CommandElabM (Array LocalObligationDecl) := do
  match stx with
  | `(verityLocalObligations| local_obligations [ $[$obligations:verityLocalObligation],* ]) =>
      obligations.mapM parseLocalObligation
  | _ => throwErrorAt stx "invalid local obligations declaration"

private def hiddenEntrypointIdent (name : String) : Ident :=
  mkIdent (Name.mkSimple s!"__verity_{name}")

private def modifierInternalName (name : String) : String :=
  s!"__modifier_{name}"

private def parseSpecialEntrypoint (stx : Syntax) : CommandElabM FunctionDecl := do
  match stx with
  | `(veritySpecialEntrypoint| receive $[$localObligations?:verityLocalObligations]? := $body:term) => do
      let parsedLocalObligations ←
        match localObligations? with
        | some obligations => parseLocalObligations obligations
        | none => pure #[]
      pure {
        ident := hiddenEntrypointIdent "receive"
        name := "receive"
        params := #[]
        returnTy := .unit
        isPayable := true
        localObligations := parsedLocalObligations
        body := body
      }
  | `(veritySpecialEntrypoint| fallback $[$localObligations?:verityLocalObligations]? := $body:term) => do
      let parsedLocalObligations ←
        match localObligations? with
        | some obligations => parseLocalObligations obligations
        | none => pure #[]
      pure {
        ident := hiddenEntrypointIdent "fallback"
        name := "fallback"
        params := #[]
        returnTy := .unit
        localObligations := parsedLocalObligations
        body := body
      }
  | _ => throwErrorAt stx "invalid special entrypoint declaration"

private def parseModifier (stx : Syntax) : CommandElabM ModifierDecl := do
  match stx with
  | `(verityModifier| modifier $name:ident := $body:term) =>
      pure { ident := name, name := toString name.getId, body := body }
  | _ => throwErrorAt stx "invalid modifier declaration"

private def parseModifierUse (stx : TSyntax `verityModifierUse) : CommandElabM (Array Ident) := do
  match stx with
  | `(verityModifierUse| with $[$names:ident],*) => pure names
  | _ => throwErrorAt stx "invalid modifier use"

private def parseFunction (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl := #[]) (adtDecls : Array AdtDecl := #[]) (interfaceNames : Array String := #[]) (stx : Syntax) : CommandElabM FunctionDecl := do
  match stx with
  | `(verityFunction| function $[$modsBefore:verityMutability]* $[$pureMod?:pureMutabilityMarker]? $[$modsAfter:verityMutability]* $name:ident ($[$params:verityParam],*) $[$guard?:verityInitGuard]? $[$modifierUse?:verityModifierUse]? $[$requiresRoleClause?:verityRequiresRole]? $[$modifiesClause?:verityModifies]? $[$localObligations?:verityLocalObligations]? : $retTy:term := $body:term) => do
      let mut_ ← parseMutabilityModifiers (modsBefore ++ modsAfter) stx
      let mut_ := { mut_ with isPure := pureMod?.isSome }
      let parsedParams ← params.mapM (parseFunctionParamWithInterfaces newtypes structDecls adtDecls interfaceNames)
      let parsedReturnTy ← valueTypeFromSyntax newtypes structDecls adtDecls retTy
      let parsedGuard? ←
        match guard? with
        | some guard => pure (some (← parseInitGuard guard))
        | none => pure none
      let parsedRequiresRole ←
        match requiresRoleClause? with
        | some roleClause =>
            match roleClause with
            | `(verityRequiresRole| requires($roleField:ident)) => pure (some roleField)
            | _ => throwErrorAt roleClause "invalid requires annotation"
        | none => pure none
      let parsedModifies ←
        match modifiesClause? with
        | some modClause => parseModifies modClause
        | none => pure #[]
      let parsedModifiers ←
        match modifierUse? with
        | some modUse => parseModifierUse modUse
        | none => pure #[]
      let parsedLocalObligations ←
        match localObligations? with
        | some obligations => parseLocalObligations obligations
        | none => pure #[]
      pure {
        ident := name
        name := toString name.getId
        params := parsedParams
        returnTy := parsedReturnTy
        isPayable := mut_.isPayable
        isView := mut_.isView
        isPure := mut_.isPure
        isInternal := mut_.isInternal
        noExternalCalls := mut_.noExternalCalls
        allowPostInteractionWrites := mut_.allowPostInteractionWrites
        nonReentrantLock := mut_.nonReentrantLock
        ceiSafe := mut_.ceiSafe
        requiresRole := parsedRequiresRole
        initGuard? := parsedGuard?
        modifies := parsedModifies
        localObligations := parsedLocalObligations
        modifiers := parsedModifiers
        body := body
      }
  | _ => throwErrorAt stx "invalid function declaration"

private def parseConstructor (newtypes : Array NewtypeDecl) (structDecls : Array StructDecl := #[]) (adtDecls : Array AdtDecl := #[]) (stx : Syntax) : CommandElabM ConstructorDecl := do
  match stx with
  | `(verityConstructor| constructor ($[$params:verityParam],*) payable local_obligations [ $[$obligations:verityLocalObligation],* ] := $body:term) =>
      pure {
        params := ← params.mapM (parseParam newtypes structDecls adtDecls)
        isPayable := true
        localObligations := ← obligations.mapM parseLocalObligation
        body := body
      }
  | `(verityConstructor| constructor ($[$params:verityParam],*) payable := $body:term) =>
      pure {
        params := ← params.mapM (parseParam newtypes structDecls adtDecls)
        isPayable := true
        body := body
      }
  | `(verityConstructor| constructor ($[$params:verityParam],*) local_obligations [ $[$obligations:verityLocalObligation],* ] := $body:term) =>
      pure {
        params := ← params.mapM (parseParam newtypes structDecls adtDecls)
        localObligations := ← obligations.mapM parseLocalObligation
        body := body
      }
  | `(verityConstructor| constructor ($[$params:verityParam],*) := $body:term) =>
      pure {
        params := ← params.mapM (parseParam newtypes structDecls adtDecls)
        body := body
      }
  | _ => throwErrorAt stx "invalid constructor declaration"

private def immutableHiddenName (imm : ImmutableDecl) : String :=
  s!"__immutable_{imm.name}"

private def storageFieldFootprintSize (field : StorageFieldDecl) : Nat :=
  match field.ty with
  | .scalar (.adt _ maxFields) => maxFields + 1
  | _ => 1

private def immutableSlotIndex (fields : Array StorageFieldDecl) (idx : Nat) : Nat :=
  let nextUserSlot := fields.foldl (fun maxSlot field =>
    max maxSlot (field.slotNum + storageFieldFootprintSize field)) 0
  nextUserSlot + idx

private def immutableSlotIdent (imm : ImmutableDecl) : Ident :=
  mkIdent (Name.mkSimple s!"__verity_immutable_slot_{imm.name}")

def immutableStorageFieldDecl
    (fields : Array StorageFieldDecl)
    (imm : ImmutableDecl)
    (idx : Nat) : StorageFieldDecl :=
  {
    ident := immutableSlotIdent imm
    name := immutableHiddenName imm
    ty := match imm.ty with
      | .uint256 | .int256 | .uint8 | .uint16 | .bytes32 | .bool => .scalar .uint256
      | .address => .scalar .address
      | _ => .scalar imm.ty
    slotNum := immutableSlotIndex fields idx
    adtInfo? := none
  }

private def validateImmutableType (imm : ImmutableDecl) : CommandElabM Unit :=
  match imm.ty with
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => pure ()
  | _ =>
      throwErrorAt imm.ident
        s!"contract immutables currently support only Uint256, Int256, Uint8, Address, Bytes32, and Bool; '{imm.name}' uses unsupported type"

private def validateImmutableBodyType
    (imm : ImmutableDecl)
    (ctorParams : Array ParamDecl) : CommandElabM Unit := do
  let expectedTy ← contractValueTypeTerm imm.ty
  let mut wrappedBody : Term := imm.body
  wrappedBody ← `(term| (($wrappedBody : $expectedTy)))
  for param in ctorParams.reverse do
    wrappedBody ← `(term| fun ($(param.ident) : $(← contractValueTypeTerm param.ty)) => $wrappedBody)
  liftTermElabM do
    discard <| Lean.Elab.Term.elabTerm wrappedBody none

private partial def containsAdtValueType : ValueType → Bool
  | .adt _ _ => true
  | .newtype _ baseType => containsAdtValueType baseType
  | .array elemTy => containsAdtValueType elemTy
  | .fixedArray elemTy _ => containsAdtValueType elemTy
  | .tuple elemTys => elemTys.any containsAdtValueType
  | .struct _ fields => fields.any (fun field => containsAdtValueType field.snd)
  | _ => false

private def rejectExecutableBoundaryAdt
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM Unit := do
  if containsAdtValueType ty then
    throwErrorAt stx
      s!"{context} uses an ADT at the executable contract boundary. ADT storage is supported, but ABI/function boundary ADT lowering is not yet implemented; pass scalar fields explicitly or keep the ADT in storage."

private def externalExecutableWordType? : ValueType → Bool
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => true
  | .newtype _ baseType => externalExecutableWordType? baseType
  | _ => false

private def externalExecutableParamType? : ValueType → Bool
  | .array elemTy => externalExecutableWordType? elemTy
  | .bytes | .string => true
  | .newtype _ baseType => externalExecutableParamType? baseType
  | ty => externalExecutableWordType? ty

private partial def externalExecutableReturnType? : ValueType → Bool
  | .tuple elemTys => elemTys.all externalExecutableReturnType?
  | .fixedArray elemTy _ => externalExecutableReturnType? elemTy
  | .struct _ fields => fields.all (fun field => externalExecutableReturnType? field.snd)
  | .newtype _ baseType => externalExecutableReturnType? baseType
  | ty => externalExecutableWordType? ty

private def validateExternalExecutableType
    (extIdent : Ident)
    (extName : String)
    (ty : ValueType)
    (role : String) : CommandElabM Unit := do
  if !externalExecutableReturnType? ty then
    throwErrorAt extIdent
      s!"linked external '{extName}' uses unsupported {role} type; executable externalCall currently supports only word-like values and static ABI composites of word-like values"

private def validateExternalExecutableParamType
    (extIdent : Ident)
    (extName : String)
    (ty : ValueType) : CommandElabM Unit := do
  if !externalExecutableParamType? ty then
    throwErrorAt extIdent
      s!"linked external '{extName}' uses unsupported parameter type; executable externalCall currently supports word-like values plus direct Array<word-like>, Bytes, and String calldata parameters"

private def tupleElemsFromTerm? (stx : Term) : Option (Array Term) :=
  tupleElemsFromSyntax? (stripParens stx).raw |>.map (·.map (fun syn => ⟨syn⟩))

private def throwNonCompileTimeConstantError (stx : Syntax) (what : String) : CommandElabM α :=
  throwErrorAt stx s!"contract constants must be compile-time expressions; '{what}' is runtime-dependent"

private def lookupStructMemberDecl
    (fields : Array StorageFieldDecl)
    (fieldName memberName : String)
    (expectNested : Bool) : CommandElabM StructMemberDecl := do
  let field ←
    match fields.find? (fun f => f.name == fieldName || f.aliases.contains fieldName) with
    | some f => pure f
    | none => throwError s!"unknown storage field '{fieldName}'"
  let members ←
    match field.ty with
    | .mappingStruct _ members =>
        if expectNested then
          throwError s!"field '{fieldName}' is not a nested struct mapping"
        pure members
    | .mappingStruct2 _ _ members =>
        if expectNested then pure members
        else throwError s!"field '{fieldName}' is a nested struct mapping; use structMember2/setStructMember2"
    | _ =>
        if expectNested then
          throwError s!"field '{fieldName}' is not a nested struct mapping"
        else
          throwError s!"field '{fieldName}' is not a struct-valued mapping"
  match members.find? (fun member => member.name == memberName) with
  | some member => pure member
  | none => throwError s!"unknown struct member '{memberName}' on field '{fieldName}'"

private def lookupStorageField (fields : Array StorageFieldDecl) (name : String) : CommandElabM StorageFieldDecl := do
  match fields.find? (fun f => f.name == name || f.aliases.contains name) with
  | some f => pure f
  | none => throwError s!"unknown storage field '{name}'"

private def resolveInitGuardField
    (fields : Array StorageFieldDecl)
    (guard : InitGuardDecl)
    (stx : Syntax) : CommandElabM StorageFieldDecl := do
  let field ←
    match guard with
    | .initializer _ fieldName => lookupStorageField fields fieldName
    | .reinitializer _ fieldName _ => lookupStorageField fields fieldName
  match field.ty with
  | .scalar .uint256 => pure field
  | _ =>
      throwErrorAt stx
        s!"initializer guard field '{field.name}' must be a Uint256 storage slot"

private def initGuardRequireMessage : InitGuardDecl → String
  | .initializer .. => "initializer already run"
  | .reinitializer _ _ version => s!"reinitializer({version}) already run"

private def initGuardVersionTerm (version : Nat) : Term :=
  natTerm version

private def initGuardPreludeStmtTerms
    (fields : Array StorageFieldDecl)
    (fn : FunctionDecl) : CommandElabM (Array Term) := do
  match fn.initGuard? with
  | none => pure #[]
  | some guard =>
      let field ← resolveInitGuardField fields guard fn.ident
      let message := strTerm (initGuardRequireMessage guard)
      match guard with
      | .initializer _ _ =>
          pure #[
            ← `(Compiler.CompilationModel.Stmt.require
                  (Compiler.CompilationModel.Expr.eq
                    (Compiler.CompilationModel.Expr.storage $(strTerm field.name))
                    (Compiler.CompilationModel.Expr.literal 0))
                  $message),
            ← `(Compiler.CompilationModel.Stmt.setStorage
                  $(strTerm field.name)
                  (Compiler.CompilationModel.Expr.literal 1))
          ]
      | .reinitializer _ _ version =>
          pure #[
            ← `(Compiler.CompilationModel.Stmt.require
                  (Compiler.CompilationModel.Expr.lt
                    (Compiler.CompilationModel.Expr.storage $(strTerm field.name))
                    (Compiler.CompilationModel.Expr.literal $(initGuardVersionTerm version)))
                  $message),
            ← `(Compiler.CompilationModel.Stmt.setStorage
                  $(strTerm field.name)
                  (Compiler.CompilationModel.Expr.literal $(initGuardVersionTerm version)))
          ]

private def mkInitGuardedBody
    (fields : Array StorageFieldDecl)
    (fn : FunctionDecl) : CommandElabM Term := do
  match fn.initGuard? with
  | none => pure fn.body
  | some guard =>
      let field ← resolveInitGuardField fields guard fn.ident
      let currentVersion := mkIdent (Name.mkSimple s!"__verity_init_version_{field.name}")
      let message := strTerm (initGuardRequireMessage guard)
      match fn.body with
      | `(term| do $[$elems:doElem]*) =>
          match guard with
          | .initializer _ _ =>
              `(do
                  let $currentVersion ← getStorage $field.ident
                  require ($currentVersion == 0) $message
                  setStorage $field.ident 1
                  $[$elems:doElem]*)
          | .reinitializer _ _ version =>
              `(do
                  let $currentVersion ← getStorage $field.ident
                  require ($currentVersion < $(initGuardVersionTerm version)) $message
                  setStorage $field.ident $(initGuardVersionTerm version)
                  $[$elems:doElem]*)
      | _ => throwErrorAt fn.body "function body must be a do block"

/-- Classifies a `requires(role)` annotation by the shape of the named storage
    field. `scalarAddress` is the canonical `onlyOwner` shape — a scalar
    Address-typed slot; the macro injects `require (caller == storage[slot])`.
    `mappingAddress` is the role-as-mapping shape (e.g. `onlyRelayer`,
    `onlyMinter`) — a `mapping(address => uint256)` slot used as a 0/1 flag;
    the macro injects `require (storage[slot][caller] != 0)`. (verity#1837) -/
private inductive RoleKind where
  | scalarAddress
  | mappingAddress

/-- Resolve the storage field referenced by a `requires(role)` annotation.
    Accepts either an Address-typed scalar field (`onlyOwner`-style) or an
    `Address → Uint256` mapping field (`onlyRelayer`-style, verity#1837). -/
private def resolveRoleField
    (fields : Array StorageFieldDecl) (roleIdent : Ident) (fnIdent : Ident)
    : CommandElabM (StorageFieldDecl × RoleKind) := do
  let roleName := toString roleIdent.getId
  match fields.find? (fun f => f.name == roleName) with
  | none =>
      throwErrorAt roleIdent s!"function '{toString fnIdent.getId}': requires references unknown storage field '{roleName}'; known fields: {(fields.map (·.name)).toList}"
  | some field =>
      match field.ty with
      | .scalar .address | .scalar (.newtype _ .address) =>
          pure (field, .scalarAddress)
      | .mappingAddressToUint256 =>
          pure (field, .mappingAddress)
      | _ => throwErrorAt roleIdent s!"function '{toString fnIdent.getId}': requires({roleName}) must reference an Address-typed scalar storage field or an Address→Uint256 mapping (role-as-mapping), but '{roleName}' has a different type"

/-- Generate IR-level prelude statements for a `requires(role)` annotation.
    Scalar Address: injects `Stmt.require (Expr.eq Expr.caller (Expr.storageAddr roleField)) message`.
    Address→Uint256 mapping: injects `Stmt.require (Expr.mapping roleField Expr.caller) message`
    (the truthy check works because the value is 0 or non-zero).
    (#1728, Axis 2 Step 2c; mapping-keyed extension verity#1837) -/
private def roleGuardPreludeStmtTerms
    (fields : Array StorageFieldDecl)
    (fn : FunctionDecl) : CommandElabM (Array Term) := do
  match fn.requiresRole with
  | none => pure #[]
  | some roleIdent =>
      let (field, kind) ← resolveRoleField fields roleIdent fn.ident
      let message := strTerm s!"Access denied: caller is not {field.name}"
      match kind with
      | .scalarAddress =>
          pure #[
            ← `(Compiler.CompilationModel.Stmt.require
                  (Compiler.CompilationModel.Expr.eq
                    (Compiler.CompilationModel.Expr.caller)
                    (Compiler.CompilationModel.Expr.storageAddr $(strTerm field.name)))
                  $message)
          ]
      | .mappingAddress =>
          pure #[
            ← `(Compiler.CompilationModel.Stmt.require
                  (Compiler.CompilationModel.Expr.mapping $(strTerm field.name)
                    Compiler.CompilationModel.Expr.caller)
                  $message)
          ]

/-- Transform the source-level do-block body to inject a role access control check
    at the start. Scalar Address: `let __sender ← msgSender; let __roleHolder ← getStorageAddr field;
    require (__sender == __roleHolder) message`. Address→Uint256 mapping:
    `let __sender ← msgSender; let __roleValue ← getMapping field __sender;
    require (__roleValue != 0) message`.
    (#1728, Axis 2 Step 2c; mapping-keyed extension verity#1837) -/
private def mkRoleGuardedBody
    (fields : Array StorageFieldDecl)
    (fn : FunctionDecl) : CommandElabM Term := do
  match fn.requiresRole with
  | none => pure fn.body
  | some roleIdent =>
      let (field, kind) ← resolveRoleField fields roleIdent fn.ident
      let senderVar := mkIdent (Name.mkSimple s!"__verity_role_sender_{field.name}")
      let message := strTerm s!"Access denied: caller is not {field.name}"
      match fn.body with
      | `(term| do $[$elems:doElem]*) =>
          match kind with
          | .scalarAddress =>
              let holderVar := mkIdent (Name.mkSimple s!"__verity_role_holder_{field.name}")
              `(do
                  let $senderVar ← msgSender
                  let $holderVar ← getStorageAddr $field.ident
                  require ($senderVar == $holderVar) $message
                  $[$elems:doElem]*)
          | .mappingAddress =>
              let valueVar := mkIdent (Name.mkSimple s!"__verity_role_value_{field.name}")
              `(do
                  let $senderVar ← msgSender
                  let $valueVar ← getMapping $field.ident $senderVar
                  require ($valueVar != 0) $message
                  $[$elems:doElem]*)
      | _ => throwErrorAt fn.body "function body must be a do block"

private def mkImmutableBoundBody
    (fields : Array StorageFieldDecl)
    (immutableDecls : Array ImmutableDecl)
    (fn : FunctionDecl)
    (body : Term) : CommandElabM Term := do
  let visibleImmutables := immutableDecls.filter fun imm =>
    !fn.params.any (fun p => p.name == imm.name)
  match body with
  | `(term| do $[$elems:doElem]*) =>
      let preludeElemGroups ← visibleImmutables.zipIdx.mapM fun (imm, idx) => do
        let slotField := immutableStorageFieldDecl fields imm idx
        match imm.ty with
        | .uint256 | .uint8 | .bytes32 =>
            pure #[← `(doElem| let $(imm.ident) ← getStorage $(slotField.ident))]
        | .int256 =>
            pure #[← `(doElem| let $(imm.ident) := toInt256 (← getStorage $(slotField.ident)))]
        | .bool =>
            let rawName := mkIdent (Name.mkSimple s!"__verity_immutable_raw_{imm.name}")
            pure #[
              ← `(doElem| let $rawName ← getStorage $(slotField.ident)),
              ← `(doElem| let $(imm.ident) := ($rawName != 0))
            ]
        | .address =>
            pure #[← `(doElem| let $(imm.ident) ← getStorageAddr $(slotField.ident))]
        | _ => throwErrorAt imm.ident s!"immutable '{imm.name}' uses unsupported type"
      let preludeElems := preludeElemGroups.foldl (· ++ ·) #[]
      `(do $[$preludeElems:doElem]* $[$elems:doElem]*)
  | _ => throwErrorAt body "function body must be a do block"

private def expectStringLiteral (stx : Term) : CommandElabM String :=
  match (stripParens stx).raw.isStrLit? with
  | some s => pure s
  | none => throwErrorAt stx "expected string literal"

private def expectStringOrIdent (stx : Term) : CommandElabM String := do
  match stripParens stx with
  | `(term| $id:ident) => pure (toString id.getId)
  | other => expectStringLiteral other

private def expectStringList (stx : Term) : CommandElabM (Array String) := do
  match stripParens stx with
  | `(term| [ $[$xs],* ]) =>
      xs.mapM expectStringOrIdent
  | _ => throwErrorAt stx "expected list literal [..]"

private def tupleBinderNames? (stx : Syntax) : Option (Array (Option String)) := do
  let elems ← tupleElemsFromSyntax? stx
  elems.mapM fun elem =>
    match elem with
    | .ident _ raw _ _ => some raw.toString
    | .node _ `Lean.Parser.Term.hole _ => some none
    | _ => none

private def ensureFreshLocalNames
    (locals : Array String)
    (names : Array (Option String))
    (stx : Syntax) : CommandElabM Unit := do
  let boundNames := names.filterMap id
  let rec firstDuplicate (seen : Array String) (rest : Array String) (idx : Nat) : Option String :=
    if h : idx < rest.size then
      let name := rest[idx]
      if seen.contains name then
        some name
      else
        firstDuplicate (seen.push name) rest (idx + 1)
    else
      none
  match firstDuplicate #[] boundNames 0 with
  | some dup => throwErrorAt stx s!"duplicate local variable '{dup}'"
  | none => pure ()
  for name in boundNames do
    if locals.contains name then
      throwErrorAt stx s!"duplicate local variable '{name}'"

private def freshDiscardName (usedNames : List String) (idx : Nat) : String :=
  let base := s!"__tuple_discard_{idx}"
  if !usedNames.contains base then
    base
  else
    let rec go (suffix : Nat) (remaining : Nat) : String :=
      let candidate := s!"{base}_{suffix}"
      if !usedNames.contains candidate then
        candidate
      else
        match remaining with
        | 0 => s!"{base}_fresh"
        | n + 1 => go (suffix + 1) n
    go 1 usedNames.length

private def tupleParamElemExprs?
    (params : Array ParamDecl)
    (name : String) : CommandElabM (Option (Array Term)) := do
  match params.find? (fun p => p.name == name) with
  | some p =>
      match p.ty with
      | .tuple elemTys => do
          let exprs ← (elemTys.toArray.zipIdx).mapM fun (_ty, idx) =>
            `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_{idx}"))
          pure (some exprs)
      | _ => pure none
  | none => pure none

private def isTupleComponentRef (params : Array ParamDecl) (name : String) : Bool :=
  -- Check if `name` matches `<paramName>_<digit>` for a tuple-typed param
  match name.splitOn "_" with
  | [baseName, indexStr] =>
      match indexStr.toNat? with
      | some idx =>
          params.any fun p =>
            p.name == baseName &&
            match p.ty with
            | .tuple elemTys => idx < elemTys.length
            | _ => false
      | none => false
  | _ => false

private def lookupVarExpr (params : Array ParamDecl) (locals : Array String) (name : String) : CommandElabM Term := do
  if params.any (fun p => p.name == name) then
    `(Compiler.CompilationModel.Expr.param $(strTerm name))
  else if isTupleComponentRef params name then
    `(Compiler.CompilationModel.Expr.param $(strTerm name))
  else if locals.contains name then
    `(Compiler.CompilationModel.Expr.localVar $(strTerm name))
  else
    throwError s!"unknown variable '{name}'"

private inductive LocalSource where
  | value
  | arrayElement (paramName : String) (index : Term) (elemTy : ValueType)
  | memoryArray
  | externalStaticStruct (fields : List (String × String))

private structure TypedLocal where
  name : String
  ty : ValueType
  source : LocalSource := .value
  interfaceName? : Option String := none

private def mkTypedLocal (name : String) (ty : ValueType) (interfaceName? : Option String := none) : TypedLocal :=
  { name, ty, interfaceName? }

private def memoryArrayDataOffsetName (name : String) : String :=
  s!"{name}_data_offset"

private def memoryArrayLengthName (name : String) : String :=
  s!"{name}_length"

private def typedLocalNames (locals : Array TypedLocal) : Array String :=
  locals.map (·.name)

private def matchesBareName (actual bare : String) : Bool :=
  actual == bare || actual.endsWith s!".{bare}"

private def declaredNameMatches (query declared : String) : Bool :=
  matchesBareName query declared || matchesBareName declared query

private def contextAccessorBareName? (name : String) : Option String :=
  if matchesBareName name "msgSender" then some "msgSender"
  else if matchesBareName name "msgValue" then some "msgValue"
  else if matchesBareName name "selfBalance" then some "selfBalance"
  else if matchesBareName name "blockTimestamp" then some "blockTimestamp"
  else if matchesBareName name "blockNumber" then some "blockNumber"
  else if matchesBareName name "blobbasefee" then some "blobbasefee"
  else if matchesBareName name "contractAddress" then some "contractAddress"
  else if matchesBareName name "chainid" then some "chainid"
  else none

private def findContextAccessorShadowName?
    (params : Array ParamDecl) (locals : Array String) (name : String) : Option String :=
  match params.find? (fun p => matchesBareName p.name name) with
  | some param => some param.name
  | none => locals.find? (fun localName => matchesBareName localName name)

private def isSignedWordValueType : ValueType → Bool
  | .int256 => true
  | .newtype _ baseType => isSignedWordValueType baseType
  | _ => false

private def isWordLikeValueType : ValueType → Bool
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 => true
  | .newtype _ baseType => isWordLikeValueType baseType
  | _ => false

private def isSingleWordStaticValueType : ValueType → Bool
  | .bool => true
  | .newtype _ baseType => isSingleWordStaticValueType baseType
  | ty => isWordLikeValueType ty

private def externalCallDynamicArgSupported (ty : ValueType) : Bool :=
  match ty with
  | .array elemTy => isSingleWordStaticValueType elemTy
  | .bytes | .string => true
  | _ => false

private partial def staticAbiWordCount? : ValueType → Option Nat
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => some 1
  | .fixedArray elemTy size => do
      let elemWords ← staticAbiWordCount? elemTy
      some (size * elemWords)
  | .tuple elemTys =>
      elemTys.foldl
        (fun acc ty =>
          match acc, staticAbiWordCount? ty with
          | some n, some m => some (n + m)
          | _, _ => none)
        (some 0)
  | .struct _ fields =>
      fields.foldl
        (fun acc field =>
          match acc, staticAbiWordCount? field.snd with
          | some n, some m => some (n + m)
          | _, _ => none)
        (some 0)
  | .newtype _ baseType => staticAbiWordCount? baseType
  | _ => none

private partial def staticAbiLeafNames? : ValueType → Option (List String)
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => some [""]
  | .fixedArray elemTy size => do
      let elemNames ← staticAbiLeafNames? elemTy
      let mut out : List String := []
      for idx in List.range size do
        for suffix in elemNames do
          out := out ++ [if suffix == "" then toString idx else s!"{idx}_{suffix}"]
      some out
  | .tuple elemTys => do
      let mut out : List String := []
      for (ty, idx) in elemTys.zipIdx do
        let elemNames ← staticAbiLeafNames? ty
        for suffix in elemNames do
          out := out ++ [if suffix == "" then toString idx else s!"{idx}_{suffix}"]
      some out
  | .struct _ fields => do
      let mut out : List String := []
      for (fieldName, fieldTy) in fields do
        let fieldNames ← staticAbiLeafNames? fieldTy
        for suffix in fieldNames do
          out := out ++ [if suffix == "" then fieldName else s!"{fieldName}_{suffix}"]
      some out
  | .newtype _ baseType => staticAbiLeafNames? baseType
  | _ => none

private partial def staticStructDirectFieldLocals?
    (baseName : String) (ty : ValueType) : Option (List (String × String)) := do
  let .struct _ fields := ty | none
  let mut out : List (String × String) := []
  for (fieldName, fieldTy) in fields do
    unless isSingleWordStaticValueType fieldTy do
      failure
    out := out ++ [(fieldName, s!"{baseName}_{fieldName}")]
  some out

private def flattenExternalResultNames
    (baseName : String) (ty : ValueType) : CommandElabM (List String) := do
  match staticAbiLeafNames? ty with
  | some [""] => pure [baseName]
  | some suffixes => pure (suffixes.map fun suffix => s!"{baseName}_{suffix}")
  | none =>
      throwError s!"external result '{baseName}' has a dynamic type; bind dynamic external returns explicitly"

private partial def abiLocalHeadWordCount? : ValueType → Option Nat
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool
  | .string | .bytes | .array _ => some 1
  | .fixedArray elemTy size => do
      match staticAbiWordCount? elemTy with
      | some elemWords => some (size * elemWords)
      | none => some size
  | .tuple elemTys =>
      elemTys.foldl
        (fun acc ty =>
          match acc, abiLocalHeadWordCount? ty with
          | some n, some m => some (n + m)
          | _, _ => none)
        (some 0)
  | .struct _ fields =>
      fields.foldl
        (fun acc field =>
          match acc, abiLocalHeadWordCount? field.snd with
          | some n, some m => some (n + m)
          | _, _ => none)
        (some 0)
  | .newtype _ baseType => abiLocalHeadWordCount? baseType
  | .adt _ _ | .unit => none

private partial def valueTypeUsesDynamicData : ValueType → Bool
  | .string | .bytes | .array _ => true
  | .fixedArray elemTy _ => valueTypeUsesDynamicData elemTy
  | .tuple elemTys => elemTys.any valueTypeUsesDynamicData
  | .struct _ fields => fields.any (fun field => valueTypeUsesDynamicData field.snd)
  | .newtype _ baseType => valueTypeUsesDynamicData baseType
  | .adt _ _ => false  -- ADTs are stored as tag + fixed-width slots, not dynamic
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool | .unit => false

private partial def abiParentHeadWordCount? (ty : ValueType) : Option Nat :=
  match ty with
  | .string | .bytes | .array _ => some 1
  | .fixedArray elemTy size =>
      if valueTypeUsesDynamicData (.fixedArray elemTy size) then
        some 1
      else
        abiLocalHeadWordCount? (.fixedArray elemTy size)
  | .tuple _ | .struct _ _ =>
      if valueTypeUsesDynamicData ty then
        some 1
      else
        abiLocalHeadWordCount? ty
  | .newtype _ baseType => abiParentHeadWordCount? baseType
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bytes32 | .bool => some 1
  | .adt _ _ | .unit => none

private def classifyWordArithmeticResultType
    (stx : Syntax)
    (context : String)
    (lhsTy rhsTy : ValueType) : CommandElabM ValueType := do
  unless isWordLikeValueType lhsTy do
    throwErrorAt stx s!"{context} requires a word-like value (Uint256, Int256, Uint8, Address, or Bytes32), got {reprStr lhsTy}"
  unless isWordLikeValueType rhsTy do
    throwErrorAt stx s!"{context} requires a word-like value (Uint256, Int256, Uint8, Address, or Bytes32), got {reprStr rhsTy}"
  if isSignedWordValueType lhsTy || isSignedWordValueType rhsTy then
    if lhsTy == .int256 && rhsTy == .int256 then
      pure .int256
    else
      throwErrorAt stx
        s!"{context} requires explicit casts when mixing Int256 with non-Int256 words; got {reprStr lhsTy} and {reprStr rhsTy}"
  else
    pure .uint256

private def classifyUnsignedWordArithmeticResultType
    (stx : Syntax)
    (context : String)
    (lhsTy rhsTy : ValueType) : CommandElabM ValueType := do
  unless isWordLikeValueType lhsTy do
    throwErrorAt stx s!"{context} requires a word-like value (Uint256, Int256, Uint8, Address, or Bytes32), got {reprStr lhsTy}"
  unless isWordLikeValueType rhsTy do
    throwErrorAt stx s!"{context} requires a word-like value (Uint256, Int256, Uint8, Address, or Bytes32), got {reprStr rhsTy}"
  pure .uint256

private def isNatLiteralTerm (stx : Term) : Bool :=
  match stripParens stx with
  | `(term| $_n:num) => true
  | _ => false

private def numericLiteralCompatibleValueType : ValueType → Bool
  | .uint256 | .int256 | .uint8 | .uint16 => true
  | .newtype _ baseType => numericLiteralCompatibleValueType baseType
  | _ => false

private def argumentTypeMatchesParam (arg : Term) (argTy paramTy : ValueType) : Bool :=
  argTy == paramTy ||
    (argTy == .uint256 && paramTy == .bytes32) ||
    (argTy == .bytes32 && paramTy == .uint256) ||
    (argTy == .uint256 && isNatLiteralTerm arg && numericLiteralCompatibleValueType paramTy)

private def argumentTypesMatchParams (args : Array Term) (argTypes : Array ValueType)
    (params : Array ParamDecl) : Bool :=
  args.size == params.size && Id.run do
    for idx in [:args.size] do
      let some arg := args[idx]? | return false
      let some argTy := argTypes[idx]? | return false
      let some param := params[idx]? | return false
      unless argumentTypeMatchesParam arg argTy param.ty do
        return false
    return true

private def preferSignedNumericLiteralPeer
    (lhs rhs : Term)
    (lhsTy rhsTy : ValueType) : ValueType × ValueType :=
  let lhsTy :=
    if lhsTy == .uint256 && rhsTy == .int256 && isNatLiteralTerm lhs then .int256 else lhsTy
  let rhsTy :=
    if rhsTy == .uint256 && lhsTy == .int256 && isNatLiteralTerm rhs then .int256 else rhsTy
  (lhsTy, rhsTy)

private def lookupTypedLocalType? (locals : Array TypedLocal) (name : String) : Option ValueType :=
  locals.findSome? fun localTy =>
    if localTy.name == name then some localTy.ty else none

private def lookupInterfaceName?
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (name : String) : Option String :=
  params.findSome? (fun p =>
    if p.name == name then p.interfaceName? else none)
  <|> locals.findSome? (fun localTy =>
    if localTy.name == name then localTy.interfaceName? else none)

private def interfaceNameOfTerm?
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option String) := do
  match stripParens stx with
  | `(term| $ident:ident) => pure (lookupInterfaceName? params locals (toString ident.getId))
  | _ => pure none

private def tupleParamElemType? (params : Array ParamDecl) (name : String) : Option ValueType :=
  match name.splitOn "_" with
  | [baseName, indexStr] =>
      match indexStr.toNat? with
      | some idx =>
          params.findSome? fun p =>
            if p.name == baseName then
              match p.ty with
              | .tuple elemTys => elemTys.toArray[idx]?
              | _ => none
            else
              none
      | none => none
  | _ => none

private partial def structProjectionPath? (stx : Term) : Option (Term × List String) :=
  match stripParens stx with
  | `(term| $id:ident) =>
      match (toString id.getId).splitOn "." with
      | root :: field :: rest =>
          some (mkIdent (Name.mkSimple root), field :: rest)
      | _ => none
  | `(term| $base:term.$field:ident) =>
      let fieldPath := (toString field.getId).splitOn "."
      match structProjectionPath? base with
      | some (root, path) => some (root, path ++ fieldPath)
      | none => some (base, fieldPath)
  | _ => none

private partial def structFieldPath?
    (ty : ValueType)
    (path : List String)
    (indices : List Nat := []) : Option (ValueType × List Nat) :=
  -- The returned indices are the recursive tuple binding path used by
  -- ParamLoading.staticParamBindingNames, not flattened ABI word offsets.
  match path with
  | [] => some (ty, indices)
  | fieldName :: rest =>
      match ty with
      | .struct _ fields =>
          fields.zipIdx.findSome? fun (field, idx) =>
            if field.fst == fieldName then
              structFieldPath? field.snd rest (indices ++ [idx])
            else
              none
      | _ => none

private partial def structFieldHeadOffset?
    (ty : ValueType)
    (path : List String)
    (baseOffset : Nat := 0) : Option (ValueType × Nat) :=
  match path with
  | [] => none
  | fieldName :: rest =>
      match ty with
      | .struct _ fields =>
          let rec go (remaining : List (String × ValueType)) (curOffset : Nat) : Option (ValueType × Nat) :=
            match remaining with
            | [] => none
            | (name, fieldTy) :: more =>
                if name == fieldName then
                  match rest with
                  | [] => some (fieldTy, curOffset)
                  | _ =>
                      if valueTypeUsesDynamicData fieldTy then
                        none
                      else
                        structFieldHeadOffset? fieldTy rest curOffset
                else
                  match abiParentHeadWordCount? fieldTy with
                  | some n => go more (curOffset + n)
                  | none => none
          go fields baseOffset
      | _ => none

private def paramStructProjectionResolved?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType × ValueType) := do
  let resolve (rootName : String) (path : List String) := do
    let param ← params.find? (fun p => p.name == rootName)
    let (fieldTy, indices) ← structFieldPath? param.ty path
    if indices.isEmpty then
      none
    else
      some (String.intercalate "_" (rootName :: indices.map toString), fieldTy, param.ty)
  match (stripParens stx).raw with
  | .ident _ raw _ _ =>
      let parts := raw.toString.splitOn "."
      let rec findParamPath : List String → Option (String × List String)
        | [] | [_] => none
        | rootName :: fieldName :: rest =>
            if params.any (fun p => p.name == rootName) then
              some (rootName, fieldName :: rest)
            else
              findParamPath (fieldName :: rest)
      match findParamPath parts with
      | some (rootName, path) => resolve rootName path
      | none => none
  | _ =>
      let (root, path) ← structProjectionPath? stx
      match stripParens root with
      | `(term| $id:ident) => resolve (toString id.getId) path
      | _ => none

private def arrayElementStructProjectionResolved?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let (root, path) ← structProjectionPath? stx
  match stripParens root with
  | `(term| arrayElement $name:term $index:term) =>
      let paramName ←
        match stripParens name with
        | `(term| $id:ident) => some (toString id.getId)
        | _ => none
      let param ← params.find? (fun p => p.name == paramName)
      let elemTy ← match param.ty with
        | .array elemTy => some elemTy
        | _ => none
      let (fieldTy, wordOffset) ← structFieldHeadOffset? elemTy path
      some (paramName, index, fieldTy, elemTy, wordOffset)
  | _ => none

private def arrayElementStructProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let resolved@(_, _, fieldTy, _, _) ← arrayElementStructProjectionResolved? params stx
  if isSingleWordStaticValueType fieldTy then
    some resolved
  else
    none

/-- Resolve `(arrayElement <param> <i>).<dynamicField>` projections whose
    projected field is a dynamically-sized member (`Array<T>`, `bytes`,
    `string`). Returns `(paramName, index, fieldTy, elemTy, wordOffset)`
    just like `arrayElementStructProjection?`, but with the dynamic-leaf
    type test inverted: this helper succeeds exactly when the projected
    field is dynamic (so the macro can lower into
    `Expr.arrayElementDynamicMemberLength` / future G2 element-indexing
    constructors). (verity#1849, G1) -/
private def arrayElementDynamicMemberProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let resolved@(_, _, fieldTy, _, _) ← arrayElementStructProjectionResolved? params stx
  match fieldTy with
  | .array _ | .bytes | .string => some resolved
  | _ => none

private def paramStructProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType) := do
  let (paramName, fieldTy, rootTy) ← paramStructProjectionResolved? params stx
  if isSingleWordStaticValueType fieldTy && !valueTypeUsesDynamicData rootTy then
    some (paramName, fieldTy)
  else
    none

/-- Resolve a `param.field[.field2…]` projection where `param` is a struct
    parameter whose ABI encoding is dynamic (carries nested dynamic
    members) and the projected leaf is a single-word static value at a
    fixed head offset. Returns `(paramName, fieldTy, wordOffset)`. The
    word offset is the head-word index relative to the parameter's
    `{name}_data_offset`, which after verity#1839 points at the first
    head word of the encoded tuple (no length prefix). (verity#1832) -/
private def paramDynamicHeadProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType × Nat) := do
  let (root, path) ←
    match (stripParens stx).raw with
    | .ident _ raw _ _ =>
        let parts := raw.toString.splitOn "."
        let rec findParamPath : List String → Option (String × List String)
          | [] | [_] => none
          | rootName :: fieldName :: rest =>
              if params.any (fun p => p.name == rootName) then
                some (rootName, fieldName :: rest)
              else
                findParamPath (fieldName :: rest)
        findParamPath parts
    | _ =>
        let (rootStx, path) ← structProjectionPath? stx
        match stripParens rootStx with
        | `(term| $id:ident) => some (toString id.getId, path)
        | _ => none
  let param ← params.find? (fun p => p.name == root)
  if !valueTypeUsesDynamicData param.ty then
    none
  else
    let (fieldTy, wordOffset) ← structFieldHeadOffset? param.ty path
    if isSingleWordStaticValueType fieldTy then
      some (root, fieldTy, wordOffset)
    else
      none

private def paramDynamicMemberProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType × Nat) := do
  let (root, path) ←
    match (stripParens stx).raw with
    | .ident _ raw _ _ =>
        let parts := raw.toString.splitOn "."
        let rec findParamPath : List String → Option (String × List String)
          | [] | [_] => none
          | rootName :: fieldName :: rest =>
              if params.any (fun p => p.name == rootName) then
                some (rootName, fieldName :: rest)
              else
                findParamPath (fieldName :: rest)
        findParamPath parts
    | _ =>
        let (rootStx, path) ← structProjectionPath? stx
        match stripParens rootStx with
        | `(term| $id:ident) => some (toString id.getId, path)
        | _ => none
  let param ← params.find? (fun p => p.name == root)
  if !valueTypeUsesDynamicData param.ty then
    none
  else
    let (fieldTy, wordOffset) ← structFieldHeadOffset? param.ty path
    match fieldTy with
    | .array _ | .bytes | .string => some (root, fieldTy, wordOffset)
    | _ => none

private def paramDynamicStaticCompositeProjection?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType × Nat) := do
  let (root, path) ←
    match (stripParens stx).raw with
    | .ident _ raw _ _ =>
        let parts := raw.toString.splitOn "."
        let rec findParamPath : List String → Option (String × List String)
          | [] | [_] => none
          | rootName :: fieldName :: rest =>
              if params.any (fun p => p.name == rootName) then
                some (rootName, fieldName :: rest)
              else
                findParamPath (fieldName :: rest)
        findParamPath parts
    | _ =>
        let (rootStx, path) ← structProjectionPath? stx
        match stripParens rootStx with
        | `(term| $id:ident) => some (toString id.getId, path)
        | _ => none
  let param ← params.find? (fun p => p.name == root)
  let (fieldTy, wordOffset) ← structFieldHeadOffset? param.ty path
  if !valueTypeUsesDynamicData fieldTy && !isSingleWordStaticValueType fieldTy then
    some (root, fieldTy, wordOffset)
  else
    none

private def arrayElementDynamicTupleArg?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Term × ValueType) := do
  match stripParens stx with
  | `(term| arrayElement $name:term $index:term) =>
      let paramName ←
        match stripParens name with
        | `(term| $id:ident) => some (toString id.getId)
        | _ => none
      let param ← params.find? (fun p => p.name == paramName)
      let elemTy ← match param.ty with
        | .array elemTy => some elemTy
        | _ => none
      if valueTypeUsesDynamicData elemTy then
        some (paramName, index, elemTy)
      else
        none
  | _ => none

private def arrayElementAliasSource?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Term × ValueType) := do
  match stripParens stx with
  | `(term| arrayElement $name:term $index:term) =>
      let paramName ←
        match stripParens name with
        | `(term| $id:ident) => some (toString id.getId)
        | _ => none
      let param ← params.find? (fun p => p.name == paramName)
      let elemTy ← match param.ty with
        | .array elemTy => some elemTy
        | _ => none
      if valueTypeUsesDynamicData elemTy then
        some (paramName, index, elemTy)
      else
        none
  | _ => none

private def localArrayElementAlias?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × Term × ValueType) := do
  let name ←
    match stripParens stx with
    | `(term| $id:ident) => some (toString id.getId)
    | _ => none
  let localDecl ← locals.find? (fun localDecl => localDecl.name == name)
  match localDecl.source with
  | .arrayElement paramName index elemTy => some (paramName, index, elemTy)
  | .value => none
  | .memoryArray => none
  | .externalStaticStruct _ => none

private def localExternalStaticStructProjection?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × ValueType) := do
  let (rootName, path) ←
    match (stripParens stx).raw with
    | .ident _ raw _ _ =>
        let parts := raw.toString.splitOn "."
        let rec findLocalPath : List String → Option (String × List String)
          | [] | [_] => none
          | rootName :: fieldName :: rest =>
              if locals.any (fun localDecl => localDecl.name == rootName) then
                some (rootName, fieldName :: rest)
              else
                findLocalPath (fieldName :: rest)
        findLocalPath parts
    | _ =>
        let (root, path) ← structProjectionPath? stx
        match stripParens root with
        | `(term| $id:ident) => some (toString id.getId, path)
        | _ => none
  let localDecl ← locals.find? (fun localDecl => localDecl.name == rootName)
  let .externalStaticStruct fieldLocals := localDecl.source | none
  let fieldName ←
    match path with
    | [field] => some field
    | _ => none
  let localName ← fieldLocals.findSome? fun (field, localName) =>
    if field == fieldName then some localName else none
  let fieldTy ←
    match localDecl.ty with
    | .struct _ fields => fields.findSome? fun (field, ty) =>
        if field == fieldName then some ty else none
    | _ => none
  some (localName, fieldTy)

private def localMemoryArray?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × ValueType) := do
  let name ←
    match stripParens stx with
    | `(term| $id:ident) => some (toString id.getId)
    | _ => none
  let localDecl ← locals.find? (fun localDecl => localDecl.name == name)
  match localDecl.source, localDecl.ty with
  | .memoryArray, .array elemTy => some (name, elemTy)
  | _, _ => none

private def requireSupportedMemoryArrayLocal
    (stx : Term)
    (context : String)
    (locals : Array TypedLocal) : CommandElabM (String × ValueType) := do
  match localMemoryArray? locals stx with
  | some (name, elemTy) => pure (name, elemTy)
  | none => throwErrorAt stx s!"{context} requires a memory-backed Array local"

private def localArrayElementStructProjectionResolved?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let (root, path) ← structProjectionPath? stx
  let (paramName, index, elemTy) ← localArrayElementAlias? locals root
  let (fieldTy, wordOffset) ← structFieldHeadOffset? elemTy path
  some (paramName, index, fieldTy, elemTy, wordOffset)

private def localArrayElementStructProjection?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let resolved@(_, _, fieldTy, _, _) ← localArrayElementStructProjectionResolved? locals stx
  if isSingleWordStaticValueType fieldTy then
    some resolved
  else
    none

private def localArrayElementDynamicMemberProjection?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let resolved@(_, _, fieldTy, _, _) ← localArrayElementStructProjectionResolved? locals stx
  match fieldTy with
  | .array _ | .bytes | .string => some resolved
  | _ => none

private def localArrayElementStaticCompositeProjection?
    (locals : Array TypedLocal)
    (stx : Term) : Option (String × Term × ValueType × ValueType × Nat) := do
  let resolved@(_, _, fieldTy, _, _) ← localArrayElementStructProjectionResolved? locals stx
  if !valueTypeUsesDynamicData fieldTy && !isSingleWordStaticValueType fieldTy then
    some resolved
  else
    none

private def abiHeadWordTarget?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × Option Term × ValueType) := do
  match stripParens stx with
  | `(term| arrayElement $name:term $index:term) =>
      let paramName ←
        match stripParens name with
        | `(term| $id:ident) => some (toString id.getId)
        | _ => none
      let param ← params.find? (fun p => p.name == paramName)
      let elemTy ← match param.ty with
        | .array elemTy => some elemTy
        | _ => none
      some (paramName, some index, elemTy)
  | _ => do
      let paramName ←
        match stripParens stx with
        | `(term| $id:ident) => some (toString id.getId)
        | _ => none
      let param ← params.find? (fun p => p.name == paramName)
      some (paramName, none, param.ty)

private def isParamStructNonLeafProjection (params : Array ParamDecl) (stx : Term) : Bool :=
  match paramStructProjectionResolved? params stx with
  | some (_, fieldTy, _) => !isSingleWordStaticValueType fieldTy
  | none => false

private def throwStructNonLeafProjectionError (stx : Term) : CommandElabM α :=
  throwErrorAt stx
    "non-leaf struct parameter projection is not supported; project a scalar or static single-word leaf field instead (#1832)"

private def renderValueType (ty : ValueType) : String :=
  reprStr ty

/-- verity#1849, G3: allow `Array <wordLike>` and `bytes` / `string` as
    external-call / event / custom-error argument types when the argument
    is a direct param reference. The lowering still happens through
    `Expr.param`, which becomes `YulExpr.ident name`; for `Array <T>` /
    `bytes` / `string` direct-param leaves the resulting Yul ident is
    really the `{name}_data_offset` / `{name}_length` pair that the
    dynamic-data param loader already binds, so callers can forward
    pre-decoded dynamic-data leaves directly. The CompilationModel's
    `Expr.externalCall` Yul-call lowering accepts this shape verbatim
    (one Yul arg per `Expr` arg) — the actual ABI encoding for downstream
    Solidity targets is handled by the ECM / `Stmt.externalCallBind`
    family, not by `Expr.externalCall`. -/
private def isWordOrDirectArrayType (ty : ValueType) : Bool :=
  match ty with
  | .array elemTy => isSingleWordStaticValueType elemTy
  | .bytes | .string => true
  | _ => isWordLikeValueType ty

private def requireWordOrDirectArrayType (stx : Syntax) (context : String) (ty : ValueType) : CommandElabM Unit := do
  unless isWordOrDirectArrayType ty do
    throwErrorAt stx
      s!"{context} requires a word-like value or a direct `Array <wordLike>`/`bytes`/`string` parameter reference, got {renderValueType ty}"

private def requireWordLikeType (stx : Syntax) (context : String) (ty : ValueType) : CommandElabM Unit := do
  unless isWordLikeValueType ty do
    throwErrorAt stx s!"{context} requires a word-like value (Uint256, Int256, Uint8, Address, or Bytes32), got {renderValueType ty}"

private def requireBoolType (stx : Syntax) (context : String) (ty : ValueType) : CommandElabM Unit := do
  unless ty == .bool do
    throwErrorAt stx s!"{context} requires Bool, got {renderValueType ty}"

private def requireSupportedReturnArrayType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM Unit := do
  match ty with
  | .array elemTy =>
      unless isSingleWordStaticValueType elemTy do
        throwErrorAt stx
          s!"{context} currently supports only arrays with single-word static elements on the compilation-model path, got {renderValueType ty}"
  | _ =>
      throwErrorAt stx s!"{context} requires an Array value, got {renderValueType ty}"

private def requireSupportedArrayElementSourceType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM ValueType := do
  match ty with
  | .array elemTy =>
      unless isSingleWordStaticValueType elemTy do
        throwErrorAt stx
          s!"{context} currently supports only arrays with single-word static elements on the compilation-model path, got {renderValueType ty}"
      pure elemTy
  | _ =>
      throwErrorAt stx s!"{context} requires an Array parameter, got {renderValueType ty}"

private def requireSupportedArrayElementTupleSourceType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM ValueType := do
  match ty with
  | .array elemTy@(.tuple _) =>
      match staticAbiWordCount? elemTy, abiLocalHeadWordCount? elemTy with
      | some _, _ => pure elemTy
      | none, some _ => pure elemTy
      | none, none =>
          throwErrorAt stx
            s!"{context} currently supports only arrays with ABI-decodable tuple elements, got {renderValueType ty}"
  | .array elemTy =>
      match staticAbiWordCount? elemTy with
      | some _ => pure elemTy
      | none =>
          throwErrorAt stx
            s!"{context} currently supports only arrays with static ABI-word elements on the tuple arrayElement path, got {renderValueType ty}"
  | _ =>
      throwErrorAt stx s!"{context} requires an Array parameter, got {renderValueType ty}"

private def directParamNameWithType?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType) :=
  match stripParens stx with
  | `(term| $id:ident) =>
      let name := toString id.getId
      params.findSome? fun p =>
        if p.name == name then
          some (name, p.ty)
        else
          none
  | _ => none

private def requireDirectParamRef
    (stx : Term)
    (context : String)
    (params : Array ParamDecl) : CommandElabM ValueType := do
  match directParamNameWithType? params stx with
  | some (_, paramTy) => pure paramTy
  | none =>
      throwErrorAt stx
        s!"{context} currently requires a direct parameter reference on the compilation-model path"

private def requireSupportedReturnBytesType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM Unit := do
  unless ty == .bytes || ty == .string do
    throwErrorAt stx
      s!"{context} requires a Bytes or String parameter on the compilation-model path, got {renderValueType ty}"

private def requireSupportedReturnStorageWordsType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM Unit := do
  match ty with
  | .array elemTy =>
      if isSingleWordStaticValueType elemTy then
        pure ()
      else
        throwErrorAt stx
          s!"{context} requires an array with single-word static elements on the compilation-model path, got {renderValueType ty}"
  | _ =>
      throwErrorAt stx
        s!"{context} requires an Array parameter on the compilation-model path, got {renderValueType ty}"

private def requireEqComparableTypes (stx : Syntax) (lhsTy rhsTy : ValueType) : CommandElabM Unit := do
  let bothWordLike := isWordLikeValueType lhsTy && isWordLikeValueType rhsTy
  let bothBool := lhsTy == .bool && rhsTy == .bool
  let bothDynamicBytes := (lhsTy == .string && rhsTy == .string) || (lhsTy == .bytes && rhsTy == .bytes)
  unless bothWordLike || bothBool || bothDynamicBytes do
    throwErrorAt stx
      s!"equality is currently supported only for Bool, matching bytes/string params, and word-like values (Uint256, Int256, Uint8, Address, Bytes32); got {renderValueType lhsTy} and {renderValueType rhsTy}"

private def directDynamicComparableParamName?
    (params : Array ParamDecl)
    (stx : Term) : Option (String × ValueType) :=
  match stripParens stx with
  | `(term| $id:ident) =>
      let name := toString id.getId
      params.findSome? fun p =>
        if p.name == name && (p.ty == .string || p.ty == .bytes) then
          some (name, p.ty)
        else
          none
  | _ => none

private def dynamicEqParamNames
    (stx : Syntax)
    (params : Array ParamDecl)
    (lhs rhs : Term)
    (lhsTy rhsTy : ValueType) : CommandElabM (String × String) := do
  match directDynamicComparableParamName? params lhs, directDynamicComparableParamName? params rhs with
  | some (lhsName, lhsParamTy), some (rhsName, rhsParamTy) =>
      if lhsParamTy == lhsTy && rhsParamTy == rhsTy && lhsTy == rhsTy then
        pure (lhsName, rhsName)
      else
        throwErrorAt stx
          s!"bytes/string equality requires matching direct parameter references, got {renderValueType lhsTy} and {renderValueType rhsTy}"
  | _, _ =>
      throwErrorAt stx
        "bytes/string equality currently requires direct parameter references on the compilation-model path"

private def requireSameOrWordLikeTypes (stx : Syntax) (context : String) (lhsTy rhsTy : ValueType) : CommandElabM Unit := do
  unless lhsTy == rhsTy || (isWordLikeValueType lhsTy && isWordLikeValueType rhsTy) do
    throwErrorAt stx
      s!"{context} requires matching branch types, got {renderValueType lhsTy} and {renderValueType rhsTy}"

private def requireDeclaredValueType
    (stx : Syntax)
    (context : String)
    (expectedTy actualTy : ValueType) : CommandElabM Unit := do
  unless actualTy == expectedTy || (isWordLikeValueType actualTy && isWordLikeValueType expectedTy) do
    throwErrorAt stx
      s!"{context} expects {renderValueType expectedTy}, got {renderValueType actualTy}"

private def localBindingUsesDynamicData : ValueType → Bool :=
  valueTypeUsesDynamicData

private def requireSupportedLocalBindingType
    (stx : Syntax)
    (context : String)
    (ty : ValueType) : CommandElabM Unit := do
  if localBindingUsesDynamicData ty then
    throwErrorAt stx
      s!"{context} currently cannot bind dynamic values ({renderValueType ty}) to local variables on the compilation-model path; use the parameter directly"

private unsafe def qualifiedTupleBindTypedLocals
    (stx : Syntax)
    (fnName : Name)
    (names : Array (Option String)) : CommandElabM (Array TypedLocal) := do
  let valueTys ← unsafe qualifiedFunctionReturnTypes stx fnName
  if names.size != valueTys.size then
    throwErrorAt stx
      s!"tuple destructuring binds {names.size} names, but qualified helper '{qualifiedFunctionDisplayName fnName}' returns {valueTys.size} values"
  for (name?, ty) in names.zip valueTys do
    if let some name := name? then
      requireSupportedLocalBindingType stx s!"local binding '{name}'" ty
  pure <| (names.zip valueTys).filterMap fun (name?, ty) =>
    name?.map (fun name => mkTypedLocal name ty)

private unsafe def qualifiedSingleBindType
    (stx : Syntax)
    (fnName : Name) : CommandElabM ValueType := do
  let valueTys ← unsafe qualifiedFunctionReturnTypes stx fnName
  match valueTys.toList with
  | [] =>
      throwErrorAt stx
        s!"qualified helper '{qualifiedFunctionDisplayName fnName}' returns Unit and cannot be bound"
  | [retTy] =>
      pure retTy
  | _ =>
      throwErrorAt stx
        s!"qualified helper '{qualifiedFunctionDisplayName fnName}' returns multiple values; use tuple destructuring"

private def customErrorRequiresDirectParamRef : ValueType → Bool
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bool | .bytes32 => false
  | .newtype _ baseType => customErrorRequiresDirectParamRef baseType
  | _ => true

private def directParamRefName? (stx : Term) : Option String :=
  match stripParens stx with
  | `(term| $id:ident) => some (toString id.getId)
  | _ => none

private def validateDirectParamCustomErrorArg
    (arg : Term)
    (fnName errorName : String)
    (params : Array ParamDecl)
    (expectedTy : ValueType)
    (argIdx : Nat) : CommandElabM Unit := do
  match directParamRefName? arg with
  | some name =>
      match params.find? (·.name == name) with
      | some param =>
          unless param.ty == expectedTy do
            throwErrorAt arg
              s!"custom error '{errorName}' arg {argIdx + 1} in function '{fnName}' expects direct parameter reference of type {renderValueType expectedTy}, got parameter '{name}' of type {renderValueType param.ty}"
      | none =>
          throwErrorAt arg
            s!"custom error '{errorName}' arg {argIdx + 1} in function '{fnName}' references unknown parameter '{name}' on the compilation-model path"
  | none =>
      throwErrorAt arg
        s!"custom error '{errorName}' arg {argIdx + 1} in function '{fnName}' currently requires direct parameter reference of type {renderValueType expectedTy} on the compilation-model path"

private def validateCustomErrorCall
    (fnName errorName : String)
    (params : Array ParamDecl)
    (errorDecls : Array ErrorDecl)
    (args : Array Term) : CommandElabM Unit := do
  let errorDecl ←
    match errorDecls.find? (·.name == errorName) with
    | some decl => pure decl
    | none => throwError s!"unknown custom error '{errorName}'"
  unless errorDecl.params.size == args.size do
    throwError s!"custom error '{errorName}' expects {errorDecl.params.size} args, got {args.size}"
  for ((expectedTy, arg), argIdx) in errorDecl.params.zip args |>.zipIdx do
    if customErrorRequiresDirectParamRef expectedTy then
      validateDirectParamCustomErrorArg arg fnName errorName params expectedTy argIdx

private def localFunctionAppSyntax?
    (stx : Term) : Option (String × Array Term) :=
  let stx := stripParens stx
  match stx.raw with
  | .node _ `Lean.Parser.Term.app args =>
      match args.getD 0 Syntax.missing with
      | .ident _ raw _ _ =>
          let argTerms := (args.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
          some (raw.toString, argTerms)
      | _ => none
  | .ident _ raw _ _ =>
      some (raw.toString, #[])
  | _ => none

/-! ### Higher-order internal helper monomorphization (#1747)

    Verity's `CompilationModel` has no first-class function-pointer values: an
    internal helper cannot receive another helper as a runtime argument.  Rather
    than extend the model, IR and proof stack with a closure representation, we
    *eliminate* higher-order internal calls with a compile-time monomorphization
    pre-pass over the parsed `FunctionDecl` array, run before any model/IR
    lowering or elaboration.

    A *higher-order* (HO) helper has at least one function-pointer parameter
    (recorded as `ParamDecl.funcPtr?`).  For every call site that passes a
    statically-known internal-helper name in a function-pointer position, we
    synthesize a specialized first-order clone of the HO helper — with the
    function-pointer parameters removed and each occurrence replaced by the
    concrete helper — and rewrite the call site to target the clone.  The
    original HO helpers are dropped.  The result contains only first-order
    helpers, so 100% of the existing machinery applies unchanged, and there is
    zero overhead (the pre-pass is a no-op) when no helper is higher-order.

    Specialization runs to a fixpoint: a clone body may itself forward a
    function pointer to another HO helper, which requires a further clone.

    Restrictions (a clear error is raised otherwise):
      * a function-pointer argument must be a statically-known internal helper
        name (no dynamic dispatch, no qualified cross-contract names);
      * a function-pointer parameter may only be *called*, never otherwise used
        or shadowed within the helper body. -/

private def paramIsFuncPtr (p : ParamDecl) : Bool := p.funcPtr?.isSome

private def functionIsHigherOrder (fn : FunctionDecl) : Bool :=
  fn.params.any paramIsFuncPtr

/-- Indices of the function-pointer parameters of `fn`, in declaration order. -/
private def funcPtrParamIndices (fn : FunctionDecl) : Array Nat :=
  fn.params.zipIdx.filterMap (fun (p, i) => if paramIsFuncPtr p then some i else none)

/-- The printed name of `stx` when it is a bare (parenthesis-stripped)
    identifier — i.e. a candidate statically-known helper reference. -/
private def bareIdentName? (stx : Term) : Option String :=
  match (stripParens stx).raw with
  | .ident _ _ val _ => some val.toString
  | _ => none

private def monoSpecBaseName (hoName : String) (concretes : Array String) : String :=
  concretes.foldl (fun acc c => acc ++ "_" ++ c) (hoName ++ "_mono")

/-- Replace every identifier whose macro-scope-erased name is a key of `subst`
    with an identifier carrying the mapped name (preserving source position).
    Used to substitute a function-pointer parameter by its concrete helper. -/
private partial def substituteIdentNames (subst : Array (Name × Name)) (stx : Syntax) : Syntax :=
  match stx with
  | .ident _ _ val _ =>
      match subst.find? (fun (frm, _) => frm == val.eraseMacroScopes) with
      | some (_, repl) => (mkIdentFrom stx repl).raw
      | none => stx
  | .node info kind args => .node info kind (args.map (substituteIdentNames subst))
  | other => other

/-- Build a Verity-EDSL call term `head arg₀ … argₙ` (application by
    juxtaposition), matching the shape `localFunctionAppSyntax?` decodes. -/
private def mkLocalCallTerm (head : Ident) (args : Array Term) : Term :=
  if args.isEmpty then ⟨head.raw⟩
  else ⟨Syntax.node .none `Lean.Parser.Term.app
        #[head.raw, Lean.mkNullNode (args.map (·.raw))]⟩

private structure MonoState where
  /-- Assigned specialized name for each `(HO helper, concrete helper names)`
      key, so identical call sites share a single clone. -/
  assigned : Array ((String × Array String) × String) := #[]
  /-- All function names in use, to keep synthesized names fresh. -/
  usedNames : Array String := #[]
  /-- Specializations still to generate: `(specName, hoHelper, concreteNames)`. -/
  pending : Array (String × FunctionDecl × Array String) := #[]

private def monoSpecNameFor
    (ref : IO.Ref MonoState) (hoFn : FunctionDecl) (concretes : Array String) :
    CommandElabM String := do
  let st ← ref.get
  let key := (hoFn.name, concretes)
  match st.assigned.find? (fun (k, _) => k == key) with
  | some (_, nm) => pure nm
  | none =>
      let nm := Compiler.CompilationModel.pickFreshName
        (monoSpecBaseName hoFn.name concretes) st.usedNames.toList
      ref.set { st with
        assigned := st.assigned.push (key, nm)
        usedNames := st.usedNames.push nm
        pending := st.pending.push (nm, hoFn, concretes) }
      pure nm

/-- Rewrite every higher-order call in a syntax tree to its monomorphic
    specialization, enqueuing the specializations that must be generated.
    Bottom-up, so nested higher-order calls are specialized first. -/
private partial def monoRewriteCalls
    (hoHelpers : Array FunctionDecl) (allHelperNames : Array String)
    (ref : IO.Ref MonoState) (stx : Syntax) : CommandElabM Syntax := do
  match stx with
  | .node info kind args =>
      let args ← args.mapM (monoRewriteCalls hoHelpers allHelperNames ref)
      let node := Syntax.node info kind args
      match localFunctionAppSyntax? ⟨node⟩ with
      | none => pure node
      | some (fnName, argTerms) =>
          match hoHelpers.find? (fun fn => fn.name == fnName) with
          | none => pure node
          | some hoFn =>
              unless argTerms.size == hoFn.params.size do
                throwErrorAt stx s!"#1747: call to higher-order helper '{fnName}' expects {hoFn.params.size} argument(s), got {argTerms.size}"
              let fpIdxs := funcPtrParamIndices hoFn
              let mut concretes : Array String := #[]
              for i in fpIdxs do
                match argTerms[i]? with
                | none =>
                    throwErrorAt stx s!"#1747: internal error: missing argument {i} specializing '{fnName}'"
                | some arg =>
                    match bareIdentName? arg with
                    | none =>
                        throwErrorAt arg "#1747: a higher-order argument must be a statically-known internal helper name (no expressions, runtime values, or qualified cross-contract names)"
                    | some nm =>
                        unless allHelperNames.contains nm do
                          throwErrorAt arg s!"#1747: function-pointer argument '{nm}' is not a known internal helper in this contract"
                        concretes := concretes.push nm
              let specName ← monoSpecNameFor ref hoFn concretes
              let keptArgs := argTerms.zipIdx.filterMap (fun (a, i) =>
                if fpIdxs.contains i then none else some a)
              pure (mkLocalCallTerm (mkIdentFrom stx (Name.mkSimple specName)) keptArgs).raw
  | other => pure other

/-- Names of internal helpers (drawn from `allNames`) that `stx` calls. -/
private partial def collectCalleeNames (allNames : Array String) (stx : Syntax) : Array String :=
  match stx with
  | .node _ _ args =>
      let fromArgs := args.foldl (fun acc a => acc ++ collectCalleeNames allNames a) #[]
      match localFunctionAppSyntax? ⟨stx⟩ with
      | some (fnName, _) =>
          if allNames.contains fnName && !fromArgs.contains fnName then fromArgs.push fnName
          else fromArgs
      | none => fromArgs
  | _ => #[]

/-- A helper can be emitted once every internal helper it calls (other than
    itself) has already been emitted — executable emission requires
    callee-before-caller ordering with no forward references. -/
private def funcDeclEmittable
    (allNames : Array String) (emitted : Array String) (fn : FunctionDecl) : Bool :=
  (collectCalleeNames allNames fn.body.raw).all (fun d => d == fn.name || emitted.contains d)

/-- Remove the first emittable helper from `fns`, preserving the order of the
    rest (so the sort is stable). -/
private def pickEmittableFunction (allNames : Array String) (emitted : Array String) :
    List FunctionDecl → Option (FunctionDecl × List FunctionDecl)
  | [] => none
  | fn :: rest =>
      if funcDeclEmittable allNames emitted fn then some (fn, rest)
      else (pickEmittableFunction allNames emitted rest).map (fun (p, r) => (p, fn :: r))

private partial def topoEmitFunctions (allNames : Array String)
    (remaining : List FunctionDecl) (emitted : Array String)
    (output : Array FunctionDecl) : Array FunctionDecl :=
  match pickEmittableFunction allNames emitted remaining with
  | some (fn, rest) => topoEmitFunctions allNames rest (emitted.push fn.name) (output.push fn)
  | none => output ++ remaining.toArray

/-- Stable topological sort placing every internal helper after the helpers it
    calls.  Monomorphization can introduce a clone that depends on a first-order
    helper yet is itself called by a first-order helper, so neither "clones
    first" nor "clones last" yields a valid executable emission order; this sort
    does.  Genuine recursive cycles fall back to original order. -/
private def orderFunctionsByInternalCalls (fns : Array FunctionDecl) : Array FunctionDecl :=
  topoEmitFunctions (fns.map (·.name)) fns.toList #[] #[]

/-- Eliminate higher-order internal helpers by compile-time monomorphization
    (#1747).  Returns a first-order-only `FunctionDecl` array; a no-op when no
    helper is higher-order.  See the section comment above for the design. -/
private def monomorphizeHigherOrderHelpers
    (functions : Array FunctionDecl) : CommandElabM (Array FunctionDecl) := do
  let hoHelpers := functions.filter functionIsHigherOrder
  if hoHelpers.isEmpty then
    return functions
  let allHelperNames := functions.map (·.name)
  let ref ← IO.mkRef ({ usedNames := allHelperNames : MonoState })
  -- 1. Rewrite every first-order body; higher-order originals are dropped.
  let mut firstOrder : Array FunctionDecl := #[]
  for fn in functions do
    if functionIsHigherOrder fn then
      continue
    let body ← monoRewriteCalls hoHelpers allHelperNames ref fn.body.raw
    firstOrder := firstOrder.push { fn with body := ⟨body⟩ }
  -- 2. Generate specializations to a fixpoint.
  let mut clones : Array FunctionDecl := #[]
  let mut idx := 0
  for _ in [0:100001] do
    match (← ref.get).pending[idx]? with
    | none => break
    | some (specName, hoFn, concretes) =>
        idx := idx + 1
        let fpIdxs := funcPtrParamIndices hoFn
        let subst : Array (Name × Name) := (fpIdxs.zip concretes).filterMap (fun (i, nm) =>
          match hoFn.params[i]? with
          | some p => some (p.ident.getId.eraseMacroScopes, Name.mkSimple nm)
          | none => none)
        let substituted := substituteIdentNames subst hoFn.body.raw
        let body ← monoRewriteCalls hoHelpers allHelperNames ref substituted
        let cloneParams := hoFn.params.zipIdx.filterMap (fun (p, i) =>
          if fpIdxs.contains i then none else some p)
        clones := clones.push { hoFn with
          ident := mkIdentFrom hoFn.ident (Name.mkSimple specName)
          name := specName
          params := cloneParams
          body := ⟨body⟩ }
  if idx < (← ref.get).pending.size then
    throwError "#1747: higher-order monomorphization exceeded the specialization budget; this usually indicates an unsupported unbounded recursive function-pointer pattern"
  -- A clone may depend on a first-order helper while being called by another
  -- first-order helper, so order the combined set by callee-before-caller.
  let result := orderFunctionsByInternalCalls (firstOrder ++ clones)
  if result.any functionIsHigherOrder then
    throwError "#1747 internal error: a higher-order helper survived monomorphization"
  return result

private def internalHelperSpecName
    (functions : Array FunctionDecl)
    (fnName : String) : String :=
  Compiler.CompilationModel.pickFreshName
    (Compiler.CompilationModel.internalFunctionPrefix ++ fnName)
    (functions.map (·.name)).toList

private partial def hasDynamicInternalHelperType (ty : ValueType) : Bool :=
  match ty with
  | .string | .bytes | .array _ => true
  | .fixedArray elemTy _ => hasDynamicInternalHelperType elemTy
  | .tuple elemTys => elemTys.any hasDynamicInternalHelperType
  | .struct _ fields => fields.any (fun field => hasDynamicInternalHelperType field.snd)
  | _ => false

private def supportsInternalHelperParamType (ty : ValueType) : Bool :=
  match ty with
  | .string | .bytes => true
  | .array _ => true
  | .struct _ _ => true
  | .tuple _ => true
  | _ => !hasDynamicInternalHelperType ty

private def supportsInternalHelperSpec (fn : FunctionDecl) : Bool :=
  fn.name != "fallback" &&
    fn.name != "receive" &&
    fn.params.all (fun param => supportsInternalHelperParamType param.ty) &&
    !hasDynamicInternalHelperType fn.returnTy

private def ensureSupportsInternalHelperSpec
    (stx : Syntax)
    (fn : FunctionDecl) : CommandElabM Unit := do
  unless supportsInternalHelperSpec fn do
    throwErrorAt stx
      s!"helper call '{fn.name}' uses a parameter or return type that direct macro helper lowering does not support yet; only static non-fallback/non-receive helpers can be lowered to internal specs"

private def throwPureContextAccessorError (stx : Syntax) (name : String) : CommandElabM α :=
  throwErrorAt stx
    s!"context accessor '{name}' is monadic; use `let x ← {name}` before using it in a pure expression"

mutual
private partial def leanExprAppFnArgs (expr : Expr) : Expr × Array Expr :=
  let rec go (fn : Expr) (argsRev : Array Expr) : Expr × Array Expr :=
    match fn.consumeMData with
    | .app f arg => go f (argsRev.push arg)
    | other => (other, argsRev.reverse)
  go expr #[]

private partial def leanConstName? (expr : Expr) : Option Name :=
  match expr.consumeMData with
  | .const name _ => some name
  | _ => none

private partial def leanConstNameMatches (expr : Expr) (names : List Name) : Bool :=
  match leanConstName? expr with
  | some name => names.contains name
  | none => false

private partial def leanConstNameMatchesStringOrSuffix
    (expr : Expr)
    (names : List String)
    (suffixes : List String) : Bool :=
  match leanConstName? expr with
  | some name =>
      let nameString := name.toString
      names.contains nameString || suffixes.any (fun suffix => nameString.endsWith s!".{suffix}")
  | none => false

private partial def valueTypeFromLeanTypeExpr? (expr : Expr) : Option ValueType :=
  match expr.consumeMData with
  | .const name _ =>
      let nameString := name.toString
      if nameString == "Verity.Uint256" || nameString == "Verity.Core.Uint256" ||
          nameString.endsWith ".Uint256" then
        some .uint256
      else if nameString == "Verity.Int256" || nameString == "Verity.Core.Int256" ||
          nameString.endsWith ".Int256" then
        some .int256
      else if nameString == "Verity.Address" || nameString == "Verity.Core.Address" ||
          nameString.endsWith ".Address" then
        some .address
      else if nameString == "Bool" then
        some .bool
      else if nameString == "String" then
        some .string
      else if nameString == "ByteArray" then
        some .bytes
      else
        none
  | _ => none

private partial def peelForallTypes (type : Expr) : List Expr × Expr :=
  let rec go (remaining : Expr) (acc : List Expr) : List Expr × Expr :=
    match remaining.consumeMData with
    | .forallE _ domain body _ => go body (domain :: acc)
    | other => (acc.reverse, other)
  go type []

private partial def peelLambdaBody (value : Expr) (arity : Nat) : Option Expr :=
  match arity with
  | 0 => some value
  | n + 1 =>
      match value.consumeMData with
      | .lam _ _ body _ => peelLambdaBody body n
      | _ => none

private partial def leanDefAppSyntax? (stx : Term) : Option (Syntax × String × Array Term) :=
  let stx := stripParens stx
  match stx.raw with
  | .node _ `Lean.Parser.Term.app args =>
      match args.getD 0 Syntax.missing with
      | head@(.ident _ _ raw _) =>
          if isQualifiedFunctionName raw then
            none
          else
            let argTerms := (args.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
            some (head, raw.toString, argTerms)
      | _ => none
  | _ => none

private partial def resolveLeanDefName? (head : Syntax) : CommandElabM (Option Name) := do
  try
    pure (some (← liftCoreM <| Lean.Elab.realizeGlobalConstNoOverloadWithInfo head none))
  catch _ =>
    pure none

private partial def leanDefInfo? (name : Name) : CommandElabM (Option DefinitionVal) := do
  match (← getEnv).find? name with
  | some (.defnInfo info) => pure (some info)
  | _ => pure none

private partial def checkLeanDefCallArgs
    (stx : Syntax)
    (fnDisplay : String)
    (argTerms : Array Term)
    (paramTypeExprs : List Expr)
    (argTypes : Array ValueType) : CommandElabM Unit := do
  unless argTerms.size == paramTypeExprs.length do
    throwErrorAt stx
      s!"Lean helper '{fnDisplay}' expects {paramTypeExprs.length} argument(s), got {argTerms.size}"
  for ((argTerm, argTy), expectedExpr) in argTerms.zip argTypes |>.zip paramTypeExprs.toArray do
    let expectedTy ←
      match valueTypeFromLeanTypeExpr? expectedExpr with
      | some ty => pure ty
      | none =>
          throwErrorAt stx
            s!"Lean helper '{fnDisplay}' uses an unsupported parameter type; supported pure helper parameters are Uint256, Int256, Address, Bytes32/Uint256, Bool, String, and Bytes"
    unless argTy == expectedTy || (isNatLiteralTerm argTerm && numericLiteralCompatibleValueType expectedTy) do
      throwErrorAt argTerm
        s!"Lean helper '{fnDisplay}' argument expects {renderValueType expectedTy}, got {renderValueType argTy}"

private partial def inferLeanDefCallType?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term)
    (visitingConstants : List String) : CommandElabM (Option ValueType) := do
  let some (head, fnDisplay, argTerms) := leanDefAppSyntax? stx
    | pure none
  let some fnName ← resolveLeanDefName? head
    | pure none
  let some info ← leanDefInfo? fnName
    | pure none
  let (paramTypeExprs, resultTypeExpr) := peelForallTypes info.type
  let argTypes ← argTerms.mapM
    (inferPureExprType fields constDecls immutableDecls externalDecls params locals · visitingConstants)
  checkLeanDefCallArgs stx.raw fnDisplay argTerms paramTypeExprs argTypes
  match valueTypeFromLeanTypeExpr? resultTypeExpr with
  | some ty =>
      requireSupportedLocalBindingType stx.raw s!"Lean helper '{fnDisplay}' return" ty
      pure (some ty)
  | none =>
      throwErrorAt stx
        s!"Lean helper '{fnDisplay}' uses an unsupported return type; supported pure helper returns are Uint256, Int256, Address, Bytes32/Uint256, and Bool"

private partial def inferPureExprType
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term)
    (visitingConstants : List String := []) : CommandElabM ValueType := do
  let stx := stripParens stx
  let inferContextAccessorOrLocal (name : String) : CommandElabM ValueType := do
    match locals.findSome? (fun localDecl =>
        if matchesBareName localDecl.name name then some localDecl.ty else none)
        <|> params.findSome? (fun p =>
          if matchesBareName p.name name then some p.ty else none) with
    | some ty => pure ty
    | none => throwPureContextAccessorError stx name
  if let some (_, index, fieldTy, _, _) := arrayElementStructProjection? params stx then
    requireWordLikeType index "arrayElement index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
    pure fieldTy
  else
  if let some (_, index, fieldTy, _, _) := localArrayElementStructProjection? locals stx then
    requireWordLikeType index "arrayElement alias index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
    pure fieldTy
  else
  if let some (_, index, fieldTy, _, _) := arrayElementDynamicMemberProjection? params stx then
    requireWordLikeType index "arrayElement index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
    pure fieldTy
  else
  if let some (_, index, fieldTy, _, _) := localArrayElementDynamicMemberProjection? locals stx then
    requireWordLikeType index "arrayElement alias index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
    pure fieldTy
  else
  if let some (_, index, fieldTy, _, _) := localArrayElementStaticCompositeProjection? locals stx then
    requireWordLikeType index "arrayElement alias index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
    pure fieldTy
  else
  if let some (_, fieldTy, _) := paramDynamicHeadProjection? params stx then
    pure fieldTy
  else
  if let some (_, fieldTy, _) := paramDynamicStaticCompositeProjection? params stx then
    pure fieldTy
  else
  if let some (_, fieldTy, _) := paramDynamicMemberProjection? params stx then
    pure fieldTy
  else
  if isParamStructNonLeafProjection params stx then
    throwStructNonLeafProjectionError stx
  else
  if let some (_, ty) := paramStructProjection? params stx then
    pure ty
  else
  if let some (_, ty) := localExternalStaticStructProjection? locals stx then
    pure ty
  else
  match stx with
  | `(term| true) | `(term| false) => pure .bool
  | `(term| constructorArg $idx:num) =>
      match params[(← natFromSyntax idx)]? with
      | some param => pure param.ty
      | none => throwErrorAt stx s!"constructorArg index {idx.raw.reprint.getD ""} is out of bounds"
  | `(term| abiHeadWord $target:term $wordOffset:num) => do
      let offset ← natFromSyntax wordOffset
      let (index?, ty) ←
        match abiHeadWordTarget? params target with
        | some (_, index?, ty) => pure (index?, ty)
        | none =>
            match localArrayElementAlias? locals target with
            | some (_, index, ty) => pure (some index, ty)
            | none => throwErrorAt target "abiHeadWord requires a direct parameter, dynamic local alias, or `arrayElement <param> <index>` target"
      if let some index := index? then
        requireWordLikeType index "arrayElement index"
          (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
      let headWords? :=
        if valueTypeUsesDynamicData ty then
          abiLocalHeadWordCount? ty
        else
          staticAbiWordCount? ty
      match headWords? with
      | some headWords =>
          unless offset < headWords do
            throwErrorAt stx s!"abiHeadWord offset {offset} is out of bounds for target with {headWords} ABI head word(s)"
      | none =>
          throwErrorAt target s!"abiHeadWord target type has no ABI head-word layout: {renderValueType ty}"
      pure .uint256
  | `(term| abiEncode $target:term) => do
      let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals target visitingConstants
      match staticAbiWordCount? ty with
      | some _ => pure .uint256
      | none => throwErrorAt target s!"abiEncode requires a static ABI value, got {renderValueType ty}"
  | `(term| Verity.msgSender) =>
      throwPureContextAccessorError stx "msgSender"
  | `(term| Verity.msgValue) =>
      throwPureContextAccessorError stx "msgValue"
  | `(term| Verity.selfBalance) =>
      throwPureContextAccessorError stx "selfBalance"
  | `(term| Verity.blockTimestamp) =>
      throwPureContextAccessorError stx "blockTimestamp"
  | `(term| Verity.blockNumber) =>
      throwPureContextAccessorError stx "blockNumber"
  | `(term| Verity.blobbasefee) =>
      throwPureContextAccessorError stx "blobbasefee"
  | `(term| Verity.chainid) =>
      throwPureContextAccessorError stx "chainid"
  | `(term| Verity.contractAddress) =>
      throwPureContextAccessorError stx "contractAddress"
  | `(term| $id:ident) =>
      let name := toString id.getId
      match params.findSome? (fun p => if p.name == name then some p.ty else none)
          <|> tupleParamElemType? params name
          <|> lookupTypedLocalType? locals name
          <|> immutableDecls.findSome? (fun imm =>
                if declaredNameMatches name imm.name then some imm.ty else none)
          <|> constDecls.findSome? (fun constant =>
                if declaredNameMatches name constant.name then some constant.ty else none) with
      | some ty => pure ty
      | none =>
          if matchesBareName name "calldatasize" || matchesBareName name "returndataSize" then
            pure .uint256
          else if matchesBareName name "zeroAddress" then
            pure .address
          else
            match contextAccessorBareName? name with
            | some accessor => inferContextAccessorOrLocal accessor
            | none => throwErrorAt stx s!"unknown variable '{name}'"
  | `(term| calldatasize) | `(term| returndataSize) =>
      pure .uint256
  | `(term| zeroAddress) =>
      match lookupTypedLocalType? locals "zeroAddress" <|> params.findSome? (fun p =>
        if p.name == "zeroAddress" then some p.ty else none) with
      | some ty => pure ty
      | none => pure .address
  | `(term| isZeroAddress $a:term) =>
      requireWordLikeType a "isZeroAddress" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .bool
  | `(term| wordToAddress $a:term) =>
      requireWordLikeType a "wordToAddress" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .address
  | `(term| addressToWord $a:term) =>
      let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      unless ty == .address do
        throwErrorAt a s!"addressToWord requires Address, got {renderValueType ty}"
      pure .uint256
  | `(term| toInt256 $a:term) => do
      requireWordLikeType a "toInt256" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .int256
  | `(term| toUint256 $a:term) => do
      requireWordLikeType a "toUint256" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .uint256
  | `(term| boolToWord $a:term) =>
      requireBoolType a "boolToWord" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .uint256
  | `(term| $n:num) =>
      pure .uint256
  | `(term| add $a $b) | `(term| sub $a $b) | `(term| mul $a $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      classifyWordArithmeticResultType stx "word arithmetic" lhsTy rhsTy
  | `(term| bitAnd $a $b)
    | `(term| bitOr $a $b) | `(term| bitXor $a $b) | `(term| and $a $b)
    | `(term| or $a $b) | `(term| xor $a $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      classifyUnsignedWordArithmeticResultType stx "bitwise word arithmetic" lhsTy rhsTy
  | `(term| pow $a $b) | `(term| $a ^ $b)
  | `(term| min $a $b) | `(term| max $a $b) | `(term| wMulDown $a $b) | `(term| wDivUp $a $b)
  | `(term| ceilDiv $a $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      classifyUnsignedWordArithmeticResultType stx "unsigned word arithmetic" lhsTy rhsTy
  | `(term| div $a $b) | `(term| $a / $b) | `(term| mod $a $b) | `(term| $a % $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      classifyWordArithmeticResultType stx "division/modulo" lhsTy rhsTy
  | `(term| sdiv $a $b) | `(term| smod $a $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      classifyUnsignedWordArithmeticResultType stx "signed builtin arithmetic" lhsTy rhsTy
  | `(term| bitNot $a) | `(term| not $a) => do
      let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      requireWordLikeType a "bitwise not" ty
      pure .uint256
  | `(term| shl $shift $value) | `(term| shr $shift $value) | `(term| sar $shift $value)
    | `(term| signextend $shift $value) => do
      requireWordLikeType shift "shift" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals shift visitingConstants)
      let valueTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value visitingConstants
      requireWordLikeType value "shift" valueTy
      pure .uint256
  | `(term| byte $index $value) => do
      requireWordLikeType index "byte index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
      let valueTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value visitingConstants
      requireWordLikeType value "byte value" valueTy
      pure .uint256
  | `(term| slt $a $b) | `(term| sgt $a $b) => do
      requireWordLikeType a "signed ordering comparison" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      requireWordLikeType b "signed ordering comparison" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants)
      pure .bool
  | `(term| $a == $b) | `(term| $a != $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      requireEqComparableTypes stx lhsTy rhsTy
      pure .bool
  | `(term| $a >= $b) | `(term| $a > $b) | `(term| $a < $b) | `(term| $a <= $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      discard <| classifyWordArithmeticResultType stx "ordering comparison" lhsTy rhsTy
      pure .bool
  | `(term| $a && $b) | `(term| $a || $b) => do
      requireBoolType a "logical operator" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      requireBoolType b "logical operator" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants)
      pure .bool
  | `(term| logicalAnd $a $b) | `(term| logicalOr $a $b) => do
      requireWordLikeType a "logical word operator" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      requireWordLikeType b "logical word operator" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b visitingConstants)
      pure .uint256
  | `(term| logicalNot $a) => do
      requireWordLikeType a "logical word operator" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .uint256
  | `(term| ! $a) => do
      requireBoolType a "logical not" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a visitingConstants)
      pure .bool
  | `(term| mload $offset) | `(term| tload $offset) | `(term| calldataload $offset)
    | `(term| extcodesize $offset) => do
      requireWordLikeType offset "word offset expression" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals offset visitingConstants)
      pure .uint256
  | `(term| keccak256 $offset $size) => do
      requireWordLikeType offset "keccak256" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals offset visitingConstants)
      requireWordLikeType size "keccak256" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals size visitingConstants)
      pure .uint256
  | `(term| call $gas $target $value $inOffset $inSize $outOffset $outSize) => do
      for arg in [gas, target, value, inOffset, inSize, outOffset, outSize] do
        requireWordLikeType arg "low-level call" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg visitingConstants)
      pure .uint256
  | `(term| staticcall $gas $target $inOffset $inSize $outOffset $outSize)
    | `(term| delegatecall $gas $target $inOffset $inSize $outOffset $outSize) => do
      for arg in [gas, target, inOffset, inSize, outOffset, outSize] do
        requireWordLikeType arg "low-level call" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg visitingConstants)
      pure .uint256
  | `(term| arrayLength $name:term) =>
      -- verity#1849, G1: accept `arrayLength (arrayElement <param> <i>).<dynField>`
      -- projections where `dynField` is a dynamic member of the struct-array
      -- element type. Lowers through `Expr.arrayElementDynamicMemberLength`.
      if let some _ := arrayElementDynamicMemberProjection? params name then
        pure .uint256
      else if let some _ := localArrayElementDynamicMemberProjection? locals name then
        pure .uint256
      else if let some _ := localMemoryArray? locals name then
        pure .uint256
      else if let some _ := paramDynamicMemberProjection? params name then
        pure .uint256
      else
        match lookupNamedValueType? constDecls immutableDecls params locals (← expectStringOrIdent name) with
        | some (.array _) => pure .uint256
        | some ty => throwErrorAt name s!"arrayLength expects an Array value, got {renderValueType ty}"
        | none => throwErrorAt name "unknown array value"
  | `(term| arrayElement $name:term $index:term) => do
      requireWordLikeType index "arrayElement index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index visitingConstants)
      -- verity#1849, G2: accept `arrayElement (arrayElement <param> <i>).<dynField> <k>`
      -- when `<dynField>` is `Array <wordLike>`. Returns the word-like element type.
      if let some (_, _, fieldTy, _, _) := arrayElementDynamicMemberProjection? params name then
        match fieldTy with
        | .array elemTy =>
            if isSingleWordStaticValueType elemTy then
              pure elemTy
            else
              throwErrorAt name s!"arrayElement on a dynamic member of a struct-array element currently supports only Array<wordLike> members, got Array {renderValueType elemTy}"
        | _ =>
            throwErrorAt name s!"arrayElement on a struct-array element projection requires an Array-typed dynamic member, got {renderValueType fieldTy}"
      else if let some (_, _, fieldTy, _, _) := localArrayElementDynamicMemberProjection? locals name then
        match fieldTy with
        | .array elemTy =>
            if isSingleWordStaticValueType elemTy then
              pure elemTy
            else
              throwErrorAt name s!"arrayElement on a dynamic member of an aliased struct-array element currently supports only Array<wordLike> members, got Array {renderValueType elemTy}"
        | _ =>
            throwErrorAt name s!"arrayElement on an aliased struct-array element projection requires an Array-typed dynamic member, got {renderValueType fieldTy}"
      else if let some (_, fieldTy, _) := paramDynamicMemberProjection? params name then
        match fieldTy with
        | .array elemTy =>
            if isSingleWordStaticValueType elemTy then
              pure elemTy
            else
              throwErrorAt name s!"arrayElement on a dynamic member of a struct parameter currently supports only Array<wordLike> members, got Array {renderValueType elemTy}"
        | _ =>
            throwErrorAt name s!"arrayElement on a struct parameter projection requires an Array-typed dynamic member, got {renderValueType fieldTy}"
      else if let some (_, elemTy) := localMemoryArray? locals name then
        if isSingleWordStaticValueType elemTy then
          pure elemTy
        else
          throwErrorAt name s!"arrayElement on a memory array local currently supports only Array<wordLike> values, got Array {renderValueType elemTy}"
      else
        let sourceTy ← requireDirectParamRef name "arrayElement" params
        match sourceTy with
        | .array elemTy =>
            match elemTy with
            | .struct _ _ | .tuple _ =>
              if valueTypeUsesDynamicData elemTy then
                pure elemTy
              else
                requireSupportedArrayElementSourceType name "arrayElement" sourceTy
            | _ =>
              requireSupportedArrayElementSourceType name "arrayElement" sourceTy
        | _ =>
            requireSupportedArrayElementSourceType name "arrayElement" sourceTy
  | `(term| mulDivDown $a $b $c) | `(term| mulDivUp $a $b $c) => do
      for arg in [a, b, c] do
        requireWordLikeType arg "mulDiv" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg visitingConstants)
      pure .uint256
  | `(term| mulDiv512Down $a $b $c) | `(term| mulDiv512Up $a $b $c) => do
      for arg in [a, b, c] do
        requireWordLikeType arg "mulDiv512" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg visitingConstants)
      pure .uint256
  | `(term| ite $cond $thenVal $elseVal) => do
      requireBoolType cond "ite condition" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals cond visitingConstants)
      let thenTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals thenVal visitingConstants
      let elseTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals elseVal visitingConstants
      requireSameOrWordLikeTypes stx "ite" thenTy elseTy
      pure thenTy
  | `(term| externalCall $name:term $args:term) =>
      let extName := ← expectStringOrIdent name
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            -- verity#1849, G3: accept `Array <wordLike>` / `bytes` / `string`
            -- direct param refs alongside word-like args.
            if let some (_, _, fieldTy, _elemTy, _) := arrayElementDynamicMemberProjection? params x then
              match fieldTy with
              | .array elemTy =>
                  unless externalCallDynamicArgSupported (.array elemTy) do
                    throwErrorAt x s!"externalCall '{extName}' dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt x s!"externalCall '{extName}' dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
            else if let some (_, _, fieldTy, _elemTy, _) := localArrayElementDynamicMemberProjection? locals x then
              match fieldTy with
              | .array elemTy =>
                  unless externalCallDynamicArgSupported (.array elemTy) do
                    throwErrorAt x s!"externalCall '{extName}' dynamic-member alias argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt x s!"externalCall '{extName}' dynamic-member alias argument requires an Array-typed member, got {renderValueType fieldTy}"
            else if let some (_, fieldTy, _) := paramDynamicMemberProjection? params x then
              match fieldTy with
              | .array elemTy =>
                  unless externalCallDynamicArgSupported (.array elemTy) do
                    throwErrorAt x s!"externalCall '{extName}' dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt x s!"externalCall '{extName}' dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
            else
              requireWordOrDirectArrayType x s!"externalCall '{extName}' argument" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x visitingConstants)
      | _ => throwErrorAt args "expected list literal [..]"
      match externalDecls.find? (fun ext => ext.name == extName) with
      | some ext =>
          match ext.returnTys.toList with
          | [retTy] => pure retTy
          | [] => throwErrorAt name s!"externalCall '{extName}' returns no values; use `let success ← tryExternalCall \"{extName}\" [...]` instead"
          | _ => throwErrorAt name s!"externalCall '{extName}' returns {ext.returnTys.size} values; use `let (success, ...) ← tryExternalCall \"{extName}\" [...]` for multi-return"
      | none => pure .uint256
  | `(term| intrinsic_cancun $name:term $_lowering:term $args:term)
  | `(term| intrinsic_prague $name:term $_lowering:term $args:term)
  | `(term| intrinsic_fusaka $name:term $_lowering:term $args:term)
  | `(term| intrinsic_osaka $name:term $_lowering:term $args:term)
  | `(term| intrinsic $name:term $_lowering:term $args:term) =>
      let _ := ← expectStringOrIdent name
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            requireWordLikeType x "intrinsic argument"
              (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x visitingConstants)
          pure .uint256
      | _ => throwErrorAt args "expected list literal [..]"
  | `(term| fork_if_at_least $fork:ident then $thenExpr:term else $elseExpr:term) =>
      let thenTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals thenExpr visitingConstants
      let elseTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals elseExpr visitingConstants
      unless thenTy == elseTy do
        throwErrorAt elseExpr
          s!"fork_if_at_least branches must have the same type, got {renderValueType thenTy} and {renderValueType elseTy}"
      let _ ← hardForkTermFromIdent fork
      pure thenTy
  | `(term| structMember $field:term $key:term $member:term) => do
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let memberDecl ← lookupStructMemberDecl fields fieldName memberName false
      requireWordLikeType key "structMember key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key visitingConstants)
      pure memberDecl.ty
  | `(term| structMember2 $field:term $key1:term $key2:term $member:term) => do
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let memberDecl ← lookupStructMemberDecl fields fieldName memberName true
      requireWordLikeType key1 "structMember2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key1 visitingConstants)
      requireWordLikeType key2 "structMember2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key2 visitingConstants)
      pure memberDecl.ty
  | _ =>
      match qualifiedFunctionAppSyntax? stx with
      | some (fnName, _) =>
          if (nameComponents fnName).head? == some "Verity" then
            throwErrorAt stx "unsupported expression in verity_contract body (see #1003 for planned macro support expansions)"
          else
            throwErrorAt stx
              s!"qualified library helper call '{qualifiedFunctionDisplayName fnName}' is only supported as a monadic bind source; use `let x ← {qualifiedFunctionDisplayName fnName} ...` or tuple destructuring bind syntax"
      | none =>
          match ← inferLeanDefCallType? fields constDecls immutableDecls externalDecls params locals stx visitingConstants with
          | some ty => pure ty
          | none => throwErrorAt stx "unsupported expression in verity_contract body (see #1003 for planned macro support expansions)"

private partial def lookupNamedValueType?
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (name : String) : Option ValueType :=
  params.findSome? (fun p => if p.name == name then some p.ty else none)
    <|> lookupTypedLocalType? locals name
    <|> immutableDecls.findSome? (fun imm => if imm.name == name then some imm.ty else none)
    <|> constDecls.findSome? (fun constant => if constant.name == name then some constant.ty else none)

private partial def inferBindSourceType
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM ValueType := do
  let rhs := stripParens rhs
  match rhs with
  | `(term| getStorage $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar .uint256 => pure .uint256
      | .scalar .int256 => pure .int256
      | .scalar (.newtype ntName (.uint256)) => pure (.newtype ntName .uint256)
      | .scalar (.adt name maxFields) => pure (.adt name maxFields)
      | .scalar (.newtype _ (.address)) => throwErrorAt rhs s!"field '{f.name}' is Address-based newtype; use getStorageAddr"
      | .scalar .address => throwErrorAt rhs s!"field '{f.name}' is Address; use getStorageAddr"
      | .scalar .bool => throwErrorAt rhs s!"field '{f.name}' is Bool; encode as Uint256 and use getStorage"
      | .dynamicArray _ => throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | _ => throwErrorAt rhs s!"field '{f.name}' is a mapping; use getMapping/getMapping2/getMappingN"
  | `(term| getStorageAddr $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar .address => pure .address
      | .scalar (.newtype ntName (.address)) => pure (.newtype ntName .address)
      | .scalar (.newtype _ (.uint256)) => throwErrorAt rhs s!"field '{f.name}' is Uint256-based newtype; use getStorage"
      | .scalar .uint256 => throwErrorAt rhs s!"field '{f.name}' is Uint256; use getStorage"
      | .scalar .bool => throwErrorAt rhs s!"field '{f.name}' is Bool; use getStorage"
      | .dynamicArray _ => throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | _ => throwErrorAt rhs s!"field '{f.name}' is a mapping; use getMapping/getMapping2/getMappingN"
  | `(term| getStorageArrayLength $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ => pure .uint256
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a storage dynamic array"
  | `(term| getStorageArrayElement $field:ident $index:term) => do
      requireWordLikeType index "storage array index" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index)
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray .uint256 => pure .uint256
      | .dynamicArray .address => pure .address
      | .dynamicArray .bool => pure .bool
      | .dynamicArray .uint8 => pure .uint8
      | .dynamicArray .bytes32 => pure .bytes32
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a storage dynamic array"
  | `(term| getMapping $field:ident $key:term) | `(term| getMappingUint $field:ident $key:term)
    | `(term| getMappingWord $field:ident $key:term $_wordOffset:num) => do
      requireWordLikeType key "mapping key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key)
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 | .mappingUintToUint256 => pure .uint256
      | .mappingStruct _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember"
      | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a nested struct mapping; use structMember2"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMappingAddr $field:ident $key:term) | `(term| getMappingUintAddr $field:ident $key:term) => do
      requireWordLikeType key "mapping key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key)
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 | .mappingUintToUint256 => pure .address
      | .mappingStruct _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember"
      | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a nested struct mapping; use structMember2"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMapping2 $field:ident $key1:term $key2:term) => do
      requireWordLikeType key1 "mapping key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key1)
      requireWordLikeType key2 "mapping key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key2)
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mapping2AddressToAddressToUint256 => pure .uint256
      | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a nested struct mapping; use structMember2"
      | .mappingStruct _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember"
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a double mapping"
  | `(term| getMappingN $field:ident $keys:term) => do
      let keyTerms ← expectMappingKeyTerms keys
      for key in keyTerms do
        requireWordLikeType key "mapping key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key)
      let f ← lookupStorageField fields (toString field.getId)
      match storageTypeMappingKeyTypes? f.ty with
      | some keyTypes =>
          if keyTerms.size == keyTypes.length then
            pure .uint256
          else
            throwErrorAt rhs s!"field '{f.name}' expects {keyTypes.length} mapping keys, but getMappingN received {keyTerms.size}"
      | none =>
          match f.ty with
          | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
              throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
          | _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| structMember $field:term $key:term $member:term) => do
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let memberDecl ← lookupStructMemberDecl fields fieldName memberName false
      requireWordLikeType key "structMember key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key)
      pure memberDecl.ty
  | `(term| structMember2 $field:term $key1:term $key2:term $member:term) => do
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let memberDecl ← lookupStructMemberDecl fields fieldName memberName true
      requireWordLikeType key1 "structMember2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key1)
      requireWordLikeType key2 "structMember2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key2)
      pure memberDecl.ty
  | `(term| msgSender) | `(term| Verity.msgSender) =>
      pure .address
  | `(term| msgValue) | `(term| Verity.msgValue) | `(term| selfBalance)
    | `(term| Verity.selfBalance) | `(term| blockTimestamp)
    | `(term| Verity.blockTimestamp) | `(term| blockNumber) | `(term| Verity.blockNumber)
    | `(term| blobbasefee) | `(term| Verity.blobbasefee) | `(term| chainid)
    | `(term| Verity.chainid) =>
      pure .uint256
  | `(term| contractAddress) | `(term| Verity.contractAddress) =>
      pure .address
  | `(term| tload $offset:term) => do
      requireWordLikeType offset "tload offset" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals offset)
      pure .uint256
  | `(term| ecrecover $hash:term $v:term $r:term $s:term) => do
      for arg in [hash, v, r, s] do
        requireWordLikeType arg "ecrecover" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
      pure .address
  | `(term| balanceOf $token:term $owner:term) =>
      for arg in [token, owner] do
        requireWordLikeType arg "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
      pure .uint256
  | `(term| allowance $token:term $owner:term $spender:term) =>
      for arg in [token, owner, spender] do
        requireWordLikeType arg "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
      pure .uint256
  | `(term| totalSupply $token:term) => do
      requireWordLikeType token "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals token)
      pure .uint256
  | `(term| ecmCall $_moduleFactory:term $args:term) => do
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            requireWordOrDirectArrayType x "ECM argument"
              (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x)
      | _ => throwErrorAt args "expected list literal [..]"
      pure .uint256
  | `(term| tryExternalCall $name:term $args:term) => do
      let extName := ← expectStringOrIdent name
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            -- verity#1849, G3: accept `Array <wordLike>` / `bytes` / `string`
            -- direct param refs alongside word-like args.
            if let some (_, _, fieldTy, _elemTy, _) := arrayElementDynamicMemberProjection? params x then
              match fieldTy with
              | .array elemTy =>
                  unless externalCallDynamicArgSupported (.array elemTy) do
                    throwErrorAt x s!"tryExternalCall '{extName}' dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt x s!"tryExternalCall '{extName}' dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
            else if let some (_, _, fieldTy, _elemTy, _) := localArrayElementDynamicMemberProjection? locals x then
              match fieldTy with
              | .array elemTy =>
                  unless externalCallDynamicArgSupported (.array elemTy) do
                    throwErrorAt x s!"tryExternalCall '{extName}' dynamic-member alias argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt x s!"tryExternalCall '{extName}' dynamic-member alias argument requires an Array-typed member, got {renderValueType fieldTy}"
            else
              requireWordOrDirectArrayType x s!"tryExternalCall '{extName}' argument"
                (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x)
      | _ => throwErrorAt args "expected list literal [..]"
      match externalDecls.find? (fun ext => ext.name == extName) with
      | some ext =>
          if ext.returnTys.size > 0 then
            throwErrorAt rhs s!"tryExternalCall '{extName}' returns {ext.returnTys.size} value(s); use tuple destructuring: `let (success, ...) ← tryExternalCall ...`"
          -- Zero-return external: success flag only
          pure .bool
      | none =>
          throwErrorAt rhs s!"unknown external function '{extName}'"
  | `(term| requireSomeUint $optExpr:term $_msg:term) =>
      match stripParens optExpr with
      | `(term| safeAdd $a:term $b:term)
      | `(term| safeSub $a:term $b:term)
      | `(term| safeMul $a:term $b:term)
      | `(term| safeDiv $a:term $b:term) => do
          requireWordLikeType a "safe uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a)
          requireWordLikeType b "safe uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b)
          pure .uint256
      | _ => throwErrorAt rhs "unsupported requireSomeUint source; expected safeAdd, safeSub, safeMul, or safeDiv"
  -- Typed-error counterpart to `requireSomeUint`. `requireSomeUintError
  -- (safeXxx a b) ErrName(args)` validates the same `safeXxx` source shape
  -- and result type; argument-type validation against the contract's
  -- `errors` block happens in the do-element walk before the lowering pass.
  | `(term| requireSomeUintError $optExpr:term $_errorName:ident($_args,*)) =>
      match stripParens optExpr with
      | `(term| safeAdd $a:term $b:term)
      | `(term| safeSub $a:term $b:term)
      | `(term| safeMul $a:term $b:term)
      | `(term| safeDiv $a:term $b:term) => do
          requireWordLikeType a "safe uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a)
          requireWordLikeType b "safe uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b)
          pure .uint256
      | _ => throwErrorAt rhs "unsupported requireSomeUintError source; expected safeAdd, safeSub, safeMul, or safeDiv"
  -- Solidity-0.8 default-revert arithmetic (verity#1752). `addPanic`,
  -- `subPanic`, `mulPanic`, `divPanic` are ergonomic shorthands for the
  -- corresponding `requireSomeUint (safeXxx a b) <fixed Panic-style message>`
  -- pattern.  They match the surface of Solidity's `a + b` / `a - b` / `a * b`
  -- / `a / b` operators on `uint256`, where overflow / underflow / division
  -- by zero reverts with `Panic(0x11)` / `Panic(0x12)` rather than wrapping
  -- mod 2^256.
  | `(term| addPanic $a:term $b:term)
  | `(term| subPanic $a:term $b:term)
  | `(term| mulPanic $a:term $b:term)
  | `(term| divPanic $a:term $b:term) => do
      requireWordLikeType a "panic uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals a)
      requireWordLikeType b "panic uint helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals b)
      pure .uint256
  | _ =>
      match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals rhs with
      | some (fn, _argTerms) =>
          ensureSupportsInternalHelperSpec rhs fn
          match fn.returnTy with
          | .tuple _ =>
              throwErrorAt rhs
                s!"helper call '{fn.name}' returns multiple values; use tuple destructuring"
          | .unit =>
              throwErrorAt rhs
                s!"helper call '{fn.name}' returns Unit and cannot be bound"
          | retTy =>
              pure retTy
      | none =>
          match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
          | some (qualifiedName, _) =>
              unsafe qualifiedSingleBindType rhs.raw qualifiedName
          | none =>
              throwErrorAt rhs
                "unsupported bind source; expected getStorage/getStorageAddr/getStorageArrayLength/getStorageArrayElement/getMapping/getMappingAddr/getMappingUint/getMappingUintAddr/getMappingWord/getMapping2/getMappingN/structMember/structMember2/msgSender/msgValue/selfBalance/tload/ecrecover/ecmCall, a direct internal helper call, or a qualified library helper call"

private partial def inferTupleSourceTypes?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Option (Array ValueType)) := do
  let structCtorTypes? : CommandElabM (Option (Array ValueType)) := do
    match qualifiedFunctionAppSyntax? (stripParens rhs) with
    | some (name, ctorArgs) =>
        if name.toString.endsWith ".mk" then
          pure (some (← ctorArgs.mapM (inferPureExprType fields constDecls immutableDecls externalDecls params locals)))
        else
          pure none
    | none =>
        match (stripParens rhs).raw with
        | .node _ `Lean.Parser.Term.app args =>
            match args.getD 0 Syntax.missing with
            | .ident _ raw _ _ =>
                if raw.toString.endsWith ".mk" then
                  let ctorArgs := (args.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
                  pure (some (← ctorArgs.mapM (inferPureExprType fields constDecls immutableDecls externalDecls params locals)))
                else
                  pure none
            | _ => pure none
        | _ => pure none
  match tupleElemsFromTerm? rhs with
  | some elems =>
      pure <| some (← elems.mapM (inferPureExprType fields constDecls immutableDecls externalDecls params locals))
  | none =>
      match ← structCtorTypes? with
      | some tys => pure (some tys)
      | none =>
        match stripParens rhs with
        | `(term| $id:ident) =>
            match params.find? (fun p => p.name == toString id.getId) with
            | some p =>
                match p.ty with
                | .tuple elemTys => pure (some elemTys.toArray)
                | _ => pure none
            | none => pure none
        | `(term| structMembers $field:term $key:term $members:term) => do
            let fieldName := ← expectStringOrIdent field
            let memberNames := ← expectStringList members
            let memberDecls ← memberNames.mapM fun memberName =>
              lookupStructMemberDecl fields fieldName memberName false
            requireWordLikeType key "structMembers key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key)
            pure (some (memberDecls.map (·.ty)))
        | `(term| structMembers2 $field:term $key1:term $key2:term $members:term) => do
            let fieldName := ← expectStringOrIdent field
            let memberNames := ← expectStringList members
            let memberDecls ← memberNames.mapM fun memberName =>
              lookupStructMemberDecl fields fieldName memberName true
            requireWordLikeType key1 "structMembers2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key1)
            requireWordLikeType key2 "structMembers2 key" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals key2)
            pure (some (memberDecls.map (·.ty)))
        | `(term| arrayElement $name:term $index:term) => do
            requireWordLikeType index "arrayElement index"
              (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index)
            -- verity#1849, G2: `arrayElement (arrayElement <param> <i>).<dynField> <k>`
            -- never destructures into a tuple — fall through to the scalar
            -- return path. The compound projection's name isn't a direct param
            -- ref so `requireDirectParamRef` would throw if invoked here.
            if (arrayElementDynamicMemberProjection? params name).isSome ||
                (localArrayElementDynamicMemberProjection? locals name).isSome then
              pure none
            else
              match directParamNameWithType? params name with
              | none => pure none
              | some _ =>
                  let sourceTy ← requireDirectParamRef name "arrayElement" params
                  match sourceTy with
                  | .array (.tuple elemTys) =>
                      let _ ← requireSupportedArrayElementTupleSourceType name "arrayElement tuple destructuring" sourceTy
                      pure (some elemTys.toArray)
                  | _ => pure none
        | `(term| tryExternalCall $name:term $args:term) =>
            let extName := ← expectStringOrIdent name
            match stripParens args with
            | `(term| [ $[$xs],* ]) =>
                for x in xs do
                  requireWordOrDirectArrayType x s!"tryExternalCall '{extName}' argument"
                    (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x)
            | _ => throwErrorAt args "expected list literal [..]"
            match externalDecls.find? (fun ext => ext.name == extName) with
            | some ext =>
                -- tryExternalCall returns (success : Bool, result₁ : T₁, ..., resultₙ : Tₙ)
                pure (some (#[.bool] ++ ext.returnTys))
            | none =>
                -- When called from translation path with empty externalDecls, return none
                -- to let the tryExternalCallBindStmt? helper handle translation.
                -- The validation path (with real externalDecls) catches actual errors.
                pure none
        | other =>
            match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals other with
            | some (fn, _argTerms) =>
                ensureSupportsInternalHelperSpec rhs fn
                match fn.returnTy with
                | .tuple elemTys => pure (some elemTys.toArray)
                | _ => pure none
            | none => pure none

private partial def resolveLocalFunctionApp?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option (FunctionDecl × Array Term)) := do
  let some (fnName, argTerms) := localFunctionAppSyntax? stx
    | pure none
  let candidates := functions.filter (fun fn => fn.name == fnName && fn.params.size == argTerms.size)
  if candidates.isEmpty then
    pure none
  else
    let argTypes ← argTerms.mapM (inferPureExprType fields constDecls immutableDecls externalDecls params locals)
    let exactMatchedFns := candidates.filter (fun fn =>
      fn.params.map (fun param => param.ty) == argTypes)
    let literalCompatibleFns :=
      if exactMatchedFns.isEmpty then
        candidates.filter (fun fn => argumentTypesMatchParams argTerms argTypes fn.params)
      else
        exactMatchedFns
    match literalCompatibleFns.toList with
    | [fn] => pure (some (fn, argTerms))
    | [] =>
        let expected :=
          String.intercalate ", "
            (candidates.toList.map functionSignatureKey)
        let actual :=
          fnName ++ "(" ++ String.intercalate "," (argTypes.toList.map renderValueType) ++ ")"
        throwErrorAt stx
          s!"no overload of '{fnName}' matches argument types {actual}; candidates: {expected}"
    | _ =>
        throwErrorAt stx
          s!"ambiguous overload resolution for '{fnName}'"

private partial def localInternalArrayReturnBind?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Option (FunctionDecl × Array Term × ValueType)) := do
  match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals rhs with
  | some (fn, argTerms) =>
      unless fn.name != "fallback" &&
          fn.name != "receive" &&
          fn.params.all (fun param => supportsInternalHelperParamType param.ty) do
        throwErrorAt rhs
          s!"helper call '{fn.name}' uses a parameter or return type that direct macro helper lowering does not support yet; only static non-fallback/non-receive helpers can be lowered to internal specs"
      match fn.returnTy with
      | .array elemTy =>
          if isSingleWordStaticValueType elemTy then
            pure (some (fn, argTerms, elemTy))
          else
            throwErrorAt rhs
              s!"local binding from helper '{fn.name}' returning Array currently supports only static ABI-word elements, got Array {renderValueType elemTy}"
      | _ => pure none
  | none => pure none

private partial def resolveQualifiedFunctionApp?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option (Name × Array Term)) := do
  let some (fnName, argTerms) := qualifiedFunctionAppSyntax? stx
    | pure none
  if (nameComponents fnName).head? == some "Verity" then
    pure none
  else
    for arg in argTerms do
      requireWordLikeType arg s!"qualified helper '{qualifiedFunctionDisplayName fnName}' argument"
        (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
    pure (some (fnName, argTerms))
end

mutual
private partial def validateConstantBody
    (constDecls : Array ConstantDecl)
    (stx : Term)
    (visiting : List String := []) : CommandElabM Unit := do
  let stx := stripParens stx
  match stx with
  | `(term| true) => pure ()
  | `(term| false) => pure ()
  | `(term| constructorArg $idx:num) => throwNonCompileTimeConstantError idx "constructorArg"
  | `(term| msgValue) => throwNonCompileTimeConstantError stx "msgValue"
  | `(term| selfBalance) => throwNonCompileTimeConstantError stx "selfBalance"
  | `(term| blockTimestamp) => throwNonCompileTimeConstantError stx "blockTimestamp"
  | `(term| blockNumber) => throwNonCompileTimeConstantError stx "blockNumber"
  | `(term| blobbasefee) | `(term| Verity.blobbasefee) =>
      throwNonCompileTimeConstantError stx "blobbasefee"
  | `(term| contractAddress) => throwNonCompileTimeConstantError stx "contractAddress"
  | `(term| chainid) => throwNonCompileTimeConstantError stx "chainid"
  | `(term| calldatasize) => throwNonCompileTimeConstantError stx "calldatasize"
  | `(term| returndataSize) => throwNonCompileTimeConstantError stx "returndataSize"
  | `(term| zeroAddress) => pure ()
  | `(term| isZeroAddress $a:term) => validateConstantBody constDecls a visiting
  | `(term| wordToAddress $a:term) => validateConstantBody constDecls a visiting
  | `(term| addressToWord $a:term) => validateConstantBody constDecls a visiting
  | `(term| toInt256 $a:term) => validateConstantBody constDecls a visiting
  | `(term| toUint256 $a:term) => validateConstantBody constDecls a visiting
  | `(term| boolToWord $a:term) => validateConstantBody constDecls a visiting
  | `(term| $id:ident) =>
      let name := toString id.getId
      match constDecls.find? (fun c => c.name == name) with
      | none => throwErrorAt stx s!"unknown variable '{name}'"
      | some constant =>
          if visiting.contains name then
            throwErrorAt stx s!"constant '{name}' depends on itself recursively"
          validateConstantBody constDecls constant.body (name :: visiting)
  | `(term| $n:num) => pure ()
  | `(term| add $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| sub $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| mul $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| div $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| sdiv $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| mod $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| smod $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| bitAnd $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| bitOr $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| bitXor $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| bitNot $a) => validateConstantBody constDecls a visiting
  | `(term| shl $shift $value) => validateConstantBody constDecls shift visiting *> validateConstantBody constDecls value visiting
  | `(term| shr $shift $value) => validateConstantBody constDecls shift visiting *> validateConstantBody constDecls value visiting
  | `(term| $a == $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a != $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a >= $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a > $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a < $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a <= $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a && $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| $a || $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| logicalAnd $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| logicalOr $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| logicalNot $a) => validateConstantBody constDecls a visiting
  | `(term| and $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| or $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| xor $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| not $a) => validateConstantBody constDecls a visiting
  | `(term| mload $offset) => throwNonCompileTimeConstantError offset "mload"
  | `(term| tload $offset) => throwNonCompileTimeConstantError offset "tload"
  | `(term| calldataload $offset) => throwNonCompileTimeConstantError offset "calldataload"
  | `(term| extcodesize $addr) => throwNonCompileTimeConstantError addr "extcodesize"
  | `(term| keccak256 $offset $_size) => throwNonCompileTimeConstantError offset "keccak256"
  | `(term| call $gas $_target $_value $_inOffset $_inSize $_outOffset $_outSize) =>
      throwNonCompileTimeConstantError gas "call"
  | `(term| staticcall $gas $_target $_inOffset $_inSize $_outOffset $_outSize) =>
      throwNonCompileTimeConstantError gas "staticcall"
  | `(term| delegatecall $gas $_target $_inOffset $_inSize $_outOffset $_outSize) =>
      throwNonCompileTimeConstantError gas "delegatecall"
  | `(term| byte $index $word) => validateConstantBody constDecls index visiting *> validateConstantBody constDecls word visiting
  | `(term| addmod $a $b $c) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting *> validateConstantBody constDecls c visiting
  | `(term| mulmod $a $b $c) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting *> validateConstantBody constDecls c visiting
  | `(term| signextend $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| sar $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| slt $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| sgt $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| min $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| max $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| ceilDiv $a $b) => validateConstantBody constDecls a visiting *> validateConstantBody constDecls b visiting
  | `(term| ite $cond $thenVal $elseVal) =>
      validateConstantBody constDecls cond visiting *>
      validateConstantBody constDecls thenVal visiting *>
      validateConstantBody constDecls elseVal visiting
  | _ => throwErrorAt stx "unsupported expression in contract constant"

private partial def translateConstantExpr
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (visiting : List String)
    (name : String) : CommandElabM Term := do
  match constDecls.find? (fun c => c.name == name) with
  | none => throwError s!"unknown variable '{name}'"
  | some constant =>
      if visiting.contains name then
        throwError s!"constant '{name}' depends on itself recursively"
      translatePureExprWithTypes fields constDecls immutableDecls #[] #[] constant.body (name :: visiting)

private partial def translateLeanExprFromDef
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (origin : Syntax)
    (fnDisplay : String)
    (argExprs : Array Term)
    (expr : Expr) : CommandElabM Term := do
  match expr.consumeMData with
  | .bvar idx =>
      if idx < argExprs.size then
        let argIdx := argExprs.size - 1 - idx
        match argExprs[argIdx]? with
        | some arg => pure arg
        | none => throwErrorAt origin s!"Lean helper '{fnDisplay}' body references an out-of-scope argument"
      else
        throwErrorAt origin s!"Lean helper '{fnDisplay}' body references an out-of-scope argument"
  | .lit (.natVal n) =>
      `(Compiler.CompilationModel.Expr.literal $(natTerm n))
  | .const ``Bool.true _ =>
      `(Compiler.CompilationModel.Expr.literal 1)
  | .const ``Bool.false _ =>
      `(Compiler.CompilationModel.Expr.literal 0)
  | other =>
      let (fn, args) := leanExprAppFnArgs other
      if leanConstNameMatches fn [``OfNat.ofNat] then
        match args.toList with
        | [_ty, .lit (.natVal n), _inst] =>
            `(Compiler.CompilationModel.Expr.literal $(natTerm n))
        | _ =>
            throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported numeric literal form"
      else if leanConstNameMatches fn [``ite] then
        match args.toList with
        | [_ty, cond, _dec, thenExpr, elseExpr] =>
            `(Compiler.CompilationModel.Expr.ite
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs cond)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs thenExpr)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs elseExpr))
        | _ =>
            throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported if/ite form"
      else if leanConstNameMatchesStringOrSuffix fn
          ["Verity.toInt256", "Verity.toUint256", "Verity.wordToAddress", "Verity.addressToWord"]
          ["toInt256", "toUint256", "wordToAddress", "addressToWord"] then
        match args.toList with
        | [arg] => translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs arg
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported cast form"
      else if leanConstNameMatches fn [``Neg.neg] then
        match args.toList with
        | [_ty, _inst, arg] =>
            `(Compiler.CompilationModel.Expr.sub
                (Compiler.CompilationModel.Expr.literal 0)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs arg))
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported negation form"
      else if leanConstNameMatches fn [``Eq] then
        match args.toList with
        | [_tyExpr, lhs, rhs] =>
            `(Compiler.CompilationModel.Expr.eq
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs lhs)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs rhs))
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported equality form"
      else if leanConstNameMatches fn [``LT.lt, ``LE.le, ``GT.gt, ``GE.ge] then
        match args.toList with
        | [tyExpr, _inst, lhs, rhs] =>
            let lhsExpr ← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs lhs
            let rhsExpr ← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs rhs
            match leanConstName? fn with
            | some ``LT.lt =>
                if valueTypeFromLeanTypeExpr? tyExpr == some .int256 then
                  `(Compiler.CompilationModel.Expr.slt $lhsExpr $rhsExpr)
                else
                  `(Compiler.CompilationModel.Expr.lt $lhsExpr $rhsExpr)
            | some ``GT.gt =>
                if valueTypeFromLeanTypeExpr? tyExpr == some .int256 then
                  `(Compiler.CompilationModel.Expr.sgt $lhsExpr $rhsExpr)
                else
                  `(Compiler.CompilationModel.Expr.gt $lhsExpr $rhsExpr)
            | some ``GE.ge =>
                if valueTypeFromLeanTypeExpr? tyExpr == some .int256 then
                  `(Compiler.CompilationModel.Expr.logicalNot
                      (Compiler.CompilationModel.Expr.slt $lhsExpr $rhsExpr))
                else
                  `(Compiler.CompilationModel.Expr.ge $lhsExpr $rhsExpr)
            | _ =>
                if valueTypeFromLeanTypeExpr? tyExpr == some .int256 then
                  `(Compiler.CompilationModel.Expr.logicalNot
                      (Compiler.CompilationModel.Expr.sgt $lhsExpr $rhsExpr))
                else
                  `(Compiler.CompilationModel.Expr.le $lhsExpr $rhsExpr)
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported comparison form"
      else if leanConstNameMatchesStringOrSuffix fn
          ["Verity.EVM.Uint256.add", "Verity.EVM.Int256.add",
           "Verity.Core.Uint256.add", "Verity.Core.Int256.add"]
          ["add"] then
        match args.toList with
        | [lhs, rhs] =>
            `(Compiler.CompilationModel.Expr.add
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs lhs)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs rhs))
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported add form"
      else if leanConstNameMatchesStringOrSuffix fn
          ["Verity.EVM.Uint256.sub", "Verity.EVM.Int256.sub",
           "Verity.Core.Uint256.sub", "Verity.Core.Int256.sub"]
          ["sub"] then
        match args.toList with
        | [lhs, rhs] =>
            `(Compiler.CompilationModel.Expr.sub
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs lhs)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs rhs))
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported sub form"
      else if leanConstNameMatchesStringOrSuffix fn
          ["Verity.EVM.Uint256.mul", "Verity.EVM.Int256.mul",
           "Verity.Core.Uint256.mul", "Verity.Core.Int256.mul"]
          ["mul"] then
        match args.toList with
        | [lhs, rhs] =>
            `(Compiler.CompilationModel.Expr.mul
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs lhs)
                $(← translateLeanExprFromDef fields constDecls immutableDecls params locals origin fnDisplay argExprs rhs))
        | _ => throwErrorAt origin s!"Lean helper '{fnDisplay}' contains an unsupported mul form"
      else
        throwErrorAt origin
          s!"Lean helper '{fnDisplay}' body contains unsupported expression '{fn}'; inline it or rewrite the helper using supported pure Verity operations"

private partial def translateLeanDefCall?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term)
    (visitingConstants : List String) : CommandElabM (Option Term) := do
  let some (head, fnDisplay, argTerms) := leanDefAppSyntax? stx
    | pure none
  let some fnName ← resolveLeanDefName? head
    | pure none
  let some info ← leanDefInfo? fnName
    | pure none
  let (paramTypeExprs, resultTypeExpr) := peelForallTypes info.type
  let argTypes ← argTerms.mapM
    (inferPureExprType fields constDecls immutableDecls externalDecls params locals · visitingConstants)
  checkLeanDefCallArgs stx.raw fnDisplay argTerms paramTypeExprs argTypes
  match valueTypeFromLeanTypeExpr? resultTypeExpr with
  | some ty => requireSupportedLocalBindingType stx.raw s!"Lean helper '{fnDisplay}' return" ty
  | none =>
      throwErrorAt stx
        s!"Lean helper '{fnDisplay}' uses an unsupported return type; supported pure helper returns are Uint256, Int256, Address, Bytes32/Uint256, and Bool"
  let some body := peelLambdaBody info.value argTerms.size
    | throwErrorAt stx s!"Lean helper '{fnDisplay}' body is not a transparent function definition"
  let argExprs ← argTerms.mapM
    (translatePureExprWithTypes fields constDecls immutableDecls params locals · visitingConstants)
  pure (some (← translateLeanExprFromDef fields constDecls immutableDecls params locals stx.raw fnDisplay argExprs body))

partial def translatePureExprWithTypes
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term)
    (visitingConstants : List String := []) : CommandElabM Term := do
  let stx := stripParens stx
  let localNames := typedLocalNames locals
  let translateIntrinsic (name lowering args minForkTerm : Term) : CommandElabM Term := do
    let intrinsicName := ← expectStringOrIdent name
    let argsExprs ←
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          xs.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals · visitingConstants)
      | _ => throwErrorAt args "expected list literal [..]"
    `(Compiler.CompilationModel.Expr.intrinsic $(strTerm intrinsicName) $lowering
        $minForkTerm [ $[$argsExprs],* ])
  if let some (paramName, index, _fieldTy, elemTy, wordOffset) := arrayElementStructProjection? params stx then
    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
    if valueTypeUsesDynamicData elemTy then
      `(Compiler.CompilationModel.Expr.arrayElementDynamicWord
        $(strTerm paramName)
        $indexExpr
        $(natTerm wordOffset))
    else
      let elementWords ←
        match staticAbiWordCount? elemTy with
        | some n => pure n
        | none => throwErrorAt stx "arrayElement struct projection requires a static or dynamic ABI-decodable element"
      `(Compiler.CompilationModel.Expr.arrayElementWord
        $(strTerm paramName)
        $indexExpr
        $(natTerm elementWords)
        $(natTerm wordOffset))
  else
  if let some (paramName, index, _fieldTy, elemTy, wordOffset) := localArrayElementStructProjection? locals stx then
    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
    if valueTypeUsesDynamicData elemTy then
      `(Compiler.CompilationModel.Expr.arrayElementDynamicWord
        $(strTerm paramName)
        $indexExpr
        $(natTerm wordOffset))
    else
      let elementWords ←
        match staticAbiWordCount? elemTy with
        | some n => pure n
        | none => throwErrorAt stx "arrayElement alias struct projection requires a static or dynamic ABI-decodable element"
      `(Compiler.CompilationModel.Expr.arrayElementWord
        $(strTerm paramName)
        $indexExpr
        $(natTerm elementWords)
        $(natTerm wordOffset))
  else
  -- Direct dynamic-tuple parameter leaf projection (verity#1832): read a
  -- single-word static field at a fixed head offset from a dynamically
  -- encoded struct parameter.
  if let some (paramName, _fieldTy, wordOffset) := paramDynamicHeadProjection? params stx then
    `(Compiler.CompilationModel.Expr.paramDynamicHeadWord
      $(strTerm paramName)
      $(natTerm wordOffset))
  else
  if (paramDynamicMemberProjection? params stx).isSome then
    throwErrorAt stx "dynamic struct parameter member cannot be used as a scalar expression; pass it to a helper/external expecting an Array or use arrayLength/arrayElement"
  else
  if isParamStructNonLeafProjection params stx then
    throwStructNonLeafProjectionError stx
  else
  if let some (paramName, _ty) := paramStructProjection? params stx then
    `(Compiler.CompilationModel.Expr.param $(strTerm paramName))
  else
  if let some (localName, _ty) := localExternalStaticStructProjection? locals stx then
    `(Compiler.CompilationModel.Expr.localVar $(strTerm localName))
  else
  match stx with
  | `(term| true) => `(Compiler.CompilationModel.Expr.literal 1)
  | `(term| false) => `(Compiler.CompilationModel.Expr.literal 0)
  | `(term| constructorArg $idx:num) =>
      `(Compiler.CompilationModel.Expr.constructorArg $idx)
  | `(term| abiHeadWord $target:term $wordOffset:num) => do
      let offset ← natFromSyntax wordOffset
      let (paramName, index?, ty) ←
        match abiHeadWordTarget? params target with
        | some resolved => pure resolved
        | none =>
            match localArrayElementAlias? locals target with
            | some (paramName, index, ty) => pure (paramName, some index, ty)
            | none => throwErrorAt target "abiHeadWord requires a direct parameter, dynamic local alias, or `arrayElement <param> <index>` target"
      match index? with
      | some index =>
          let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
          if valueTypeUsesDynamicData ty then
            `(Compiler.CompilationModel.Expr.arrayElementDynamicWord
              $(strTerm paramName)
              $indexExpr
              $(natTerm offset))
          else
            let elementWords ←
              match staticAbiWordCount? ty with
              | some n => pure n
              | none => throwErrorAt target "abiHeadWord array element target requires a static ABI word layout"
            `(Compiler.CompilationModel.Expr.arrayElementWord
              $(strTerm paramName)
              $indexExpr
              $(natTerm elementWords)
              $(natTerm offset))
      | none =>
          unless valueTypeUsesDynamicData ty do
            throwErrorAt target "abiHeadWord direct parameter targets must have dynamic ABI head layout"
          `(Compiler.CompilationModel.Expr.paramDynamicHeadWord
            $(strTerm paramName)
            $(natTerm offset))
  | `(term| Verity.msgSender) =>
      throwPureContextAccessorError stx "msgSender"
  | `(term| Verity.msgValue) =>
      throwPureContextAccessorError stx "msgValue"
  | `(term| Verity.selfBalance) =>
      throwPureContextAccessorError stx "selfBalance"
  | `(term| Verity.blockTimestamp) =>
      throwPureContextAccessorError stx "blockTimestamp"
  | `(term| Verity.blockNumber) =>
      throwPureContextAccessorError stx "blockNumber"
  | `(term| Verity.blobbasefee) =>
      throwPureContextAccessorError stx "blobbasefee"
  | `(term| Verity.contractAddress) =>
      throwPureContextAccessorError stx "contractAddress"
  | `(term| Verity.chainid) =>
      throwPureContextAccessorError stx "chainid"
  | `(term| $id:ident) =>
      let name := toString id.getId
      if params.any (fun p => p.name == name) || isTupleComponentRef params name || localNames.contains name then
        lookupVarExpr params localNames name
      else if let some actualName := findContextAccessorShadowName? params localNames name then
        lookupVarExpr params localNames actualName
      else if matchesBareName name "calldatasize" then
        `(Compiler.CompilationModel.Expr.calldatasize)
      else if matchesBareName name "returndataSize" then
        `(Compiler.CompilationModel.Expr.returndataSize)
      else if matchesBareName name "zeroAddress" then
        `(Compiler.CompilationModel.Expr.literal 0)
      else if let some imm := immutableDecls.find? (fun imm => declaredNameMatches name imm.name) then
        match imm.ty with
        | .uint256 | .int256 | .uint8 | .uint16 | .bytes32 | .bool =>
            `(Compiler.CompilationModel.Expr.storage $(strTerm (immutableHiddenName imm)))
        | .address => `(Compiler.CompilationModel.Expr.storageAddr $(strTerm (immutableHiddenName imm)))
        | _ => throwErrorAt stx s!"immutable '{name}' uses unsupported type"
      else if let some constant := constDecls.find? (fun constant => declaredNameMatches name constant.name) then
        translateConstantExpr fields constDecls immutableDecls visitingConstants constant.name
      else if let some accessor := contextAccessorBareName? name then
        throwPureContextAccessorError stx accessor
      else
        translateConstantExpr fields constDecls immutableDecls visitingConstants name
  | `(term| calldatasize) => `(Compiler.CompilationModel.Expr.calldatasize)
  | `(term| returndataSize) => `(Compiler.CompilationModel.Expr.returndataSize)
  | `(term| zeroAddress) =>
      if params.any (fun p => p.name == "zeroAddress") || localNames.contains "zeroAddress" then
        lookupVarExpr params localNames "zeroAddress"
      else
        `(Compiler.CompilationModel.Expr.literal 0)
  | `(term| isZeroAddress $a:term) =>
      `(Compiler.CompilationModel.Expr.eq
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          (Compiler.CompilationModel.Expr.literal 0))
  | `(term| wordToAddress $a:term) => translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants
  | `(term| addressToWord $a:term) => translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants
  | `(term| toInt256 $a:term) => translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants
  | `(term| toUint256 $a:term) => translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants
  | `(term| boolToWord $a:term) =>
      `(Compiler.CompilationModel.Expr.ite
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          (Compiler.CompilationModel.Expr.literal 1)
          (Compiler.CompilationModel.Expr.literal 0))
  | `(term| $n:num) => `(Compiler.CompilationModel.Expr.literal $n)
  | `(term| add $a $b) => `(Compiler.CompilationModel.Expr.add $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| sub $a $b) => `(Compiler.CompilationModel.Expr.sub $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| mul $a $b) => `(Compiler.CompilationModel.Expr.mul $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| pow $a $b) | `(term| $a ^ $b) =>
      `(Compiler.CompilationModel.Expr.externalCall Compiler.CompilationModel.builtinExpName
          [$(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants),
           $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)])
  | `(term| div $a $b) | `(term| $a / $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.sdiv $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
      else
        `(Compiler.CompilationModel.Expr.div $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| sdiv $a $b) => `(Compiler.CompilationModel.Expr.sdiv $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| mod $a $b) | `(term| $a % $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.smod $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
      else
        `(Compiler.CompilationModel.Expr.mod $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| smod $a $b) => `(Compiler.CompilationModel.Expr.smod $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| bitAnd $a $b) => `(Compiler.CompilationModel.Expr.bitAnd $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| bitOr $a $b) => `(Compiler.CompilationModel.Expr.bitOr $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| bitXor $a $b) => `(Compiler.CompilationModel.Expr.bitXor $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| bitNot $a) => `(Compiler.CompilationModel.Expr.bitNot $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants))
  | `(term| shl $shift $value) => `(Compiler.CompilationModel.Expr.shl $(← translatePureExprWithTypes fields constDecls immutableDecls params locals shift visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants))
  | `(term| shr $shift $value) => `(Compiler.CompilationModel.Expr.shr $(← translatePureExprWithTypes fields constDecls immutableDecls params locals shift visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants))
  | `(term| sar $shift $value) => `(Compiler.CompilationModel.Expr.sar $(← translatePureExprWithTypes fields constDecls immutableDecls params locals shift visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants))
  | `(term| byte $index $value) => `(Compiler.CompilationModel.Expr.byte $(← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants))
  | `(term| signextend $byteIndex $value) => `(Compiler.CompilationModel.Expr.signextend $(← translatePureExprWithTypes fields constDecls immutableDecls params locals byteIndex visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants))
  | `(term| $a == $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      if (lhsTy == .string && rhsTy == .string) || (lhsTy == .bytes && rhsTy == .bytes) then
        let (lhsName, rhsName) ← dynamicEqParamNames stx params a b lhsTy rhsTy
        `(Compiler.CompilationModel.Expr.dynamicBytesEq $(strTerm lhsName) $(strTerm rhsName))
      else
        `(Compiler.CompilationModel.Expr.eq
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a != $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      if (lhsTy == .string && rhsTy == .string) || (lhsTy == .bytes && rhsTy == .bytes) then
        let (lhsName, rhsName) ← dynamicEqParamNames stx params a b lhsTy rhsTy
        `(Compiler.CompilationModel.Expr.logicalNot
            (Compiler.CompilationModel.Expr.dynamicBytesEq $(strTerm lhsName) $(strTerm rhsName)))
      else
        `(Compiler.CompilationModel.Expr.logicalNot
            (Compiler.CompilationModel.Expr.eq
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)))
  | `(term| $a >= $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.logicalNot
            (Compiler.CompilationModel.Expr.slt
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)))
      else
        `(Compiler.CompilationModel.Expr.ge $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a > $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.sgt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
      else
        `(Compiler.CompilationModel.Expr.gt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| sgt $a $b) => `(Compiler.CompilationModel.Expr.sgt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a < $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.slt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
      else
        `(Compiler.CompilationModel.Expr.lt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| slt $a $b) => `(Compiler.CompilationModel.Expr.slt $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a <= $b) => do
      let lhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals a visitingConstants
      let rhsTy ← inferPureExprType fields constDecls immutableDecls #[] params locals b visitingConstants
      let (lhsTy, rhsTy) := preferSignedNumericLiteralPeer a b lhsTy rhsTy
      if lhsTy == .int256 && rhsTy == .int256 then
        `(Compiler.CompilationModel.Expr.logicalNot
            (Compiler.CompilationModel.Expr.sgt
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)))
      else
        `(Compiler.CompilationModel.Expr.le $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a && $b) => `(Compiler.CompilationModel.Expr.logicalAnd $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| $a || $b) => `(Compiler.CompilationModel.Expr.logicalOr $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| logicalAnd $a $b) => `(Compiler.CompilationModel.Expr.logicalAnd $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| logicalOr $a $b) => `(Compiler.CompilationModel.Expr.logicalOr $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| logicalNot $a) => `(Compiler.CompilationModel.Expr.logicalNot $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants))
  | `(term| and $a $b) => `(Compiler.CompilationModel.Expr.bitAnd $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| or $a $b) => `(Compiler.CompilationModel.Expr.bitOr $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| xor $a $b) => `(Compiler.CompilationModel.Expr.bitXor $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| not $a) => `(Compiler.CompilationModel.Expr.bitNot $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants))
  | `(term| mload $offset) =>
      `(Compiler.CompilationModel.Expr.mload
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset visitingConstants))
  | `(term| tload $offset) =>
      `(Compiler.CompilationModel.Expr.tload
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset visitingConstants))
  | `(term| calldataload $offset) =>
      `(Compiler.CompilationModel.Expr.calldataload
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset visitingConstants))
  | `(term| extcodesize $addr) =>
      `(Compiler.CompilationModel.Expr.extcodesize
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals addr visitingConstants))
  | `(term| keccak256 $offset $size) =>
      `(Compiler.CompilationModel.Expr.keccak256
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals size visitingConstants))
  | `(term| call $gas $target $value $inOffset $inSize $outOffset $outSize) =>
      `(Compiler.CompilationModel.Expr.call
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals gas visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals target visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inSize visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outSize visitingConstants))
  | `(term| staticcall $gas $target $inOffset $inSize $outOffset $outSize) =>
      `(Compiler.CompilationModel.Expr.staticcall
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals gas visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals target visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inSize visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outSize visitingConstants))
  | `(term| delegatecall $gas $target $inOffset $inSize $outOffset $outSize) =>
      `(Compiler.CompilationModel.Expr.delegatecall
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals gas visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals target visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals inSize visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outOffset visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals outSize visitingConstants))
  | `(term| arrayLength $name:term) =>
      -- verity#1849, G1: `arrayLength (arrayElement <param> <i>).<dynField>`
      -- lowers through `Expr.arrayElementDynamicMemberLength`, reading the
      -- length word of the dynamic member at the resolved head offset.
      if let some (paramName, index, _fieldTy, _elemTy, wordOffset) :=
          arrayElementDynamicMemberProjection? params name then
        let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
        `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
            $(strTerm paramName)
            $indexExpr
            $(natTerm wordOffset))
      else if let some (paramName, index, _fieldTy, _elemTy, wordOffset) :=
          localArrayElementDynamicMemberProjection? locals name then
        let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
        `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
            $(strTerm paramName)
            $indexExpr
            $(natTerm wordOffset))
      else if let some (paramName, _fieldTy, wordOffset) :=
          paramDynamicMemberProjection? params name then
        `(Compiler.CompilationModel.Expr.paramDynamicMemberLength
            $(strTerm paramName)
            $(natTerm wordOffset))
      else if let some (name, _) := localMemoryArray? locals name then
        `(Compiler.CompilationModel.Expr.memoryArrayLength $(strTerm name))
      else
        `(Compiler.CompilationModel.Expr.arrayLength $(strTerm (← expectStringOrIdent name)))
  | `(term| arrayElement $name:term $index:term) =>
      -- verity#1849, G2: `arrayElement (arrayElement <param> <i>).<dynField> <k>`
      -- lowers through `Expr.arrayElementDynamicMemberElement`.
      if let some (paramName, outerIndex, _fieldTy, _elemTy, wordOffset) :=
          arrayElementDynamicMemberProjection? params name then
        let outerIndexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals outerIndex visitingConstants
        let innerIndexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
        `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberElement
            $(strTerm paramName)
            $outerIndexExpr
            $(natTerm wordOffset)
            $innerIndexExpr)
      else if let some (paramName, outerIndex, _fieldTy, _elemTy, wordOffset) :=
          localArrayElementDynamicMemberProjection? locals name then
        let outerIndexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals outerIndex visitingConstants
        let innerIndexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
        `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberElement
            $(strTerm paramName)
            $outerIndexExpr
            $(natTerm wordOffset)
            $innerIndexExpr)
      else if let some (paramName, _fieldTy, wordOffset) :=
          paramDynamicMemberProjection? params name then
        let innerIndexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants
        `(Compiler.CompilationModel.Expr.paramDynamicMemberElement
            $(strTerm paramName)
            $(natTerm wordOffset)
            $innerIndexExpr)
      else if let some (name, _) := localMemoryArray? locals name then
        `(Compiler.CompilationModel.Expr.memoryArrayElement
            $(strTerm name)
            $(← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants))
      else
        `(Compiler.CompilationModel.Expr.arrayElement
            $(strTerm (← expectStringOrIdent name))
            $(← translatePureExprWithTypes fields constDecls immutableDecls params locals index visitingConstants))
  | `(term| ceilDiv $a $b) =>
      `(Compiler.CompilationModel.Expr.ceilDiv
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| mulDivDown $a $b $c) =>
      `(Compiler.CompilationModel.Expr.mulDivDown
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals c visitingConstants))
  | `(term| mulDivUp $a $b $c) =>
      `(Compiler.CompilationModel.Expr.mulDivUp
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals c visitingConstants))
  | `(term| mulDiv512Down $a $b $c) =>
      `(Compiler.CompilationModel.Expr.mulDiv512Down
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals c visitingConstants))
  | `(term| mulDiv512Up $a $b $c) =>
      `(Compiler.CompilationModel.Expr.mulDiv512Up
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals c visitingConstants))
  | `(term| wMulDown $a $b) =>
      `(Compiler.CompilationModel.Expr.wMulDown
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| wDivUp $a $b) =>
      `(Compiler.CompilationModel.Expr.wDivUp
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| min $a $b) => `(Compiler.CompilationModel.Expr.min $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| max $a $b) => `(Compiler.CompilationModel.Expr.max $(← translatePureExprWithTypes fields constDecls immutableDecls params locals a visitingConstants) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals b visitingConstants))
  | `(term| ite $cond $thenVal $elseVal) =>
      `(Compiler.CompilationModel.Expr.ite
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals cond visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals thenVal visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals elseVal visitingConstants))
  | `(term| externalCall $name:term $args:term) =>
      let extName := ← expectStringOrIdent name
      let argsExprs ←
        match stripParens args with
        | `(term| [ $[$xs],* ]) => do
            let mut out : Array Term := #[]
            for arg in xs do
              match directParamNameWithType? params arg with
              | some (name, ty) =>
                  if externalCallDynamicArgSupported ty then
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_data_offset")))
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_length")))
                  else
                    out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg visitingConstants)
              | none =>
                  out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg visitingConstants)
            pure out
        | _ => throwErrorAt args "expected list literal [..]"
      `(Compiler.CompilationModel.Expr.externalCall $(strTerm extName) [ $[$argsExprs],* ])
  | `(term| intrinsic_fusaka $name:term $lowering:term $args:term) =>
      translateIntrinsic name lowering args (← `(Verity.Core.Intrinsics.HardFork.osaka))
  | `(term| intrinsic_osaka $name:term $lowering:term $args:term) =>
      translateIntrinsic name lowering args (← `(Verity.Core.Intrinsics.HardFork.osaka))
  | `(term| intrinsic_prague $name:term $lowering:term $args:term) =>
      translateIntrinsic name lowering args (← `(Verity.Core.Intrinsics.HardFork.prague))
  | `(term| intrinsic_cancun $name:term $lowering:term $args:term) =>
      translateIntrinsic name lowering args (← `(Verity.Core.Intrinsics.HardFork.cancun))
  | `(term| intrinsic $name:term $lowering:term $args:term) =>
      let intrinsicName := ← expectStringOrIdent name
      let registered ← liftIO getRegisteredIntrinsics
      let minFork ←
        match registered.find? (fun decl => decl.name = intrinsicName) with
        | some decl => pure decl.minFork
        | none =>
            throwErrorAt name
              s!"unknown intrinsic '{intrinsicName}'; declare it first with `verity_intrinsic` so the compiler can enforce min_fork"
      let minForkTerm ← hardForkTermFromParsed minFork
      translateIntrinsic name lowering args minForkTerm
  | `(term| fork_if_at_least $fork:ident then $thenExpr:term else $elseExpr:term) =>
      `(Compiler.CompilationModel.Expr.forkIfAtLeast
          $(← hardForkTermFromIdent fork)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals thenExpr visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals elseExpr visitingConstants))
  | `(term| structMember $field:term $key:term $member:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName false
      `(Compiler.CompilationModel.Expr.structMember
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key visitingConstants)
          $(strTerm memberName))
  | `(term| structMember2 $field:term $key1:term $key2:term $member:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName true
      `(Compiler.CompilationModel.Expr.structMember2
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1 visitingConstants)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2 visitingConstants)
          $(strTerm memberName))
  | _ =>
      match ← translateLeanDefCall? fields constDecls immutableDecls #[] params locals stx visitingConstants with
      | some expr => pure expr
      | none => throwErrorAt stx "unsupported expression in verity_contract body (see #1003 for planned macro support expansions)"
end

partial def translatePureExpr
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array String)
    (stx : Term)
    (visitingConstants : List String := []) : CommandElabM Term :=
  translatePureExprWithTypes fields constDecls immutableDecls params
    (locals.map (fun name => mkTypedLocal name .uint256)) stx visitingConstants

private def translateAbiEncodeProjection?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option (Array Term)) := do
  match stripParens stx with
  | `(term| abiEncode $target:term) =>
      if let some (paramName, index, fieldTy, elemTy, wordOffset) :=
          localArrayElementStructProjectionResolved? locals target then
        let wordCount ←
          match staticAbiWordCount? fieldTy with
          | some n => pure n
          | none => throwErrorAt target "abiEncode projection requires a nested static ABI struct/tuple"
        let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
        let mut exprs : Array Term := #[]
        for offset in List.range wordCount do
          if valueTypeUsesDynamicData elemTy then
            exprs := exprs.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicWord
              $(strTerm paramName)
              $indexExpr
              $(natTerm (wordOffset + offset))))
          else
            let elementWords ←
              match staticAbiWordCount? elemTy with
              | some n => pure n
              | none => throwErrorAt target "abiEncode array-element projection requires static ABI layout"
            exprs := exprs.push (← `(Compiler.CompilationModel.Expr.arrayElementWord
              $(strTerm paramName)
              $indexExpr
              $(natTerm elementWords)
              $(natTerm (wordOffset + offset))))
        pure (some exprs)
      else if let some (paramName, fieldTy, wordOffset) :=
          paramDynamicStaticCompositeProjection? params target then
        let wordCount ←
          match staticAbiWordCount? fieldTy with
          | some n => pure n
          | none => throwErrorAt target "abiEncode projection requires a nested static ABI struct/tuple"
        let mut exprs : Array Term := #[]
        for offset in List.range wordCount do
          exprs := exprs.push (← `(Compiler.CompilationModel.Expr.paramDynamicHeadWord
            $(strTerm paramName)
            $(natTerm (wordOffset + offset))))
        pure (some exprs)
      else
        let ty ← inferPureExprType fields constDecls immutableDecls #[] params locals target
        let wordCount ←
          match staticAbiWordCount? ty with
          | some n => pure n
          | none => throwErrorAt target "abiEncode requires a static ABI value"
        if wordCount == 1 then
          pure (some #[← translatePureExprWithTypes fields constDecls immutableDecls params locals target])
        else
          throwErrorAt target "abiEncode for this static value is not addressable as ABI head words"
  | _ => pure none

private def validateWordLikeExprListLiteral
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (args : Term)
    (context : String) : CommandElabM Unit := do
  match stripParens args with
  | `(term| [ $[$xs],* ]) =>
      for x in xs do
        requireWordLikeType x context
          (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x)
  | _ => throwErrorAt args "expected list literal [..]"

private def validateEcmExprListLiteral
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (args : Term)
    (context : String) : CommandElabM Unit := do
  match stripParens args with
  | `(term| [ $[$xs],* ]) =>
      for x in xs do
        if let some (_, _, fieldTy, _elemTy, _) := arrayElementDynamicMemberProjection? params x then
          match fieldTy with
          | .array elemTy =>
              unless externalCallDynamicArgSupported (.array elemTy) do
                throwErrorAt x s!"{context} dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
          | _ =>
              throwErrorAt x s!"{context} dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
        else if let some (_, _, fieldTy, _elemTy, _) := localArrayElementDynamicMemberProjection? locals x then
          match fieldTy with
          | .array elemTy =>
              unless externalCallDynamicArgSupported (.array elemTy) do
                throwErrorAt x s!"{context} dynamic-member alias argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
          | _ =>
              throwErrorAt x s!"{context} dynamic-member alias argument requires an Array-typed member, got {renderValueType fieldTy}"
        else if let some (_, fieldTy, _) := paramDynamicMemberProjection? params x then
          match fieldTy with
          | .array elemTy =>
              unless externalCallDynamicArgSupported (.array elemTy) do
                throwErrorAt x s!"{context} dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
          | _ =>
              throwErrorAt x s!"{context} dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
        else
          requireWordOrDirectArrayType x context
            (← inferPureExprType fields constDecls immutableDecls externalDecls params locals x)
  | _ => throwErrorAt args "expected list literal [..]"

private partial def syntaxMentionsIdent (stx : Syntax) (name : String) : Bool :=
  match stx with
  | .ident _ raw _ _ => raw.toString == name
  | .node _ _ args => args.any (fun child => syntaxMentionsIdent child name)
  | _ => false

private def freshSyntheticLocalName
    (base : String)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String) : String :=
  let used :=
    ((params.map (·.name)) ++ typedLocalNames locals ++ mutableLocals).toList
  let rec go (remaining : Nat) (suffix : Nat) : String :=
    let candidate :=
      if suffix == 0 then base else s!"{base}_{suffix}"
    if !(used.contains candidate) then
      candidate
    else
      match remaining with
      | 0 => s!"{base}_fresh"
      | n + 1 => go n (suffix + 1)
  go used.length 0

private def parseTryCatchHandler
    (handler : Term) : CommandElabM (Option String × Array (TSyntax `doElem)) := do
  match stripParens handler with
  | `(term| fun $name:ident => do $[$elems:doElem]*) =>
      pure (some (toString name.getId), elems)
  | `(term| do $[$elems:doElem]*) =>
      pure (none, elems)
  | _ =>
      throwErrorAt handler
        "tryCatch handler must be `fun _ => do ...` or a direct `do ...` block"

private def validateTryCatchHandlerDoesNotUsePayload
    (handler : Term)
    (payloadName? : Option String)
    (elems : Array (TSyntax `doElem)) : CommandElabM Unit := do
  match payloadName? with
  | none => pure ()
  | some payloadName =>
      if elems.any (fun elem => syntaxMentionsIdent elem.raw payloadName) then
        throwErrorAt handler
          s!"tryCatch catch payload '{payloadName}' is not available on the compilation-model path yet; use `_`/ignore it and read returndata explicitly if needed"

private unsafe def evalExternalCallModuleTerm
    (moduleTerm : Term) : CommandElabM Compiler.ECM.ExternalCallModule := do
  liftTermElabM do
    let expectedType := mkConst ``Compiler.ECM.ExternalCallModule
    let expr ← Lean.Elab.Term.elabTermEnsuringType moduleTerm expectedType
    Lean.Meta.evalExpr Compiler.ECM.ExternalCallModule expectedType expr .unsafe

private def validateEffectOnlyEcmModuleTerm
    (moduleTerm : Term) : CommandElabM Unit := do
  let mod ← unsafe evalExternalCallModuleTerm moduleTerm
  if !mod.resultVars.isEmpty then
    throwErrorAt moduleTerm
      s!"ecmDo requires an effect-only ECM module, but '{mod.name}' binds {mod.resultVars.length} result value(s)"

private def validateSingleResultEcmModuleTerm
    (moduleTerm : Term)
    (boundVarName : String) : CommandElabM Unit := do
  let mod ← unsafe evalExternalCallModuleTerm moduleTerm
  if mod.resultVars != [boundVarName] then
    throwErrorAt moduleTerm
      s!"ecmCall must elaborate to an ECM module binding exactly ['{boundVarName}'], but '{mod.name}' binds {repr mod.resultVars}"

private def validateResultEcmModuleTerm
    (moduleTerm : Term)
    (boundVarNames : Array String) : CommandElabM Unit := do
  let mod ← unsafe evalExternalCallModuleTerm moduleTerm
  if mod.resultVars != boundVarNames.toList then
    throwErrorAt moduleTerm
      s!"ecmBind must elaborate to an ECM module binding exactly {repr boundVarNames.toList}, but '{mod.name}' binds {repr mod.resultVars}"

private def arrayElementTupleElemExprs?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Option (Array Term)) := do
  match stripParens rhs with
  | `(term| arrayElement $name:term $index:term) =>
      -- verity#1849, G2: compound projections never destructure into a tuple.
      if (arrayElementDynamicMemberProjection? params name).isSome ||
          (localArrayElementDynamicMemberProjection? locals name).isSome then
        return none
      match directParamNameWithType? params name with
      | none => return none
      | some _ => pure ()
      let paramName := ← expectStringOrIdent name
      match params.find? (fun p => p.name == paramName) with
      | some { ty := .array (.tuple elemTys), .. } =>
          let elementWords ←
            match staticAbiWordCount? (.tuple elemTys) with
            | some n => pure n
            | none =>
                throwErrorAt rhs
                  "arrayElement tuple destructuring requires a static ABI-word tuple element type"
          let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
          let mut offset := 0
          let mut exprs : Array Term := #[]
          for elemTy in elemTys do
            let elemWords ←
              match staticAbiWordCount? elemTy with
              | some n => pure n
              | none =>
                  throwErrorAt rhs
                    "arrayElement tuple destructuring requires static ABI-word tuple members"
            if elemWords != 1 then
              throwErrorAt rhs
                "arrayElement tuple destructuring currently supports top-level single-word tuple members"
            exprs := exprs.push (← `(Compiler.CompilationModel.Expr.arrayElementWord
              $(strTerm paramName)
              $indexExpr
              $(natTerm elementWords)
              $(natTerm offset)))
            offset := offset + elemWords
          pure (some exprs)
      | _ => pure none
  | _ => pure none

private def arrayElementTupleDestructureStmts?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String)
    (rhs : Term)
    (names : Array (Option String)) : CommandElabM (Option (Array Term × TypedLocal)) := do
  match stripParens rhs with
  | `(term| arrayElement $name:term $index:term) =>
      -- verity#1849, G2: compound projections never destructure into a tuple.
      if (arrayElementDynamicMemberProjection? params name).isSome ||
          (localArrayElementDynamicMemberProjection? locals name).isSome then
        return none
      match directParamNameWithType? params name with
      | none => return none
      | some _ => pure ()
      let paramName := ← expectStringOrIdent name
      match params.find? (fun p => p.name == paramName) with
      | some { ty := .array (.tuple elemTys), .. } =>
          if names.size != elemTys.length then
            throwErrorAt rhs
              s!"tuple destructuring binds {names.size} names, but the source provides {elemTys.length} values"
          let syntheticUsed := mutableLocals ++ names.filterMap id
          let indexName := freshSyntheticLocalName "arrayElement_index" params locals syntheticUsed
          let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
          let indexStmt ←
            `(Compiler.CompilationModel.Stmt.letVar $(strTerm indexName) $indexExpr)
          let indexLocal ←
            `(Compiler.CompilationModel.Expr.localVar $(strTerm indexName))
          let mut offset := 0
          let mut valueExprs : Array Term := #[]
          match staticAbiWordCount? (.tuple elemTys) with
          | some elementWords =>
              for elemTy in elemTys do
                let elemWords ←
                  match staticAbiWordCount? elemTy with
                  | some n => pure n
                  | none =>
                      throwErrorAt rhs
                        "arrayElement tuple destructuring requires static ABI-word tuple members"
                if elemWords != 1 then
                  throwErrorAt rhs
                    "arrayElement tuple destructuring currently supports top-level single-word tuple members"
                valueExprs := valueExprs.push (← `(Compiler.CompilationModel.Expr.arrayElementWord
                  $(strTerm paramName)
                  $indexLocal
                  $(natTerm elementWords)
                  $(natTerm offset)))
                offset := offset + elemWords
          | none =>
              let _ ←
                match abiLocalHeadWordCount? (.tuple elemTys) with
                | some n => pure n
                | none =>
                    throwErrorAt rhs
                      "arrayElement tuple destructuring requires an ABI-decodable tuple element type"
              for (elemTy, name?) in elemTys.zip names.toList do
                if isSingleWordStaticValueType elemTy then
                  valueExprs := valueExprs.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicWord
                    $(strTerm paramName)
                    $indexLocal
                    $(natTerm offset)))
                else
                  match name? with
                  | none =>
                      valueExprs := valueExprs.push (← `(Compiler.CompilationModel.Expr.literal 0))
                  | some boundName =>
                      throwErrorAt rhs
                        s!"arrayElement tuple destructuring cannot bind dynamic member '{boundName}' yet; use '_' for nested dynamic members"
                let elemHeadWords ←
                  match abiLocalHeadWordCount? elemTy with
                  | some n => pure n
                  | none =>
                      throwErrorAt rhs
                        "arrayElement tuple destructuring requires ABI-decodable tuple members"
                offset := offset + elemHeadWords
          let boundPairs := (names.zip valueExprs).filterMap fun (name?, valueExpr) =>
            name?.map (fun name => (name, valueExpr))
          let boundStmts ← boundPairs.mapM fun (name, valueExpr) =>
            `(Compiler.CompilationModel.Stmt.letVar $(strTerm name) $valueExpr)
          pure (some (#[indexStmt] ++ boundStmts, mkTypedLocal indexName .uint256))
      | _ => pure none
  | _ => pure none

private def arrayElementTupleReturnStmts?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String)
    (rhs : Term) : CommandElabM (Option (Array Term × TypedLocal)) := do
  match stripParens rhs with
  | `(term| arrayElement $name:term $index:term) =>
      -- verity#1849, G2: compound projections (`(arrayElement <p> <i>).<dynField>`)
      -- never destructure into a tuple return — bail out without throwing on
      -- the non-ident name. The scalar return path will pick this up.
      if (arrayElementDynamicMemberProjection? params name).isSome ||
          (localArrayElementDynamicMemberProjection? locals name).isSome then
        return none
      match directParamNameWithType? params name with
      | none => return none
      | some _ => pure ()
      let paramName := ← expectStringOrIdent name
      match params.find? (fun p => p.name == paramName) with
      | some { ty := .array (.tuple elemTys), .. } =>
          let elementWords ←
            match staticAbiWordCount? (.tuple elemTys) with
            | some n => pure n
            | none =>
                throwErrorAt rhs
                  "arrayElement tuple return requires a static ABI-word tuple element type"
          let indexName := freshSyntheticLocalName "arrayElement_index" params locals mutableLocals
          let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
          let indexStmt ←
            `(Compiler.CompilationModel.Stmt.letVar $(strTerm indexName) $indexExpr)
          let indexLocal ←
            `(Compiler.CompilationModel.Expr.localVar $(strTerm indexName))
          let mut offset := 0
          let mut valueExprs : Array Term := #[]
          for elemTy in elemTys do
            let elemWords ←
              match staticAbiWordCount? elemTy with
              | some n => pure n
              | none =>
                  throwErrorAt rhs
                    "arrayElement tuple return requires static ABI-word tuple members"
            if elemWords != 1 then
              throwErrorAt rhs
                "arrayElement tuple return currently supports top-level single-word tuple members"
            valueExprs := valueExprs.push (← `(Compiler.CompilationModel.Expr.arrayElementWord
              $(strTerm paramName)
              $indexLocal
              $(natTerm elementWords)
              $(natTerm offset)))
            offset := offset + elemWords
          let returnStmt ←
            `(Compiler.CompilationModel.Stmt.returnValues [ $[$valueExprs],* ])
          pure (some (#[indexStmt, returnStmt], mkTypedLocal indexName .uint256))
      | _ => pure none
  | _ => pure none

private def tupleLiteralOrStructValueExprs?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Option (Array Term)) := do
  let structCtorValueExprs? : CommandElabM (Option (Array Term)) := do
    match qualifiedFunctionAppSyntax? (stripParens rhs) with
    | some (name, ctorArgs) =>
        if name.toString.endsWith ".mk" then
          pure (some (← ctorArgs.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)))
        else
          pure none
    | none =>
        match (stripParens rhs).raw with
        | .node _ `Lean.Parser.Term.app args =>
            match args.getD 0 Syntax.missing with
            | .ident _ raw _ _ =>
                if raw.toString.endsWith ".mk" then
                  let ctorArgs := (args.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
                  pure (some (← ctorArgs.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)))
                else
                  pure none
            | _ => pure none
        | _ => pure none
  let structValueExprs? : CommandElabM (Option (Array Term)) := do
    match stripParens rhs with
    | `(term| structMembers $field:term $key:term $members:term) => do
        let fieldName := ← expectStringOrIdent field
        let memberNames := ← expectStringList members
        let exprs ← memberNames.mapM fun memberName => do
          let _ ← lookupStructMemberDecl fields fieldName memberName false
          `(Compiler.CompilationModel.Expr.structMember
              $(strTerm fieldName)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(strTerm memberName))
        pure (some exprs)
    | `(term| structMembers2 $field:term $key1:term $key2:term $members:term) => do
        let fieldName := ← expectStringOrIdent field
        let memberNames := ← expectStringList members
        let exprs ← memberNames.mapM fun memberName => do
          let _ ← lookupStructMemberDecl fields fieldName memberName true
          `(Compiler.CompilationModel.Expr.structMember2
              $(strTerm fieldName)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2)
              $(strTerm memberName))
        pure (some exprs)
    | _ => pure none
  match tupleElemsFromTerm? rhs with
  | some elems =>
      pure (some (← elems.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)))
  | none =>
      match ← arrayElementTupleElemExprs? fields constDecls immutableDecls params locals rhs with
      | some exprs => pure (some exprs)
      | none =>
          match ← structCtorValueExprs? with
          | some exprs => pure (some exprs)
          | none => structValueExprs?

private def tupleValueExprs
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Array Term) := do
  match (← tupleLiteralOrStructValueExprs? fields constDecls immutableDecls params locals rhs) with
  | some exprs => pure exprs
  | none =>
      match stripParens rhs with
      | `(term| $id:ident) =>
          match (← tupleParamElemExprs? params (toString id.getId)) with
          | some exprs => pure exprs
          | none => throwErrorAt rhs "tuple destructuring currently requires a tuple literal, tuple-typed parameter, or structMembers/structMembers2 source"
      | _ =>
          throwErrorAt rhs "tuple destructuring currently requires a tuple literal, tuple-typed parameter, or structMembers/structMembers2 source"

private def tupleReturnValueExprs?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM (Option (Array Term)) := do
  match (← tupleLiteralOrStructValueExprs? fields constDecls immutableDecls params locals rhs) with
  | some exprs => pure (some exprs)
  | none =>
      match stripParens rhs with
      | `(term| $id:ident) =>
          tupleParamElemExprs? params (toString id.getId)
      | _ =>
          pure none

private partial def staticInternalHelperBindingNames (name : String) (ty : ValueType) : List String :=
  match ty with
  | .uint256 | .int256 | .uint8 | .uint16 | .address | .bool | .bytes32 => [name]
  | .fixedArray elemTy size =>
      (List.range size).flatMap fun idx =>
        staticInternalHelperBindingNames s!"{name}_{idx}" elemTy
  | .tuple elemTys =>
      let rec goTuple (tys : List ValueType) (idx : Nat) : List String :=
        match tys with
        | [] => []
        | elemTy :: rest =>
            staticInternalHelperBindingNames s!"{name}_{idx}" elemTy ++ goTuple rest (idx + 1)
      goTuple elemTys 0
  | .struct _ fields =>
      let rec goStruct (fs : List (String × ValueType)) (idx : Nat) : List String :=
        match fs with
        | [] => []
        | field :: rest =>
            staticInternalHelperBindingNames s!"{name}_{idx}" field.snd ++ goStruct rest (idx + 1)
      goStruct fields 0
  | .newtype _ baseTy => staticInternalHelperBindingNames name baseTy
  | _ => []

private def isStaticCompositeInternalHelperType : ValueType → Bool
  | .tuple _ | .struct _ _ | .fixedArray _ _ => true
  | .newtype _ baseTy => isStaticCompositeInternalHelperType baseTy
  | _ => false

private def translateInternalHelperCallArgs
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (fn : FunctionDecl)
    (argTerms : Array Term) : CommandElabM (Array Term) := do
  let mut out : Array Term := #[]
  for idx in [:argTerms.size] do
    let some arg := argTerms[idx]? | pure ()
    let some fnParam := fn.params[idx]? | pure ()
    match fnParam.ty with
    | .array _ =>
        match directParamNameWithType? params arg with
        | some (name, _) =>
            out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm (memoryArrayDataOffsetName name))))
            out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm (memoryArrayLengthName name))))
        | none =>
            if let some (name, _) := localMemoryArray? locals arg then
              out := out.push (← `(Compiler.CompilationModel.Expr.localVar $(strTerm (memoryArrayDataOffsetName name))))
              out := out.push (← `(Compiler.CompilationModel.Expr.localVar $(strTerm (memoryArrayLengthName name))))
            else if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                arrayElementDynamicMemberProjection? params arg then
              match fieldTy with
              | .array _ =>
                  let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
              | _ =>
                  throwErrorAt arg s!"helper call '{fn.name}' Array parameter '{fnParam.name}' requires an Array-typed projected member, got {renderValueType fieldTy}"
            else if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                localArrayElementDynamicMemberProjection? locals arg then
              match fieldTy with
              | .array _ =>
                  let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
              | _ =>
                  throwErrorAt arg s!"helper call '{fn.name}' Array parameter '{fnParam.name}' requires an Array-typed projected alias member, got {renderValueType fieldTy}"
            else if let some (paramName, fieldTy, wordOffset) :=
                paramDynamicMemberProjection? params arg then
              match fieldTy with
              | .array _ =>
                  out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberDataOffset
                    $(strTerm paramName)
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberLength
                    $(strTerm paramName)
                    $(natTerm wordOffset)))
              | _ =>
                  throwErrorAt arg s!"helper call '{fn.name}' Array parameter '{fnParam.name}' requires an Array-typed projected member, got {renderValueType fieldTy}"
            else
              throwErrorAt arg
                s!"helper call '{fn.name}' Array parameter '{fnParam.name}' currently requires a direct Array parameter reference or projected struct-array member"
    | _ =>
        if valueTypeUsesDynamicData fnParam.ty then
          match directParamNameWithType? params arg with
          | some (name, ty) =>
              if valueTypeUsesDynamicData ty then
                match fnParam.ty with
                | .bytes | .string =>
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_data_offset")))
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_length")))
                | _ =>
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_data_offset")))
              else
                out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
          | none =>
              if let some (paramName, index, _elemTy) := arrayElementDynamicTupleArg? params arg then
                let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicDataOffset
                  $(strTerm paramName)
                  $indexExpr))
              else
                out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
        else
          if isStaticCompositeInternalHelperType fnParam.ty then
            match directParamNameWithType? params arg with
            | some (name, ty) =>
                let bindingNames := staticInternalHelperBindingNames name ty
                if bindingNames.isEmpty then
                  out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
                else
                  for bindingName in bindingNames do
                    out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm bindingName)))
            | none =>
                out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
          else
            out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
  pure out

private def translateLinkedExternalCallArgs
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (argTerms : Array Term) : CommandElabM (Array Term) := do
  let mut out : Array Term := #[]
  for arg in argTerms do
    match ← translateAbiEncodeProjection? fields constDecls immutableDecls params locals arg with
    | some exprs =>
        out := out ++ exprs
    | none =>
        match directParamNameWithType? params arg with
        | some (name, ty) =>
            if externalCallDynamicArgSupported ty then
              out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_data_offset")))
              out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_length")))
            else
              out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
        | none =>
            if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                arrayElementDynamicMemberProjection? params arg then
              match fieldTy with
              | .array elemTy =>
                  if externalCallDynamicArgSupported (.array elemTy) then
                    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                    out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                      $(strTerm paramName)
                      $indexExpr
                      $(natTerm wordOffset)))
                    out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                      $(strTerm paramName)
                      $indexExpr
                      $(natTerm wordOffset)))
                  else
                    throwErrorAt arg s!"linked external dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt arg s!"linked external dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
            else if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                localArrayElementDynamicMemberProjection? locals arg then
              match fieldTy with
              | .array elemTy =>
                  if externalCallDynamicArgSupported (.array elemTy) then
                    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                    out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                      $(strTerm paramName)
                      $indexExpr
                      $(natTerm wordOffset)))
                    out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                      $(strTerm paramName)
                      $indexExpr
                      $(natTerm wordOffset)))
                  else
                    throwErrorAt arg s!"linked external dynamic-member alias argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt arg s!"linked external dynamic-member alias argument requires an Array-typed member, got {renderValueType fieldTy}"
            else if let some (paramName, fieldTy, wordOffset) :=
                paramDynamicMemberProjection? params arg then
              match fieldTy with
              | .array elemTy =>
                  if externalCallDynamicArgSupported (.array elemTy) then
                    out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberDataOffset
                      $(strTerm paramName)
                      $(natTerm wordOffset)))
                    out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberLength
                      $(strTerm paramName)
                      $(natTerm wordOffset)))
                  else
                    throwErrorAt arg s!"linked external dynamic-member argument currently supports only Array<wordLike> members, got {renderValueType fieldTy}"
              | _ =>
                  throwErrorAt arg s!"linked external dynamic-member argument requires an Array-typed member, got {renderValueType fieldTy}"
            else
              out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals arg)
  pure out

private def tupleInternalCallAssignStmt?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term)
    (names : Array (Option String)) : CommandElabM (Option Term) := do
  let rhs := stripParens rhs
  let initialUsedNames := (params.toList.map (fun p => p.name)) ++ (typedLocalNames locals).toList ++ (names.filterMap id).toList
  let (_, targetNamesRev) := names.toList.zipIdx.foldl
    (fun (acc : List String × List String) (name?, idx) =>
      let (usedNames, targetNamesRev) := acc
      let targetName := match name? with
        | some name => name
        | none => freshDiscardName usedNames idx
      (targetName :: usedNames, targetName :: targetNamesRev))
    (initialUsedNames, [])
  let targetNames := targetNamesRev.reverse
  let resultNameTerms := targetNames.toArray.map strTerm
  match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals rhs with
  | some (fn, argTerms) =>
      ensureSupportsInternalHelperSpec rhs fn
      let argExprs ← translateInternalHelperCallArgs
        fields constDecls immutableDecls params locals fn argTerms
      pure (some (← `(Compiler.CompilationModel.Stmt.internalCallAssign
        [ $[$resultNameTerms],* ]
        $(strTerm (internalHelperSpecNameFor fn))
        [ $[$argExprs],* ])))
  | none =>
      match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
      | some (qualifiedName, argTerms) =>
          let _ ← unsafe qualifiedTupleBindTypedLocals rhs.raw qualifiedName names
          let argExprs ← argTerms.mapM
            (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          pure (some (← `(Compiler.CompilationModel.Stmt.internalCallAssign
            [ $[$resultNameTerms],* ]
            $(strTerm (qualifiedInternalHelperName functions qualifiedName))
            [ $[$argExprs],* ])))
      | none =>
          pure none

/-- Try to translate a tuple‐destructured `tryExternalCall "name" [args]` RHS into
    a `Stmt.tryExternalCallBind` term.  Returns `none` when the RHS is not a
    `tryExternalCall` application.  Returns the statement term and the inferred
    types for each bound name (Bool for success flag, Uint256 for all result
    vars — precise return types require external decl lookup which happens in
    the validation pass).  (#1727, Axis 1 Step 5f) -/
private def tryExternalCallBindStmt?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term)
    (names : Array (Option String)) : CommandElabM (Option (Term × Array TypedLocal)) := do
  let rhs := stripParens rhs
  match rhs with
  | `(term| tryExternalCall $name:term $args:term) =>
      let extName := ← expectStringOrIdent name
      let argExprs ← match stripParens args with
        | `(term| [ $[$xs],* ]) =>
            translateLinkedExternalCallArgs fields constDecls immutableDecls params locals xs
        | _ => throwErrorAt args "expected list literal [..]"
      -- names[0] is the success flag, names[1..] are result vars
      let initialUsedNames := (params.toList.map (fun p => p.name)) ++ (typedLocalNames locals).toList ++ (names.filterMap id).toList
      let (_, targetNamesRev) := names.toList.zipIdx.foldl
        (fun (acc : List String × List String) (name?, idx) =>
          let (usedNames, targetNamesRev) := acc
          let targetName := match name? with
            | some name => name
            | none => freshDiscardName usedNames idx
          (targetName :: usedNames, targetName :: targetNamesRev))
        (initialUsedNames, [])
      let targetNames := targetNamesRev.reverse
      let successVar := match targetNames.head? with
        | some n => n
        | none => "_try_success"
      let resultVars := targetNames.drop 1
      let successVarTerm := strTerm successVar
      let resultTys ←
        match externalDecls.find? (fun ext => ext.name == extName) with
        | some ext =>
            if ext.returnTys.size != resultVars.length then
              throwErrorAt rhs s!"tryExternalCall '{extName}' binds {resultVars.length} result value(s), but the external declaration returns {ext.returnTys.size}"
            pure ext.returnTys
        | none =>
            -- Validation reports the unknown external with full context; keep
            -- translation moving with word-shaped placeholders.
            pure (Array.replicate resultVars.length .uint256)
      let mut flattenedResultVars : Array String := #[]
      let mut visibleLocals : Array TypedLocal := #[mkTypedLocal successVar .bool]
      for (resultVar, resultTy) in resultVars.toArray.zip resultTys do
        let flatNames ← flattenExternalResultNames resultVar resultTy
        flattenedResultVars := flattenedResultVars ++ flatNames.toArray
        let source :=
          match staticStructDirectFieldLocals? resultVar resultTy with
          | some fieldLocals => LocalSource.externalStaticStruct fieldLocals
          | none => LocalSource.value
        visibleLocals := visibleLocals.push { name := resultVar, ty := resultTy, source := source }
      let resultVarTerms := flattenedResultVars.map strTerm
      let stmt ← `(Compiler.CompilationModel.Stmt.tryExternalCallBind
          $successVarTerm
          [ $[$resultVarTerms],* ]
          $(strTerm extName)
          [ $[$argExprs],* ])
      pure (some (stmt, visibleLocals))
  | _ => pure none

private def expectExprList
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Array Term) := do
  match stripParens stx with
  | `(term| [ $[$xs],* ]) =>
      let mut out : Array Term := #[]
      for x in xs do
        match ← translateAbiEncodeProjection? fields constDecls immutableDecls params locals x with
        | some exprs => out := out ++ exprs
        | none => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
      pure out
  | _ => throwErrorAt stx "expected list literal [..]"

private def expectEcmExprList
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Array Term) := do
  match stripParens stx with
  | `(term| [ $[$xs],* ]) =>
      let mut out : Array Term := #[]
      for x in xs do
        match directParamNameWithType? params x with
        | some (name, ty) =>
            if externalCallDynamicArgSupported ty then
              out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_data_offset")))
              out := out.push (← `(Compiler.CompilationModel.Expr.param $(strTerm s!"{name}_length")))
            else
              match ← translateAbiEncodeProjection? fields constDecls immutableDecls params locals x with
              | some exprs => out := out ++ exprs
              | none => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
        | none =>
            if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                arrayElementDynamicMemberProjection? params x then
              match fieldTy with
              | .array _ =>
                  let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
              | _ => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
            else if let some (paramName, index, fieldTy, _elemTy, wordOffset) :=
                localArrayElementDynamicMemberProjection? locals x then
              match fieldTy with
              | .array _ =>
                  let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberDataOffset
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
                    $(strTerm paramName)
                    $indexExpr
                    $(natTerm wordOffset)))
              | _ => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
            else if let some (paramName, fieldTy, wordOffset) :=
                paramDynamicMemberProjection? params x then
              match fieldTy with
              | .array _ =>
                  out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberDataOffset
                    $(strTerm paramName)
                    $(natTerm wordOffset)))
                  out := out.push (← `(Compiler.CompilationModel.Expr.paramDynamicMemberLength
                    $(strTerm paramName)
                    $(natTerm wordOffset)))
              | _ => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
            else
              match ← translateAbiEncodeProjection? fields constDecls immutableDecls params locals x with
              | some exprs => out := out ++ exprs
              | none => out := out.push (← translatePureExprWithTypes fields constDecls immutableDecls params locals x)
      pure out
  | _ => throwErrorAt stx "expected list literal [..]"

private def translateEmitArgExpr
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM Term := do
  if let some (name, _) := localMemoryArray? locals stx then
    `(Compiler.CompilationModel.Expr.memoryArrayLength $(strTerm name))
  else if let some (paramName, index, _fieldTy, _elemTy, wordOffset) :=
      localArrayElementDynamicMemberProjection? locals stx then
    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
    `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
        $(strTerm paramName)
        $indexExpr
        $(natTerm wordOffset))
  else if let some (paramName, index, _fieldTy, _elemTy, wordOffset) :=
      arrayElementDynamicMemberProjection? params stx then
    let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
    `(Compiler.CompilationModel.Expr.arrayElementDynamicMemberLength
        $(strTerm paramName)
        $indexExpr
        $(natTerm wordOffset))
  else if let some (paramName, _fieldTy, wordOffset) :=
      paramDynamicMemberProjection? params stx then
    `(Compiler.CompilationModel.Expr.paramDynamicMemberLength
        $(strTerm paramName)
        $(natTerm wordOffset))
  else if let some (paramName, _fieldTy, wordOffset) :=
      paramDynamicStaticCompositeProjection? params stx then
    `(Compiler.CompilationModel.Expr.paramDynamicStaticComposite
        $(strTerm paramName)
        $(natTerm wordOffset))
  else
    translatePureExprWithTypes fields constDecls immutableDecls params locals stx

private def expectEmitExprList
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Array Term) := do
  match stripParens stx with
  | `(term| [ $[$xs],* ]) =>
      xs.mapM (translateEmitArgExpr fields constDecls immutableDecls params locals)
  | _ => throwErrorAt stx "expected list literal [..]"

private def inferEmitArgExprType
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM ValueType := do
  if let some (_, fieldTy, _) := paramDynamicStaticCompositeProjection? params stx then
    pure fieldTy
  else
    inferPureExprType fields constDecls immutableDecls externalDecls params locals stx

private def translateBindSource
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (rhs : Term) : CommandElabM Term := do
  let rhs := stripParens rhs
  match rhs with
  | `(term| getStorage $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar .uint256 | .scalar .int256 | .scalar (.newtype _ .uint256) | .scalar (.adt _ _) =>
          `(Compiler.CompilationModel.Expr.storage $(strTerm f.name))
      | .scalar .bool => throwErrorAt rhs s!"field '{f.name}' is Bool; encode as Uint256 and use getStorage"
      | .scalar .address | .scalar (.newtype _ .address) =>
          throwErrorAt rhs s!"field '{f.name}' is Address; use getStorageAddr"
      | .scalar .unit => throwErrorAt rhs "invalid field type"
      | .dynamicArray _ => throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | _ => throwErrorAt rhs s!"field '{f.name}' is a mapping; use getMapping/getMapping2/getMappingN"
  | `(term| getStorageAddr $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar .address | .scalar (.newtype _ .address) =>
          `(Compiler.CompilationModel.Expr.storageAddr $(strTerm f.name))
      | .scalar .uint256 | .scalar (.newtype _ .uint256) =>
          throwErrorAt rhs s!"field '{f.name}' is Uint256; use getStorage"
      | .scalar .bool => throwErrorAt rhs s!"field '{f.name}' is Bool; use getStorage"
      | .scalar .unit => throwErrorAt rhs "invalid field type"
      | .dynamicArray _ => throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | _ => throwErrorAt rhs s!"field '{f.name}' is a mapping; use getMapping/getMapping2/getMappingN"
  | `(term| getStorageArrayLength $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ =>
          `(Compiler.CompilationModel.Expr.storageArrayLength $(strTerm f.name))
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a storage dynamic array"
  | `(term| getStorageArrayElement $field:ident $index:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ =>
          `(Compiler.CompilationModel.Expr.storageArrayElement
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals index))
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a storage dynamic array"
  | `(term| getMapping $field:ident $key:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 =>
          `(Compiler.CompilationModel.Expr.mapping $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key))
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Expr.mappingUint $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key))
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMappingAddr $field:ident $key:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 =>
          `(Compiler.CompilationModel.Expr.mapping $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key))
      | .mappingUintToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is Uint256-keyed; use getMappingUintAddr"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMappingUint $field:ident $key:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Expr.mappingUint $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key))
      | .mappingAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is Address-keyed; use getMapping"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMappingUintAddr $field:ident $key:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Expr.mappingUint $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key))
      | .mappingAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is Address-keyed; use getMappingAddr"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| getMappingWord $field:ident $key:term $wordOffset:num) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Expr.mappingWord
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $wordOffset)
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt rhs s!"field '{f.name}' is a double mapping; use getMapping2Word"
      | .mappingStruct _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember"
      | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a nested struct mapping; use structMember2"
      | .dynamicArray _ =>
          throwErrorAt rhs s!"field '{f.name}' is a storage dynamic array; use getStorageArrayLength/getStorageArrayElement"
      | .scalar _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
      | .mappingChain _ =>
          throwErrorAt rhs s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use getMappingN"
  | `(term| getMapping2 $field:ident $key1:term $key2:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mapping2AddressToAddressToUint256 =>
          `(Compiler.CompilationModel.Expr.mapping2
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2))
      | .mappingStruct2 _ _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a nested struct mapping; use structMember2"
      | .mappingStruct _ _ =>
          throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember"
      | _ => throwErrorAt rhs s!"field '{f.name}' is not a double mapping"
  | `(term| getMappingN $field:ident $keys:term) => do
      let f ← lookupStorageField fields (toString field.getId)
      let keyTerms ← expectMappingKeyTerms keys
      match storageTypeMappingKeyTypes? f.ty with
      | some keyTypes =>
          if keyTerms.size != keyTypes.length then
            throwErrorAt rhs s!"field '{f.name}' expects {keyTypes.length} mapping keys, but getMappingN received {keyTerms.size}"
          let keyExprs ← keyTerms.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          `(Compiler.CompilationModel.Expr.mappingChain
              $(strTerm f.name)
              [ $[$keyExprs],* ])
      | none =>
          match f.ty with
          | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
              throwErrorAt rhs s!"field '{f.name}' is a struct-valued mapping; use structMember/structMember2"
          | _ => throwErrorAt rhs s!"field '{f.name}' is not a mapping"
  | `(term| structMember $field:term $key:term $member:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName false
      `(Compiler.CompilationModel.Expr.structMember
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
          $(strTerm memberName))
  | `(term| structMember2 $field:term $key1:term $key2:term $member:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName true
      `(Compiler.CompilationModel.Expr.structMember2
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2)
          $(strTerm memberName))
  | `(term| msgSender) | `(term| Verity.msgSender) => `(Compiler.CompilationModel.Expr.caller)
  | `(term| msgValue) | `(term| Verity.msgValue) => `(Compiler.CompilationModel.Expr.msgValue)
  | `(term| selfBalance) | `(term| Verity.selfBalance) =>
      `(Compiler.CompilationModel.Expr.selfBalance)
  | `(term| blockTimestamp) | `(term| Verity.blockTimestamp) =>
      `(Compiler.CompilationModel.Expr.blockTimestamp)
  | `(term| blockNumber) | `(term| Verity.blockNumber) =>
      `(Compiler.CompilationModel.Expr.blockNumber)
  | `(term| blobbasefee) | `(term| Verity.blobbasefee) =>
      `(Compiler.CompilationModel.Expr.blobbasefee)
  | `(term| contractAddress) | `(term| Verity.contractAddress) =>
      `(Compiler.CompilationModel.Expr.contractAddress)
  | `(term| chainid) | `(term| Verity.chainid) =>
      `(Compiler.CompilationModel.Expr.chainid)
  | `(term| tload $offset:term) =>
      `(Compiler.CompilationModel.Expr.tload
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset))
  | _ =>
      match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals rhs with
      | some (fn, argTerms) =>
          ensureSupportsInternalHelperSpec rhs fn
          let argExprs ← translateInternalHelperCallArgs
            fields constDecls immutableDecls params locals fn argTerms
          `(Compiler.CompilationModel.Expr.internalCall
              $(strTerm (internalHelperSpecNameFor fn))
              [ $[$argExprs],* ])
      | none =>
          match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
          | some (qualifiedName, argTerms) =>
              let argExprs ← argTerms.mapM
                (translatePureExprWithTypes fields constDecls immutableDecls params locals)
              `(Compiler.CompilationModel.Expr.internalCall
                  $(strTerm (qualifiedInternalHelperName functions qualifiedName))
                  [ $[$argExprs],* ])
          | none =>
              throwErrorAt rhs
                "unsupported bind source; expected getStorage/getStorageAddr/getStorageArrayLength/getStorageArrayElement/getMapping/getMappingAddr/getMappingUint/getMappingUintAddr/getMappingWord/getMapping2/getMappingN/structMember/structMember2/msgSender/msgValue/selfBalance/tload/ecrecover, a direct internal helper call, or a qualified library helper call"

private def translateSafeRequireBind
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (varName : String)
    (rhs : Term) : CommandElabM (Option (Array Term)) := do
  let translateSafeUintGuardAndValue (optExpr : Term) (label : String) :
      CommandElabM (Term × Term) := do
    match stripParens optExpr with
    | `(term| safeAdd $a:term $b:term) =>
        let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
        let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
        let valueExpr : Term ← `(Compiler.CompilationModel.Expr.add $aExpr $bExpr)
        let guardExpr : Term ← `(Compiler.CompilationModel.Expr.ge $valueExpr $aExpr)
        pure (guardExpr, valueExpr)
    | `(term| safeSub $a:term $b:term) =>
        let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
        let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
        let valueExpr : Term ← `(Compiler.CompilationModel.Expr.sub $aExpr $bExpr)
        let guardExpr : Term ← `(Compiler.CompilationModel.Expr.ge $aExpr $bExpr)
        pure (guardExpr, valueExpr)
    | `(term| safeMul $a:term $b:term) =>
        let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
        let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
        let valueExpr : Term ← `(Compiler.CompilationModel.Expr.mul $aExpr $bExpr)
        let zeroExpr : Term ← `(Compiler.CompilationModel.Expr.literal 0)
        let divisorZeroExpr : Term ← `(Compiler.CompilationModel.Expr.eq $bExpr $zeroExpr)
        let quotientExpr : Term ← `(Compiler.CompilationModel.Expr.div $valueExpr $bExpr)
        let noOverflowExpr : Term ← `(Compiler.CompilationModel.Expr.eq $quotientExpr $aExpr)
        let guardExpr : Term ← `(Compiler.CompilationModel.Expr.logicalOr $divisorZeroExpr $noOverflowExpr)
        pure (guardExpr, valueExpr)
    | `(term| safeDiv $a:term $b:term) =>
        let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
        let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
        let valueExpr : Term ← `(Compiler.CompilationModel.Expr.div $aExpr $bExpr)
        let zeroExpr : Term ← `(Compiler.CompilationModel.Expr.literal 0)
        let guardExpr : Term ←
          `(Compiler.CompilationModel.Expr.logicalNot
              (Compiler.CompilationModel.Expr.eq $bExpr $zeroExpr))
        pure (guardExpr, valueExpr)
    | _ =>
        throwErrorAt rhs s!"unsupported {label} source; expected safeAdd, safeSub, safeMul, or safeDiv"
  let rhs := stripParens rhs
  match rhs with
  | `(term| requireSomeUint $optExpr:term $msg:term) =>
      let msgLit ← strTerm <$> expectStringLiteral msg
      let (guardExpr, valueExpr) ← translateSafeUintGuardAndValue optExpr "requireSomeUint"
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.require $guardExpr $msgLit)),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  -- Typed-error counterpart to `requireSomeUint`. The lowering mirrors the
  -- string-message variant exactly, except the guard becomes a
  -- `Stmt.requireError` emitting a 4-byte selector revert with the supplied
  -- error name and argument list.
  | `(term| requireSomeUintError $optExpr:term $errorName:ident($args,*)) =>
      let errorNameLit := strTerm (toString errorName.getId)
      let argExprs ← args.getElems.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
      let (guardExpr, valueExpr) ← translateSafeUintGuardAndValue optExpr "requireSomeUintError"
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.requireError $guardExpr $errorNameLit [ $[$argExprs],* ])),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  -- Solidity-0.8 default-revert arithmetic (verity#1752): `let x ← addPanic a b`
  -- lowers to the same IR as
  -- `let x ← requireSomeUint (safeAdd a b) "Panic(0x11): arithmetic overflow"`,
  -- and analogously for `subPanic` / `mulPanic` / `divPanic`. The fixed
  -- message mirrors Solidity 0.8's `Panic(0x11)` (overflow / underflow)
  -- and `Panic(0x12)` (division by zero) opcodes.
  | `(term| addPanic $a:term $b:term) =>
      let msgLit := strTerm "Panic(0x11): arithmetic overflow"
      let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
      let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
      let valueExpr : Term ← `(Compiler.CompilationModel.Expr.add $aExpr $bExpr)
      let guardExpr : Term ← `(Compiler.CompilationModel.Expr.ge $valueExpr $aExpr)
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.require $guardExpr $msgLit)),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  | `(term| subPanic $a:term $b:term) =>
      let msgLit := strTerm "Panic(0x11): arithmetic underflow"
      let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
      let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
      let valueExpr : Term ← `(Compiler.CompilationModel.Expr.sub $aExpr $bExpr)
      let guardExpr : Term ← `(Compiler.CompilationModel.Expr.ge $aExpr $bExpr)
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.require $guardExpr $msgLit)),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  | `(term| mulPanic $a:term $b:term) =>
      let msgLit := strTerm "Panic(0x11): arithmetic overflow"
      let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
      let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
      let valueExpr : Term ← `(Compiler.CompilationModel.Expr.mul $aExpr $bExpr)
      let zeroExpr : Term ← `(Compiler.CompilationModel.Expr.literal 0)
      let divisorZeroExpr : Term ← `(Compiler.CompilationModel.Expr.eq $bExpr $zeroExpr)
      let quotientExpr : Term ← `(Compiler.CompilationModel.Expr.div $valueExpr $bExpr)
      let noOverflowExpr : Term ← `(Compiler.CompilationModel.Expr.eq $quotientExpr $aExpr)
      let guardExpr : Term ← `(Compiler.CompilationModel.Expr.logicalOr $divisorZeroExpr $noOverflowExpr)
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.require $guardExpr $msgLit)),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  | `(term| divPanic $a:term $b:term) =>
      let msgLit := strTerm "Panic(0x12): division by zero"
      let aExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals a
      let bExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals b
      let valueExpr : Term ← `(Compiler.CompilationModel.Expr.div $aExpr $bExpr)
      let zeroExpr : Term ← `(Compiler.CompilationModel.Expr.literal 0)
      let guardExpr : Term ←
        `(Compiler.CompilationModel.Expr.logicalNot
            (Compiler.CompilationModel.Expr.eq $bExpr $zeroExpr))
      pure (some #[
        (← `(Compiler.CompilationModel.Stmt.require $guardExpr $msgLit)),
        (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $valueExpr))
      ])
  | _ => pure none

private def lookupFunctionByNameAndArity
    (functions : Array FunctionDecl)
    (name : String)
    (arity : Nat) : Option FunctionDecl :=
  functions.find? fun fn => fn.name == name && fn.params.size == arity

private partial def flattenAppSyntax (stx : Term) : Term × Array Term :=
  let stx := stripParens stx
  match stx.raw with
  | .node _ `Lean.Parser.Term.app appArgs =>
      let head : Term := ⟨appArgs.getD 0 Syntax.missing⟩
      let argTerms := (appArgs.getD 1 Syntax.missing).getArgs.map (fun syn => ⟨syn⟩)
      let (head, priorArgs) := flattenAppSyntax head
      (head, priorArgs ++ argTerms)
  | _ => (stx, #[])

private partial def typedDotCallSyntax? (stx : Term) : Option (Term × String × Array Term) :=
  let (head, argTerms) := flattenAppSyntax stx
  match head.raw with
  | .ident _ _ raw _ =>
      match nameComponents raw with
      | [targetName, methodName] =>
          let target : Term := mkIdent (Name.mkSimple targetName)
          some (target, methodName, argTerms)
      | _ => none
  | _ => none

private partial def resolveTypedInterfaceCall?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option (ExternalDecl × Term × Array Term × Option ValueType × Nat)) := do
  let some (target, methodName, argTerms) := typedDotCallSyntax? stx
    | pure none
  let targetName ←
    match stripParens target with
    | `(term| $targetIdent:ident) => pure (toString targetIdent.getId)
    | _ => pure ""
  let some interfaceName := lookupInterfaceName? params locals targetName
    | pure none
  let externalName := interfaceExternalName interfaceName methodName
  let some ext := externalDecls.find? (fun ext => ext.name == externalName)
    | throwErrorAt stx s!"interface '{interfaceName}' has no method '{methodName}'"
  if argTerms.size != ext.params.size then
    throwErrorAt stx s!"interface call '{interfaceName}.{methodName}' expects {ext.params.size} argument(s), got {argTerms.size}"
  for (argTerm, expectedTy) in argTerms.zip ext.params do
    let actualTy ← inferPureExprType fields constDecls immutableDecls externalDecls params locals argTerm
    unless actualTy == expectedTy || (isNatLiteralTerm argTerm && numericLiteralCompatibleValueType expectedTy) do
      throwErrorAt argTerm
        s!"interface call '{interfaceName}.{methodName}' argument expects {renderValueType expectedTy}, got {renderValueType actualTy}"
  -- the selector is computed from the params only, so a void method's canonical
  -- signature is identical to its non-void counterpart (e.g. aave
  -- `supply(address,uint256,address,uint16)`).
  let selector := Compiler.keccak256_first_4_bytes (interfaceFunctionSignature methodName ext.params)
  match ext.returnTys.toList with
  | [retTy] => pure (some (ext, target, argTerms, some retTy, selector))
  | [] =>
      -- void interface method lowers to the no-return ECM, whose calldata is
      -- `selector ++ numArgs*32` (one word per arg). A dynamic/composite param
      -- would need head/tail ABI framing and is rejected here rather than
      -- silently mis-encoded. (lfglabs-dev/verity#1956)
      for paramTy in ext.params do
        unless isStaticSingleWordValueType paramTy do
          throwErrorAt stx
            s!"void interface call '{interfaceName}.{methodName}' has a {renderValueType paramTy} parameter; the no-return call path only supports static single-word arguments (uint*/int256/address/bytes32/bool). Dynamic or composite parameters are not yet supported."
      pure (some (ext, target, argTerms, none, selector))
  | _ => throwErrorAt stx s!"interface call '{interfaceName}.{methodName}' returns multiple values; typed dot calls currently support one return value"

mutual
private partial def validateDoSeqExprTypes
    (ownerName : String)
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (errorDecls : Array ErrorDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (doSeq : DoSeq) : CommandElabM Unit := do
  match doSeq with
  | `(doSeq| $[$elems:doElem]*) =>
      let _ ← validateDoElemsExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params locals elems
      pure ()
  | _ => throwErrorAt doSeq "unsupported branch body; expected do-sequence"

private partial def validateDoElemsExprTypes
    (ownerName : String)
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (errorDecls : Array ErrorDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (elems : Array (TSyntax `doElem)) : CommandElabM (Array TypedLocal) := do
  let mut branchLocals := locals
  for elem in elems do
    branchLocals ← validateDoElemExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params branchLocals elem
  pure branchLocals

private partial def validateDoElemExprTypes
    (ownerName : String)
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (errorDecls : Array ErrorDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (elem : TSyntax `doElem) : CommandElabM (Array TypedLocal) := do
  let tupleCase? ← do
    let stx := elem.raw
    if stx.getKind == `Lean.Parser.Term.doLet then
      let decl := stx[2]
      let patDecl := decl[0]
      match tupleBinderNames? patDecl[0] with
      | some names =>
          let rhs : Term := ⟨patDecl[4]⟩
          match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
          | some (qualifiedName, _) =>
              let typedNames ← unsafe qualifiedTupleBindTypedLocals patDecl qualifiedName names
              pure (some (locals ++ typedNames))
          | none =>
              match (← tryExternalCallBindStmt? fields constDecls immutableDecls externalDecls params locals rhs names) with
              | some (_, typedNames) => pure (some (locals ++ typedNames))
              | none =>
                  match (← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs) with
                  | some valueTys =>
                      if names.size != valueTys.size then
                        throwErrorAt patDecl s!"tuple destructuring binds {names.size} names, but the source provides {valueTys.size} values"
                      for (name?, ty) in names.zip valueTys do
                        if let some name := name? then
                          requireSupportedLocalBindingType patDecl s!"local binding '{name}'" ty
                      let typedNames := (names.zip valueTys).filterMap fun (name?, ty) =>
                        name?.map (fun name => mkTypedLocal name ty)
                      pure (some (locals ++ typedNames))
                  | none => pure none
      | none => pure none
    else if stx.getKind == `Lean.Parser.Term.doLetArrow then
      let patDecl := stx[2]
      match tupleBinderNames? patDecl[0] with
      | some names =>
          let rhs : Term := ⟨patDecl[2][0]⟩
          match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
          | some (qualifiedName, _) =>
              let typedNames ← unsafe qualifiedTupleBindTypedLocals patDecl qualifiedName names
              pure (some (locals ++ typedNames))
          | none =>
              match (← tryExternalCallBindStmt? fields constDecls immutableDecls externalDecls params locals rhs names) with
              | some (_, typedNames) => pure (some (locals ++ typedNames))
              | none =>
                  match (← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs) with
                  | some valueTys =>
                      if names.size != valueTys.size then
                        throwErrorAt patDecl s!"tuple destructuring binds {names.size} names, but the source provides {valueTys.size} values"
                      for (name?, ty) in names.zip valueTys do
                        if let some name := name? then
                          requireSupportedLocalBindingType patDecl s!"local binding '{name}'" ty
                      let typedNames := (names.zip valueTys).filterMap fun (name?, ty) =>
                        name?.map (fun name => mkTypedLocal name ty)
                      pure (some (locals ++ typedNames))
                  | none => pure none
      | none => pure none
    else
      pure none
  match tupleCase? with
  | some typedLocals => pure typedLocals
  | none => match elem with
      | `(doElem| let mut $name:ident := $rhs:term) =>
          let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals rhs
          requireSupportedLocalBindingType name s!"local binding '{toString name.getId}'" ty
          let interfaceName? ← interfaceNameOfTerm? params locals rhs
          pure <| locals.push (mkTypedLocal (toString name.getId) ty interfaceName?)
      | `(doElem| let $name:ident := $rhs:term) =>
          match arrayElementAliasSource? params rhs with
          | some (paramName, index, elemTy) =>
              requireWordLikeType index "arrayElement alias index"
                (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index)
              pure <| locals.push
                { name := toString name.getId
                  ty := elemTy
                  source := .arrayElement paramName index elemTy }
          | none =>
              let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals rhs
              requireSupportedLocalBindingType name s!"local binding '{toString name.getId}'" ty
              let interfaceName? ← interfaceNameOfTerm? params locals rhs
              pure <| locals.push (mkTypedLocal (toString name.getId) ty interfaceName?)
      | `(doElem| let $name:ident ← $rhs:term) =>
          match stripParens rhs with
          | `(term| allocArray $len:term) =>
              requireWordLikeType len "allocArray length"
                (← inferPureExprType fields constDecls immutableDecls externalDecls params locals len)
              pure <| locals.push
                { name := toString name.getId
                  ty := .array .uint256
                  source := .memoryArray }
          | _ =>
              match ← localInternalArrayReturnBind? fields constDecls immutableDecls externalDecls functions params locals rhs with
              | some (_, _, elemTy) =>
                  pure <| locals.push
                    { name := toString name.getId
                      ty := .array elemTy
                      source := .memoryArray }
              | none =>
                  -- requireSomeUintError is the only bind source that takes
                  -- a custom-error name; validate the error name against the
                  -- contract's `errors` block before falling through to the
                  -- generic bind-source typer.
                  match stripParens rhs with
                  | `(term| requireSomeUintError $_optExpr:term $errorName:ident($args,*)) =>
                      for arg in args.getElems do
                        let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg
                      validateCustomErrorCall ownerName (toString errorName.getId)
                        params errorDecls args.getElems
                  | _ => pure ()
                  match ← resolveTypedInterfaceCall? fields constDecls immutableDecls externalDecls params locals rhs with
                  | some (_, _, _, some retTy, _) =>
                      requireSupportedLocalBindingType name s!"local binding '{toString name.getId}'" retTy
                      pure <| locals.push (mkTypedLocal (toString name.getId) retTy)
                  | some (_, _, _, none, _) =>
                      -- void interface method: cannot bind its (empty) result
                      throwErrorAt rhs s!"interface call '{toString name.getId}' binds a void method; call it as a statement, not `let ... ←`"
                  | none =>
                      let ty ← inferBindSourceType fields constDecls immutableDecls externalDecls functions params locals rhs
                      requireSupportedLocalBindingType name s!"local binding '{toString name.getId}'" ty
                      pure <| locals.push (mkTypedLocal (toString name.getId) ty)
      | `(doElem| $name:ident := $rhs:term) =>
          let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals rhs
          pure locals
      | `(doElem| return $value:term) =>
          let _ ←
            match (← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals value) with
            | some _ => pure .unit
            | none => inferPureExprType fields constDecls immutableDecls externalDecls params locals value
          pure locals
      | `(doElem| pure ()) =>
          pure locals
      | `(doElem| if $cond:term then $thenBranch:doSeq else $elseBranch:doSeq) =>
          requireBoolType cond "if condition" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals cond)
          validateDoSeqExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params locals thenBranch
          validateDoSeqExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params locals elseBranch
          pure locals
      | `(doElem| forEach $name:term $count:term $body:term) =>
          requireWordLikeType count "forEach count" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals count)
          match stripParens body with
          | `(term| do $[$inner:doElem]*) =>
              let _ ← validateDoElemsExprTypes
                ownerName fields constDecls immutableDecls externalDecls errorDecls functions params
                (locals.push (mkTypedLocal (← expectStringOrIdent name) .uint256))
                inner
              pure locals
          | _ => throwErrorAt body "forEach body must be a do block"
      | `(doElem| requireError $cond:term $errorName:ident($args,*)) =>
          requireBoolType cond "requireError condition" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals cond)
          for arg in args.getElems do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg
          validateCustomErrorCall ownerName (toString errorName.getId)
            params errorDecls args.getElems
          pure locals
      | `(doElem| revert $errorName:ident($args,*)) =>
          for arg in args.getElems do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg
          validateCustomErrorCall ownerName (toString errorName.getId)
            params errorDecls args.getElems
          pure locals
      | `(doElem| revertError $errorName:ident($args,*)) =>
          for arg in args.getElems do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg
          validateCustomErrorCall ownerName (toString errorName.getId)
            params errorDecls args.getElems
          pure locals
      | `(doElem| tryCatch $attempt:term $handler:term) => do
          requireWordLikeType attempt "tryCatch attempt"
            (← inferPureExprType fields constDecls immutableDecls externalDecls params locals attempt)
          let (payloadName?, catchElems) ← parseTryCatchHandler handler
          validateTryCatchHandlerDoesNotUsePayload handler payloadName? catchElems
          let _ ← validateDoElemsExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params locals catchElems
          pure locals
      | `(doElem| unsafe $_reason:str do $body:doSeq) =>
          validateDoSeqExprTypes ownerName fields constDecls immutableDecls externalDecls errorDecls functions params locals body
          pure locals
      | `(doElem| ecmBind $names:term $module:term $args:term) =>
          let resultVars ← expectStringList names
          ensureFreshLocalNames (typedLocalNames locals) (resultVars.map some) names
          validateEcmExprListLiteral fields constDecls immutableDecls externalDecls params locals
            args "ECM argument"
          validateResultEcmModuleTerm module resultVars
          pure <| locals ++ resultVars.map (fun name => mkTypedLocal name .uint256)
      | `(doElem| emit $_eventName:term $values:term) =>
          match stripParens values with
          | `(term| [ $[$args],* ]) =>
              let mut branchLocals := locals
              for arg in args do
                match ← localInternalArrayReturnBind? fields constDecls immutableDecls externalDecls functions params branchLocals arg with
                | some (_, _, elemTy) =>
                    let tempName := freshSyntheticLocalName "emit_array" params branchLocals #[]
                    branchLocals := branchLocals.push
                      { name := tempName
                        ty := .array elemTy
                        source := .memoryArray }
                | none =>
                    let _ ← inferEmitArgExprType fields constDecls immutableDecls externalDecls params branchLocals arg
                    pure ()
              pure locals
          | _ => throwErrorAt values "expected list literal [..]"
      | `(doElem| $stmt:term) =>
          validateEffectStmtExprTypes fields constDecls immutableDecls externalDecls functions params locals stmt
          pure locals
      | _ => throwErrorAt elem "unsupported do element"

private partial def validateEffectStmtExprTypes
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM Unit := do
  let stx := stripParens stx
  match stx with
  | `(term| safeTransfer $token:term $to:term $amount:term) =>
      for arg in [token, to, amount] do
        requireWordLikeType arg "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
  | `(term| safeTransferFrom $token:term $fromAddr:term $to:term $amount:term) =>
      for arg in [token, fromAddr, to, amount] do
        requireWordLikeType arg "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
  | `(term| safeApprove $token:term $spender:term $amount:term) =>
      for arg in [token, spender, amount] do
        requireWordLikeType arg "ERC-20 helper" (← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg)
  | `(term| setStorage $field:ident $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.adtInfo?, f.ty with
      | some _, _ => pure ()
      | none, .scalar (.adt _ _) => pure ()
      | _, _ =>
          let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
          pure ()
  | `(term| setStorageAddr $_field:ident $value:term)
    | `(term| require $value:term $_msg) =>
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
      pure ()
  | `(term| setPackedStorage $_field:ident $_wordOffset:num $value:term) =>
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
      pure ()
  | `(term| pushStorageArray $_field:ident $value:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
      pure ()
  | `(term| popStorageArray $_field:ident) =>
      pure ()
  | `(term| setStorageArrayElement $_field:ident $index:term $value:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals index
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
      pure ()
  | `(term| setMapping $_field:ident $key:term $value:term) | `(term| setMappingAddr $_field:ident $key:term $value:term)
    | `(term| setMappingUint $_field:ident $key:term $value:term) | `(term| setMappingUintAddr $_field:ident $key:term $value:term)
    | `(term| setMappingWord $_field:ident $key:term $_wordOffset:num $value:term)
    | `(term| setStructMember $_field:term $key:term $_member:term $value:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals key
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
  | `(term| setMapping2 $_field:ident $key1:term $key2:term $value:term)
    | `(term| setStructMember2 $_field:term $key1:term $key2:term $_member:term $value:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals key1
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals key2
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
  | `(term| setMappingN $_field:ident $keys:term $value:term) => do
      for key in (← expectMappingKeyTerms keys) do
        let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals key
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
  | `(term| setMemoryArrayElement $name:term $index:term $value:term) => do
      let (_, elemTy) ← requireSupportedMemoryArrayLocal name "setMemoryArrayElement" locals
      unless isSingleWordStaticValueType elemTy do
        throwErrorAt name s!"setMemoryArrayElement currently supports only Array<wordLike> memory locals, got Array {renderValueType elemTy}"
      requireWordLikeType index "memory array index"
        (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index)
      requireDeclaredValueType value "setMemoryArrayElement value" elemTy
        (← inferPureExprType fields constDecls immutableDecls externalDecls params locals value)
      pure ()
  | `(term| mstore $offset:term $value:term) | `(term| tstore $offset:term $value:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals offset
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals value
  | `(term| calldatacopy $destOffset:term $sourceOffset:term $size:term)
    | `(term| returndataCopy $destOffset:term $sourceOffset:term $size:term) => do
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals destOffset
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals sourceOffset
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals size
  | `(term| rawLog $topics:term $dataOffset:term $dataSize:term) => do
      match stripParens topics with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals x
      | _ => throwErrorAt topics "expected list literal [..]"
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals dataOffset
      let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals dataSize
  | `(term| returnValues $values:term) => do
      match stripParens values with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals x
      | _ => throwErrorAt values "expected list literal [..]"
      pure ()
  | `(term| emit $_eventName:term $values:term) => do
      match stripParens values with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            let _ ← inferEmitArgExprType fields constDecls immutableDecls externalDecls params locals x
      | _ => throwErrorAt values "expected list literal [..]"
      pure ()
  | `(term| ecmDo $_module:term $args:term) => do
      validateEcmExprListLiteral fields constDecls immutableDecls externalDecls params locals
        args "ECM argument"
      pure ()
  | `(term| returnArray $name:term) => do
      match localMemoryArray? locals name with
      | some (_, elemTy) =>
          unless isSingleWordStaticValueType elemTy do
            throwErrorAt name s!"returnArray currently supports only Array<wordLike> memory locals, got Array {renderValueType elemTy}"
      | none =>
          let ty ← requireDirectParamRef name "returnArray" params
          requireSupportedReturnArrayType name "returnArray" ty
  | `(term| returnBytes $name:term) => do
      let ty ← requireDirectParamRef name "returnBytes" params
      requireSupportedReturnBytesType name "returnBytes" ty
  | `(term| returnStorageWords $name:term) => do
      let ty ← requireDirectParamRef name "returnStorageWords" params
      requireSupportedReturnStorageWordsType name "returnStorageWords" ty
  | `(term| externalCallBind $_names:term $_fnName:term $args:term)
    | `(term| tryExternalCallBind $_successVar:term $_names:term $_fnName:term $args:term) =>
      match stripParens args with
      | `(term| [ $[$xs],* ]) =>
          for x in xs do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals x
      | _ => throwErrorAt args "expected list literal [..]"
  | `(term| revertReturndata) =>
      pure ()
  | _ =>
      -- a typed interface call in statement position is only valid when the
      -- method is void; `resolveTypedInterfaceCall?` already validates arg
      -- count and arg types.
      match ← resolveTypedInterfaceCall? fields constDecls immutableDecls externalDecls params locals stx with
      | some (_, _, _, none, _) => pure ()
      | some (_, _, _, some retTy, _) =>
          throwErrorAt stx
            s!"interface call returns {renderValueType retTy}; bind it with `let ... ← ...`"
      | none =>
      match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals stx with
      | some (fn, argTerms) =>
          ensureSupportsInternalHelperSpec stx fn
          if fn.returnTy != .unit then
            throwErrorAt stx
              s!"helper call '{fn.name}' returns {renderValueType fn.returnTy}; use `let ... ← {fn.name} ...` or tuple destructuring"
          for arg in argTerms do
            let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals arg
          pure ()
      | none =>
          let _ ← inferPureExprType fields constDecls immutableDecls externalDecls params locals stx
          pure ()
end

private def validateFunctionBodyExprTypes
    (fields : Array StorageFieldDecl)
    (errorDecls : Array ErrorDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (fn : FunctionDecl) : CommandElabM Unit := do
  match fn.body with
  | `(term| do $[$elems:doElem]*) =>
      let _ ← validateDoElemsExprTypes fn.name fields constDecls immutableDecls externalDecls errorDecls functions fn.params #[] elems
      pure ()
  | _ => throwErrorAt fn.body "function body must be a do block"

private def validateConstantExprTypes
    (constDecls : Array ConstantDecl) : CommandElabM Unit := do
  for constant in constDecls do
    let inferredTy ← inferPureExprType #[] constDecls #[] #[] #[] #[] constant.body
    requireDeclaredValueType constant.body s!"constant '{constant.name}'" constant.ty inferredTy

private def validateConstructorBodyExprTypes
    (fields : Array StorageFieldDecl)
    (errorDecls : Array ErrorDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (ctor : ConstructorDecl) : CommandElabM Unit := do
  match ctor.body with
  | `(term| do $[$elems:doElem]*) =>
      let _ ← validateDoElemsExprTypes "constructor" fields constDecls immutableDecls externalDecls errorDecls functions ctor.params #[] elems
      pure ()
  | _ => throwErrorAt ctor.body "constructor body must be a do block"

private def translateERC20BindStmt?
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (varName : String)
    (rhs : Term) : CommandElabM (Option Term) := do
  let rhs := stripParens rhs
  match rhs with
  | `(term| balanceOf $token:term $owner:term) =>
      match lookupFunctionByNameAndArity functions "balanceOf" 2 with
      | some localFn =>
          throwErrorAt rhs
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | none =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          let ownerExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals owner
          pure <| some (← `(Compiler.CompilationModel.Stmt.ecm
            (Compiler.Modules.ERC20.balanceOfModule $(strTerm varName))
            [$tokenExpr, $ownerExpr]))
  | `(term| allowance $token:term $owner:term $spender:term) =>
      match lookupFunctionByNameAndArity functions "allowance" 3 with
      | some localFn =>
          throwErrorAt rhs
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | none =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          let ownerExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals owner
          let spenderExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals spender
          pure <| some (← `(Compiler.CompilationModel.Stmt.ecm
            (Compiler.Modules.ERC20.allowanceModule $(strTerm varName))
            [$tokenExpr, $ownerExpr, $spenderExpr]))
  | `(term| totalSupply $token:term) =>
      match lookupFunctionByNameAndArity functions "totalSupply" 1 with
      | some localFn =>
          throwErrorAt rhs
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | none =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          pure <| some (← `(Compiler.CompilationModel.Stmt.ecm
            (Compiler.Modules.ERC20.totalSupplyModule $(strTerm varName))
            [$tokenExpr]))
  | _ => pure none

private def adtConstructorApp? (stx : Term) : Option (Ident × Array Term) :=
  let stx := stripParens stx
  match stx with
  | `(term| $ctor:ident) => some (ctor, #[])
  | `(term| $ctor:ident $args:term*) => some (ctor, args)
  | _ => none

private def adtConstructorSyntax? (stx : Term) : Option (String × Array Term) :=
  let stx := stripParens stx
  match stx with
  | `(term| $variant:str) => some (variant.getString, #[])
  | `(term| ($variant:str, [ $[$args:term],* ])) => some (variant.getString, args)
  | `(term| adt $variant:str) => some (variant.getString, #[])
  | `(term| adt $variant:str [ $[$args:term],* ]) => some (variant.getString, args)
  | _ =>
      match adtConstructorApp? stx with
      | some (variant, args) =>
          if toString variant.getId == "adt" then
            match args with
            | #[arg] =>
                match stripParens arg with
                | `(term| $variant:str) => some (variant.getString, #[])
                | _ => none
            | #[arg, argList] =>
                match stripParens arg, stripParens argList with
                | `(term| $variant:str), `(term| [ $[$payloadArgs:term],* ]) =>
                    some (variant.getString, payloadArgs)
                | _, _ => none
            | _ => none
          else
            some (toString variant.getId, args)
      | none => none

private def translateAdtConstructForStorage
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (adtName : String)
    (value : Term) : CommandElabM Term := do
  match adtConstructorSyntax? value with
  | some (variantName, args) =>
      let argExprs ← args.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
      `(Compiler.CompilationModel.Expr.adtConstruct
          $(strTerm adtName)
          $(strTerm variantName)
          [ $[$argExprs],* ])
  | none =>
      throwErrorAt value
        s!"ADT storage assignment for '{adtName}' must use a variant constructor so payload slots are preserved"

private def storageFieldAdtName? (field : StorageFieldDecl) : Option String :=
  match field.adtInfo? with
  | some (adtName, _) => some adtName
  | none =>
      match field.ty with
      | .scalar (.adt adtName _) => some adtName
      | _ => none

private def translateEffectStmt
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM Term := do
  let stx := stripParens stx
  match stx with
  | `(term| safeTransfer $token:term $to:term $amount:term) =>
      match lookupFunctionByNameAndArity functions "safeTransfer" 3 with
      | some localFn =>
          throwErrorAt stx
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | _ =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          let toExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals to
          let amountExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals amount
          `(Compiler.CompilationModel.Stmt.ecm
              Compiler.Modules.ERC20.safeTransferModule
              [$tokenExpr, $toExpr, $amountExpr])
  | `(term| safeTransferFrom $token:term $fromAddr:term $to:term $amount:term) =>
      match lookupFunctionByNameAndArity functions "safeTransferFrom" 4 with
      | some localFn =>
          throwErrorAt stx
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | _ =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          let fromExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals fromAddr
          let toExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals to
          let amountExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals amount
          `(Compiler.CompilationModel.Stmt.ecm
              Compiler.Modules.ERC20.safeTransferFromModule
              [$tokenExpr, $fromExpr, $toExpr, $amountExpr])
  | `(term| safeApprove $token:term $spender:term $amount:term) =>
      match lookupFunctionByNameAndArity functions "safeApprove" 3 with
      | some localFn =>
          throwErrorAt stx
            s!"ERC-20 helper form '{localFn.name}' conflicts with contract function '{localFn.name}'; rename the function or avoid the direct helper syntax here"
      | _ =>
          let tokenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals token
          let spenderExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals spender
          let amountExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals amount
          `(Compiler.CompilationModel.Stmt.ecm
              Compiler.Modules.ERC20.safeApproveModule
              [$tokenExpr, $spenderExpr, $amountExpr])
  | `(term| ecmDo $module:term $args:term) =>
      validateEffectOnlyEcmModuleTerm module
      let argExprs ← expectExprList fields constDecls immutableDecls params locals args
      `(Compiler.CompilationModel.Stmt.ecm
          $module
          [ $[$argExprs],* ])
  | `(term| setStorage $field:ident $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match storageFieldAdtName? f with
      | some adtName =>
          `(Compiler.CompilationModel.Stmt.setStorage
              $(strTerm f.name)
              $(← translateAdtConstructForStorage fields constDecls immutableDecls params locals adtName value))
      | none =>
          match f.ty with
          | .scalar .uint256 | .scalar .int256 | .scalar (.newtype _ .uint256) =>
              `(Compiler.CompilationModel.Stmt.setStorage $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
          | .scalar (.adt adtName _) =>
              `(Compiler.CompilationModel.Stmt.setStorage
                  $(strTerm f.name)
                  $(← translateAdtConstructForStorage fields constDecls immutableDecls params locals adtName value))
          | .scalar .address | .scalar (.newtype _ .address) =>
              throwErrorAt stx s!"field '{f.name}' is Address-valued; use setStorageAddr"
          | .dynamicArray _ =>
              throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
          | _ =>
              throwErrorAt stx s!"field '{f.name}' is not Uint256; use setStorageAddr"
  | `(term| setStorageAddr $field:ident $value) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar .address | .scalar (.newtype _ .address) =>
          `(Compiler.CompilationModel.Stmt.setStorageAddr $(strTerm f.name) $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .scalar .uint256 | .scalar (.newtype _ .uint256) =>
          throwErrorAt stx s!"field '{f.name}' is Uint256-valued; use setStorage"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | _ =>
          throwErrorAt stx s!"field '{f.name}' is not Address; use setStorage"
  | `(term| setPackedStorage $field:ident $wordOffset:num $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .scalar _ =>
          `(Compiler.CompilationModel.Stmt.setStorageWord
              $(strTerm f.name)
              $(natTerm (← natFromSyntax wordOffset))
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; setPackedStorage requires a scalar root slot"
      | .mappingAddressToUint256 | .mappingUintToUint256 | .mapping2AddressToAddressToUint256
      | .mappingChain _ | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a mapping; setPackedStorage requires a scalar root slot"
  | `(term| setMapping $field:ident $key:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMapping
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMappingUint
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is a double mapping; use setMapping2"
      | .mappingChain _ =>
          throwErrorAt stx s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use setMappingN"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember/setStructMember2"
      | .scalar _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
  | `(term| setMappingAddr $field:ident $key:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMapping
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mappingUintToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is Uint256-keyed; use setMappingUintAddr"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is a double mapping; use setMapping2"
      | .mappingChain _ =>
          throwErrorAt stx s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use setMappingN"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember/setStructMember2"
      | .scalar _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
  | `(term| setMappingUint $field:ident $key:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMappingUint
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mappingAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is Address-keyed; use setMapping"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is a double mapping; use setMapping2"
      | .mappingChain _ =>
          throwErrorAt stx s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use setMappingN"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember/setStructMember2"
      | .scalar _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
  | `(term| setMappingUintAddr $field:ident $key:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMappingUint
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mappingAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is Address-keyed; use setMappingAddr"
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is a double mapping; use setMapping2"
      | .mappingChain _ =>
          throwErrorAt stx s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use setMappingN"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember/setStructMember2"
      | .scalar _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
  | `(term| setMappingWord $field:ident $key:term $wordOffset:num $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mappingAddressToUint256 | .mappingUintToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMappingWord
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
              $wordOffset
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mapping2AddressToAddressToUint256 =>
          throwErrorAt stx s!"field '{f.name}' is a double mapping; use setMapping2Word"
      | .mappingStruct _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember"
      | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a nested struct mapping; use setStructMember2"
      | .dynamicArray _ =>
          throwErrorAt stx s!"field '{f.name}' is a storage dynamic array; use pushStorageArray/popStorageArray/setStorageArrayElement"
      | .scalar _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
      | .mappingChain _ =>
          throwErrorAt stx s!"field '{f.name}' uses {storageTypeMappingDepth? f.ty |>.getD 0} mapping keys; use setMappingN"
  | `(term| setMapping2 $field:ident $key1:term $key2:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .mapping2AddressToAddressToUint256 =>
          `(Compiler.CompilationModel.Stmt.setMapping2
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | .mappingStruct2 _ _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a nested struct mapping; use setStructMember2"
      | .mappingStruct _ _ =>
          throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember"
      | _ => throwErrorAt stx s!"field '{f.name}' is not a double mapping"
  | `(term| setMappingN $field:ident $keys:term $value:term) => do
      let f ← lookupStorageField fields (toString field.getId)
      let keyTerms ← expectMappingKeyTerms keys
      match storageTypeMappingKeyTypes? f.ty with
      | some keyTypes =>
          if keyTerms.size != keyTypes.length then
            throwErrorAt stx s!"field '{f.name}' expects {keyTypes.length} mapping keys, but setMappingN received {keyTerms.size}"
          let keyExprs ← keyTerms.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          `(Compiler.CompilationModel.Stmt.setMappingChain
              $(strTerm f.name)
              [ $[$keyExprs],* ]
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | none =>
          match f.ty with
          | .mappingStruct _ _ | .mappingStruct2 _ _ _ =>
              throwErrorAt stx s!"field '{f.name}' is a struct-valued mapping; use setStructMember/setStructMember2"
          | _ => throwErrorAt stx s!"field '{f.name}' is not a mapping"
  | `(term| pushStorageArray $field:ident $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ =>
          `(Compiler.CompilationModel.Stmt.storageArrayPush
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | _ => throwErrorAt stx s!"field '{f.name}' is not a storage dynamic array"
  | `(term| popStorageArray $field:ident) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ =>
          `(Compiler.CompilationModel.Stmt.storageArrayPop $(strTerm f.name))
      | _ => throwErrorAt stx s!"field '{f.name}' is not a storage dynamic array"
  | `(term| setStorageArrayElement $field:ident $index:term $value:term) =>
      let f ← lookupStorageField fields (toString field.getId)
      match f.ty with
      | .dynamicArray _ =>
          `(Compiler.CompilationModel.Stmt.setStorageArrayElement
              $(strTerm f.name)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals index)
              $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
      | _ => throwErrorAt stx s!"field '{f.name}' is not a storage dynamic array"
  | `(term| require $cond $msg) =>
      `(Compiler.CompilationModel.Stmt.require
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals cond)
          $(strTerm (← expectStringLiteral msg)))
  | `(term| setMemoryArrayElement $name:term $index:term $value:term) => do
      let (arrayName, elemTy) ← requireSupportedMemoryArrayLocal name "setMemoryArrayElement" locals
      unless isSingleWordStaticValueType elemTy do
        throwErrorAt name s!"setMemoryArrayElement currently supports only Array<wordLike> memory locals, got Array {renderValueType elemTy}"
      let indexExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals index
      let valueExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals value
      let dataOffsetExpr : Term ←
        `(Compiler.CompilationModel.Expr.localVar $(strTerm (memoryArrayDataOffsetName arrayName)))
      let elementOffsetExpr : Term ←
        `(Compiler.CompilationModel.Expr.add
            $dataOffsetExpr
            (Compiler.CompilationModel.Expr.mul $indexExpr (Compiler.CompilationModel.Expr.literal 32)))
      `(Compiler.CompilationModel.Stmt.unsafeBlock
          "write memory-backed uint256 array element"
          [Compiler.CompilationModel.Stmt.mstore $elementOffsetExpr $valueExpr])
  | `(term| mstore $offset:term $value:term) =>
      `(Compiler.CompilationModel.Stmt.mstore
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
  | `(term| tstore $offset:term $value:term) =>
      `(Compiler.CompilationModel.Stmt.tstore
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals offset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
  | `(term| calldatacopy $destOffset:term $sourceOffset:term $size:term) =>
      `(Compiler.CompilationModel.Stmt.calldatacopy
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals destOffset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals sourceOffset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals size))
  | `(term| returndataCopy $destOffset:term $sourceOffset:term $size:term) =>
      `(Compiler.CompilationModel.Stmt.returndataCopy
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals destOffset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals sourceOffset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals size))
  | `(term| revertReturndata) =>
      `(Compiler.CompilationModel.Stmt.revertReturndata)
  | `(term| returnValues $values:term) =>
      let valueExprs ← expectExprList fields constDecls immutableDecls params locals values
      `(Compiler.CompilationModel.Stmt.returnValues [ $[$valueExprs],* ])
  | `(term| returnArray $name:term) =>
      `(Compiler.CompilationModel.Stmt.returnArray $(strTerm (← expectStringOrIdent name)))
  | `(term| returnBytes $name:term) =>
      `(Compiler.CompilationModel.Stmt.returnBytes $(strTerm (← expectStringOrIdent name)))
  | `(term| returnStorageWords $name:term) =>
      `(Compiler.CompilationModel.Stmt.returnStorageWords $(strTerm (← expectStringOrIdent name)))
  | `(term| emit $eventName:term $args:term) =>
      let evName := ← expectStringOrIdent eventName
      let argExprs ← expectEmitExprList fields constDecls immutableDecls params locals args
      `(Compiler.CompilationModel.Stmt.emit $(strTerm evName) [ $[$argExprs],* ])
  | `(term| rawLog $topics:term $dataOffset:term $dataSize:term) =>
      let topicExprs ← expectExprList fields constDecls immutableDecls params locals topics
      `(Compiler.CompilationModel.Stmt.rawLog
          [ $[$topicExprs],* ]
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals dataOffset)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals dataSize))
  | `(term| externalCallBind $names:term $fnName:term $args:term) =>
      let resultNames := ← expectStringList names
      let resultNameTerms := resultNames.map strTerm
      let targetFn := ← expectStringOrIdent fnName
      let argExprs ←
        match stripParens args with
        | `(term| [ $[$xs],* ]) =>
            translateLinkedExternalCallArgs fields constDecls immutableDecls params locals xs
        | _ => throwErrorAt args "expected list literal [..]"
      `(Compiler.CompilationModel.Stmt.externalCallBind
          [ $[$resultNameTerms],* ]
          $(strTerm targetFn)
          [ $[$argExprs],* ])
  | `(term| setStructMember $field:term $key:term $member:term $value:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName false
      `(Compiler.CompilationModel.Stmt.setStructMember
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key)
          $(strTerm memberName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
  | `(term| setStructMember2 $field:term $key1:term $key2:term $member:term $value:term) =>
      let fieldName := ← expectStringOrIdent field
      let memberName := ← expectStringOrIdent member
      let _ ← lookupStructMemberDecl fields fieldName memberName true
      `(Compiler.CompilationModel.Stmt.setStructMember2
          $(strTerm fieldName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key1)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals key2)
          $(strTerm memberName)
          $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value))
  | _ =>
      -- void typed interface call in statement position lowers to the
      -- no-return ECM (selector + args call, failure-returndata bubbled, but
      -- no returndatasize check and no return decode).
      match ← resolveTypedInterfaceCall? fields constDecls immutableDecls externalDecls params locals stx with
      | some (_, target, argTerms, none, selector) =>
          let targetExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals target
          let argExprs ← argTerms.mapM
            (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          `(Compiler.CompilationModel.Stmt.ecm
              (Compiler.Modules.Calls.noReturnModule
                $(natTerm selector)
                $(natTerm argExprs.size))
              [ $targetExpr, $[$argExprs],* ])
      | some (_, _, _, some retTy, _) =>
          throwErrorAt stx
            s!"interface call returns {renderValueType retTy}; bind it with `let ... ← ...`"
      | none =>
      match ← resolveLocalFunctionApp? fields constDecls immutableDecls externalDecls functions params locals stx with
      | some (fn, argTerms) =>
          ensureSupportsInternalHelperSpec stx fn
          if fn.returnTy != .unit then
            throwErrorAt stx
              s!"helper call '{fn.name}' returns {renderValueType fn.returnTy}; use `let ... ← {fn.name} ...` or tuple destructuring"
          let argExprs ← translateInternalHelperCallArgs
            fields constDecls immutableDecls params locals fn argTerms
          `(Compiler.CompilationModel.Stmt.internalCall
              $(strTerm (internalHelperSpecNameFor fn))
              [ $[$argExprs],* ])
      | none =>
          throwErrorAt stx "unsupported statement in do block"

mutual
private partial def translateDoElems
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String)
    (elems : Array (TSyntax `doElem)) : CommandElabM (Array Term × Array TypedLocal × Array String) := do
  let mut branchLocals := locals
  let mut branchMutableLocals := mutableLocals
  let mut stmts : Array Term := #[]
  for elem in elems do
    let (newStmts, newLocals, newMutableLocals) ←
      translateDoElem fields constDecls immutableDecls externalDecls functions params branchLocals branchMutableLocals elem
    stmts := stmts ++ newStmts
    branchLocals := newLocals
    branchMutableLocals := newMutableLocals
  pure (stmts, branchLocals, branchMutableLocals)

private partial def translateDoSeqToStmtTerms
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String)
    (doSeq : DoSeq) : CommandElabM (Array Term) := do
  match doSeq with
  | `(doSeq| $[$elems:doElem]*) =>
      pure (← (translateDoElems fields constDecls immutableDecls externalDecls functions params locals mutableLocals elems)).1
  | _ => throwErrorAt doSeq "unsupported branch body; expected do-sequence"

private partial def translateDoElem
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (mutableLocals : Array String)
    (elem : TSyntax `doElem) : CommandElabM (Array Term × Array TypedLocal × Array String) := do
  let localNames := typedLocalNames locals
  let tupleCase? ← do
    let stx := elem.raw
    if stx.getKind == `Lean.Parser.Term.doLet then
      let decl := stx[2]
      let patDecl := decl[0]
      match tupleBinderNames? patDecl[0] with
      | some names =>
          ensureFreshLocalNames localNames names stx
          let rhs : Term := ⟨patDecl[4]⟩
          let rhs := stripParens rhs
          match rhs with
          | `(term| $id:ident) =>
              match (← tupleParamElemExprs? params (toString id.getId)) with
              | some valueExprs =>
                  if names.size != valueExprs.size then
                    throwErrorAt patDecl s!"tuple destructuring binds {names.size} names, but the source provides {valueExprs.size} values"
                  let boundPairs := (names.zip valueExprs).filterMap fun (name?, valueExpr) =>
                    name?.map (fun name => (name, valueExpr))
                  let stmts ← boundPairs.mapM fun (name, valueExpr) =>
                    `(Compiler.CompilationModel.Stmt.letVar $(strTerm name) $valueExpr)
                  let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                  match valueTys with
                  | some tys =>
                      let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                      pure (some (stmts, locals ++ typedPairs, mutableLocals))
                  | none => throwErrorAt rhs "unable to infer tuple local types"
              | none =>
                  match (← tupleInternalCallAssignStmt? fields constDecls immutableDecls externalDecls functions params locals rhs names) with
                  | some stmt =>
                      let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                      match valueTys with
                      | some tys =>
                          let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                          pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                      | none =>
                          match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
                          | some (qualifiedName, _) =>
                              let typedPairs ← unsafe qualifiedTupleBindTypedLocals patDecl qualifiedName names
                              pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                          | none => throwErrorAt rhs "unable to infer tuple local types"
                  | none =>
                      match (← tryExternalCallBindStmt? fields constDecls immutableDecls externalDecls params locals rhs names) with
                      | some (stmt, typedPairs) =>
                          pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                      | none => throwErrorAt rhs "tuple destructuring currently requires a tuple literal, tuple-typed parameter, structMembers/structMembers2 source, internal helper call, or tryExternalCall"
          | _ =>
              match (← arrayElementTupleDestructureStmts? fields constDecls immutableDecls params locals mutableLocals rhs names) with
              | some (stmts, syntheticLocal) =>
                  let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                  match valueTys with
                  | some tys =>
                      let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                      pure (some (stmts, locals.push syntheticLocal ++ typedPairs, mutableLocals))
                  | none => throwErrorAt rhs "unable to infer tuple local types"
              | none =>
                  match (← tupleLiteralOrStructValueExprs? fields constDecls immutableDecls params locals rhs) with
                  | some valueExprs =>
                      if names.size != valueExprs.size then
                        throwErrorAt patDecl s!"tuple destructuring binds {names.size} names, but the source provides {valueExprs.size} values"
                      let boundPairs := (names.zip valueExprs).filterMap fun (name?, valueExpr) =>
                        name?.map (fun name => (name, valueExpr))
                      let stmts ← boundPairs.mapM fun (name, valueExpr) =>
                        `(Compiler.CompilationModel.Stmt.letVar $(strTerm name) $valueExpr)
                      let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                      match valueTys with
                      | some tys =>
                          let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                          pure (some (stmts, locals ++ typedPairs, mutableLocals))
                      | none => throwErrorAt rhs "unable to infer tuple local types"
                  | none =>
                      match (← tupleInternalCallAssignStmt? fields constDecls immutableDecls externalDecls functions params locals rhs names) with
                      | some stmt =>
                          let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                          match valueTys with
                          | some tys =>
                              let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                              pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                          | none =>
                              match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
                              | some (qualifiedName, _) =>
                                  let typedPairs ← unsafe qualifiedTupleBindTypedLocals patDecl qualifiedName names
                                  pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                              | none => throwErrorAt rhs "unable to infer tuple local types"
                      | none =>
                          match (← tryExternalCallBindStmt? fields constDecls immutableDecls externalDecls params locals rhs names) with
                          | some (stmt, typedPairs) =>
                              pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                          | none =>
                              let valueExprs ← tupleValueExprs fields constDecls immutableDecls params locals rhs
                              if names.size != valueExprs.size then
                                throwErrorAt patDecl s!"tuple destructuring binds {names.size} names, but the source provides {valueExprs.size} values"
                              let boundPairs := (names.zip valueExprs).filterMap fun (name?, valueExpr) =>
                                name?.map (fun name => (name, valueExpr))
                              let stmts ← boundPairs.mapM fun (name, valueExpr) =>
                                `(Compiler.CompilationModel.Stmt.letVar $(strTerm name) $valueExpr)
                              let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
                              match valueTys with
                              | some tys =>
                                  let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                                  pure (some (stmts, locals ++ typedPairs, mutableLocals))
                              | none => throwErrorAt rhs "unable to infer tuple local types"
      | none => pure none
    else if stx.getKind == `Lean.Parser.Term.doLetArrow then
      let patDecl := stx[2]
      match tupleBinderNames? patDecl[0] with
      | some names =>
          ensureFreshLocalNames localNames names stx
          let rhs : Term := ⟨patDecl[2][0]⟩
          match (← tupleInternalCallAssignStmt? fields constDecls immutableDecls externalDecls functions params locals rhs names) with
          | some stmt =>
              let valueTys ← inferTupleSourceTypes? fields constDecls immutableDecls externalDecls functions params locals rhs
              match valueTys with
              | some tys =>
                  let typedPairs := (names.zip tys).filterMap fun (name?, ty) => name?.map (fun name => mkTypedLocal name ty)
                  pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
              | none =>
                  match ← resolveQualifiedFunctionApp? fields constDecls immutableDecls externalDecls params locals rhs with
                  | some (qualifiedName, _) =>
                      let typedPairs ← unsafe qualifiedTupleBindTypedLocals patDecl qualifiedName names
                      pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
                  | none => throwErrorAt rhs "unable to infer tuple local types"
          | none =>
              match (← tryExternalCallBindStmt? fields constDecls immutableDecls externalDecls params locals rhs names) with
              | some (stmt, typedPairs) =>
                  pure (some (#[(stmt)], locals ++ typedPairs, mutableLocals))
              | none => throwErrorAt rhs "tuple bind sources must be internal helper calls or tryExternalCall"
      | none => pure none
    else
      pure none
  match tupleCase? with
  | some result => pure result
  | none => match elem with
      | `(doElem| let mut $name:ident := $rhs:term) =>
          let varName := toString name.getId
          if localNames.contains varName then
            throwErrorAt name s!"duplicate local variable '{varName}'"
          let rhsExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals rhs
          let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals rhs
          let interfaceName? ← interfaceNameOfTerm? params locals rhs
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $rhsExpr))],
              locals.push (mkTypedLocal varName ty interfaceName?),
              mutableLocals.push varName)
      | `(doElem| let $name:ident := $rhs:term) =>
          let varName := toString name.getId
          if localNames.contains varName then
            throwErrorAt name s!"duplicate local variable '{varName}'"
          match arrayElementAliasSource? params rhs with
          | some (paramName, index, elemTy) =>
              requireWordLikeType index "arrayElement alias index"
                (← inferPureExprType fields constDecls immutableDecls externalDecls params locals index)
              pure
                (#[],
                  locals.push
                    { name := varName
                      ty := elemTy
                      source := .arrayElement paramName index elemTy },
                  mutableLocals)
          | none =>
              let rhsExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals rhs
              let ty ← inferPureExprType fields constDecls immutableDecls externalDecls params locals rhs
              let interfaceName? ← interfaceNameOfTerm? params locals rhs
              pure
                (#[(← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $rhsExpr))],
                  locals.push (mkTypedLocal varName ty interfaceName?),
                  mutableLocals)
      | `(doElem| let $name:ident ← $rhs:term) =>
          let varName := toString name.getId
          if localNames.contains varName then
            throwErrorAt name s!"duplicate local variable '{varName}'"
          match stripParens rhs with
          | `(term| ecmCall $moduleFactory:term $args:term) =>
              let argExprs ← expectEcmExprList fields constDecls immutableDecls params locals args
              let moduleTerm ← `(term| (($moduleFactory) $(strTerm varName)))
              validateSingleResultEcmModuleTerm moduleTerm varName
              pure
                (#[(← `(Compiler.CompilationModel.Stmt.ecm
                        $moduleTerm
                        [ $[$argExprs],* ]))],
                  locals.push (mkTypedLocal varName .uint256),
                  mutableLocals)
          | `(term| ecrecover $hash:term $v:term $r:term $s:term) =>
              let hashExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals hash
              let vExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals v
              let rExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals r
              let sExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals s
              pure
                (#[(← `(Compiler.CompilationModel.Stmt.ecm
                        (Compiler.Modules.Precompiles.ecrecoverModule $(strTerm varName))
                        [$hashExpr, $vExpr, $rExpr, $sExpr]))],
                  locals.push (mkTypedLocal varName .address),
                  mutableLocals)
          | `(term| tryExternalCall $extName:term $args:term) =>
              -- Zero-return tryExternalCall: `let success ← tryExternalCall "fn" [args]`
              -- produces Stmt.tryExternalCallBind successVar [] externalName args
              let targetFn := ← expectStringOrIdent extName
              let argExprs ← expectExprList fields constDecls immutableDecls params locals args
              pure
                (#[(← `(Compiler.CompilationModel.Stmt.tryExternalCallBind
                        $(strTerm varName)
                        []
                        $(strTerm targetFn)
                        [ $[$argExprs],* ]))],
                  locals.push (mkTypedLocal varName .bool),
                  mutableLocals)
          | `(term| allocArray $len:term) =>
              requireWordLikeType len "allocArray length"
                (← inferPureExprType fields constDecls immutableDecls externalDecls params locals len)
              let lenExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals len
              let lengthName := memoryArrayLengthName varName
              let dataOffsetName := memoryArrayDataOffsetName varName
              let lengthLocal : Term ← `(Compiler.CompilationModel.Expr.localVar $(strTerm lengthName))
              let dataOffsetLocal : Term ← `(Compiler.CompilationModel.Expr.localVar $(strTerm dataOffsetName))
              let dataOffsetExpr : Term ←
                `(Compiler.CompilationModel.Expr.add
                    (Compiler.CompilationModel.Expr.mload (Compiler.CompilationModel.Expr.literal 64))
                    (Compiler.CompilationModel.Expr.literal 32))
              let arrayHeadExpr : Term ←
                `(Compiler.CompilationModel.Expr.sub $dataOffsetLocal (Compiler.CompilationModel.Expr.literal 32))
              let freePtrExpr : Term ←
                `(Compiler.CompilationModel.Expr.add
                    $dataOffsetLocal
                    (Compiler.CompilationModel.Expr.mul $lengthLocal (Compiler.CompilationModel.Expr.literal 32)))
              pure
                (#[
                    (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm lengthName) $lenExpr)),
                    (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm dataOffsetName) $dataOffsetExpr)),
                    (← `(Compiler.CompilationModel.Stmt.unsafeBlock
                          "allocate memory-backed uint256 array"
                          [Compiler.CompilationModel.Stmt.mstore $arrayHeadExpr $lengthLocal,
                           Compiler.CompilationModel.Stmt.mstore
                            (Compiler.CompilationModel.Expr.literal 64)
                            $freePtrExpr]))
                  ],
                  locals.push
                    { name := varName
                      ty := .array .uint256
                      source := .memoryArray },
                  mutableLocals)
          | _ =>
              match ← localInternalArrayReturnBind? fields constDecls immutableDecls externalDecls functions params locals rhs with
              | some (fn, argTerms, elemTy) =>
                  let argExprs ← translateInternalHelperCallArgs
                    fields constDecls immutableDecls params locals fn argTerms
                  let resultNameTerms := #[
                    strTerm (memoryArrayDataOffsetName varName),
                    strTerm (memoryArrayLengthName varName)
                  ]
                  pure
                    (#[(← `(Compiler.CompilationModel.Stmt.internalCallAssign
                            [ $[$resultNameTerms],* ]
                            $(strTerm (internalHelperSpecNameFor fn))
                            [ $[$argExprs],* ]))],
                      locals.push
                        { name := varName
                          ty := .array elemTy
                          source := .memoryArray },
                      mutableLocals)
              | none =>
                  let safeBind? ← translateSafeRequireBind fields constDecls immutableDecls params locals varName rhs
                  match safeBind? with
                  | some safeStmts => pure (safeStmts, locals.push (mkTypedLocal varName .uint256), mutableLocals)
                  | none =>
                      match (← translateERC20BindStmt? fields constDecls immutableDecls functions params locals varName rhs) with
                      | some stmt =>
                          pure (#[(stmt)], locals.push (mkTypedLocal varName .uint256), mutableLocals)
                      | none =>
                          match ← resolveTypedInterfaceCall? fields constDecls immutableDecls externalDecls params locals rhs with
                          | some (ext, target, argTerms, some retTy, selector) =>
                              let targetExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals target
                              let argExprs ← argTerms.mapM
                                (translatePureExprWithTypes fields constDecls immutableDecls params locals)
                              let isStaticTerm ← if ext.isView then `(true) else `(false)
                              pure
                                (#[(← `(Compiler.CompilationModel.Stmt.ecm
                                        (Compiler.Modules.Calls.withReturnModule
                                          $(strTerm varName)
                                          $(natTerm selector)
                                          $(natTerm argExprs.size)
                                          $isStaticTerm)
                                        [ $targetExpr, $[$argExprs],* ]))],
                                  locals.push (mkTypedLocal varName retTy),
                                  mutableLocals)
                          | some (_, _, _, none, _) =>
                              -- void interface method bound with `let ... ←`: not allowed
                              throwErrorAt rhs s!"interface call '{varName}' binds a void method; call it as a statement, not `let ... ←`"
                          | none =>
                              let rhsExpr ← translateBindSource fields constDecls immutableDecls externalDecls functions params locals rhs
                              let ty ← inferBindSourceType fields constDecls immutableDecls externalDecls functions params locals rhs
                              pure
                                (#[(← `(Compiler.CompilationModel.Stmt.letVar $(strTerm varName) $rhsExpr))],
                                  locals.push (mkTypedLocal varName ty),
                                  mutableLocals)
      | `(doElem| $name:ident := $rhs:term) =>
          let varName := toString name.getId
          if !localNames.contains varName then
            throwErrorAt name s!"cannot assign unknown variable '{varName}'"
          if !mutableLocals.contains varName then
            throwErrorAt name s!"cannot assign immutable variable '{varName}'; declare it with 'let mut'"
          let rhsExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals rhs
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.assignVar $(strTerm varName) $rhsExpr))],
              locals,
              mutableLocals)
      | `(doElem| return $value:term) =>
          match (← arrayElementTupleReturnStmts? fields constDecls immutableDecls params locals mutableLocals value) with
          | some (stmts, syntheticLocal) =>
              pure (stmts, locals.push syntheticLocal, mutableLocals)
          | none =>
              match (← tupleReturnValueExprs? fields constDecls immutableDecls params locals value) with
              | some valueExprs =>
                  pure (#[(← `(Compiler.CompilationModel.Stmt.returnValues [ $[$valueExprs],* ]))], locals, mutableLocals)
              | none =>
                  pure (#[(← `(Compiler.CompilationModel.Stmt.return $(← translatePureExprWithTypes fields constDecls immutableDecls params locals value)))], locals, mutableLocals)
      | `(doElem| pure ()) =>
          pure (#[], locals, mutableLocals)
      | `(doElem| if $cond:term then $thenBranch:doSeq else $elseBranch:doSeq) =>
          let condExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals cond
          let thenStmts ← translateDoSeqToStmtTerms fields constDecls immutableDecls externalDecls functions params locals mutableLocals thenBranch
          let elseStmts ← translateDoSeqToStmtTerms fields constDecls immutableDecls externalDecls functions params locals mutableLocals elseBranch
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.ite
              $condExpr
              [ $[$thenStmts],* ]
              [ $[$elseStmts],* ]))],
              locals,
              mutableLocals)
      | `(doElem| tryCatch $attempt:term $handler:term) => do
          let trySuccessName :=
            freshSyntheticLocalName "verity_try_success" params locals mutableLocals
          let (payloadName?, catchElems) ← parseTryCatchHandler handler
          validateTryCatchHandlerDoesNotUsePayload handler payloadName? catchElems
          let attemptExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals attempt
          let catchTranslation ←
            translateDoElems fields constDecls immutableDecls externalDecls functions params locals mutableLocals catchElems
          let catchStmts := catchTranslation.1
          pure
            (#[
              (← `(Compiler.CompilationModel.Stmt.letVar $(strTerm trySuccessName) $attemptExpr)),
              (← `(Compiler.CompilationModel.Stmt.ite
                    (Compiler.CompilationModel.Expr.eq
                      (Compiler.CompilationModel.Expr.localVar $(strTerm trySuccessName))
                      (Compiler.CompilationModel.Expr.literal 0))
                    [ $[$catchStmts],* ]
                    []))
            ],
            locals,
            mutableLocals)
      | `(doElem| forEach $name:term $count:term $body:term) =>
          let loopVar := ← expectStringOrIdent name
          let countExpr ← translatePureExprWithTypes fields constDecls immutableDecls params locals count
          let bodyStmts ←
            match stripParens body with
            | `(term| do $[$inner:doElem]*) =>
                pure (← (translateDoElems fields constDecls immutableDecls externalDecls functions params (locals.push (mkTypedLocal loopVar .uint256)) mutableLocals inner)).1
            | _ => throwErrorAt body "forEach body must be a do block"
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.forEach
                $(strTerm loopVar)
                $countExpr
                [ $[$bodyStmts],* ]))],
              locals,
              mutableLocals)
      | `(doElem| requireError $cond:term $errorName:ident($args,*)) =>
          let argExprs ← args.getElems.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.requireError
                    $(← translatePureExprWithTypes fields constDecls immutableDecls params locals cond)
                    $(strTerm (toString errorName.getId))
                    [ $[$argExprs],* ]))],
              locals,
              mutableLocals)
      | `(doElem| revert $errorName:ident($args,*)) =>
          let argExprs ← args.getElems.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.revertError
                    $(strTerm (toString errorName.getId))
                    [ $[$argExprs],* ]))],
              locals,
              mutableLocals)
      | `(doElem| revertError $errorName:ident($args,*)) =>
          let argExprs ← args.getElems.mapM (translatePureExprWithTypes fields constDecls immutableDecls params locals)
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.revertError
                    $(strTerm (toString errorName.getId))
                    [ $[$argExprs],* ]))],
              locals,
              mutableLocals)
      | `(doElem| unsafe $reason:str do $body:doSeq) =>
          let bodyStmts ← translateDoSeqToStmtTerms fields constDecls immutableDecls externalDecls functions params locals mutableLocals body
          let reasonStr := reason.getString
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.unsafeBlock
                    $(Lean.Quote.quote reasonStr)
                    [ $[$bodyStmts],* ]))],
              locals,
              mutableLocals)
      | `(doElem| ecmBind $names:term $module:term $args:term) =>
          let resultVars ← expectStringList names
          ensureFreshLocalNames localNames (resultVars.map some) names
          validateResultEcmModuleTerm module resultVars
          let argExprs ← expectEcmExprList fields constDecls immutableDecls params locals args
          let typedLocals := resultVars.map (fun name => mkTypedLocal name .uint256)
          pure
            (#[(← `(Compiler.CompilationModel.Stmt.ecm
                    $module
                    [ $[$argExprs],* ]))],
              locals ++ typedLocals,
              mutableLocals)
      | `(doElem| emit $eventName:term $values:term) =>
          let evName := ← expectStringOrIdent eventName
          match stripParens values with
          | `(term| [ $[$args],* ]) =>
              let mut stmts : Array Term := #[]
              let mut branchLocals := locals
              let mut emitArgs : Array Term := #[]
              for arg in args do
                match ← localInternalArrayReturnBind? fields constDecls immutableDecls externalDecls functions params branchLocals arg with
                | some (fn, argTerms, elemTy) =>
                    let tempName := freshSyntheticLocalName "emit_array" params branchLocals mutableLocals
                    let argExprs ← translateInternalHelperCallArgs
                      fields constDecls immutableDecls params branchLocals fn argTerms
                    let resultNameTerms := #[
                      strTerm (memoryArrayDataOffsetName tempName),
                      strTerm (memoryArrayLengthName tempName)
                    ]
                    stmts := stmts.push (← `(Compiler.CompilationModel.Stmt.internalCallAssign
                      [ $[$resultNameTerms],* ]
                      $(strTerm (internalHelperSpecNameFor fn))
                      [ $[$argExprs],* ]))
                    branchLocals := branchLocals.push
                      { name := tempName
                        ty := .array elemTy
                        source := .memoryArray }
                    let tempTerm : Term := ⟨(mkIdent (Name.mkSimple tempName)).raw⟩
                    emitArgs := emitArgs.push (← translateEmitArgExpr fields constDecls immutableDecls params branchLocals tempTerm)
                | none =>
                    emitArgs := emitArgs.push (← translateEmitArgExpr fields constDecls immutableDecls params branchLocals arg)
              stmts := stmts.push (← `(Compiler.CompilationModel.Stmt.emit $(strTerm evName) [ $[$emitArgs],* ]))
              pure (stmts, locals, mutableLocals)
          | _ => throwErrorAt values "expected list literal [..]"
      | `(doElem| $stmt:term) =>
          pure (#[(← translateEffectStmt fields constDecls immutableDecls externalDecls functions params locals stmt)], locals, mutableLocals)
      | _ => throwErrorAt elem "unsupported do element"
end

private def translateBodyToStmtTerms
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (fn : FunctionDecl) : CommandElabM (Array Term) := do
  match fn.body with
  | `(term| do $[$elems:doElem]*) =>
      let guardPrelude ← initGuardPreludeStmtTerms fields fn
      let rolePrelude ← roleGuardPreludeStmtTerms fields fn
      let modifierPrelude ← fn.modifiers.mapM fun modIdent =>
        `(Compiler.CompilationModel.Stmt.internalCall $(strTerm (modifierInternalName (toString modIdent.getId))) [])
      let stmts := guardPrelude ++ rolePrelude ++ modifierPrelude ++ (← translateDoElems fields constDecls immutableDecls externalDecls functions fn.params #[] #[] elems).1
      let mut stmts := stmts
      if fn.returnTy == .unit then
        stmts := stmts.push (← `(Compiler.CompilationModel.Stmt.stop))
      pure stmts
  | _ => throwErrorAt fn.body "function body must be a do block"

private def translateConstructorBodyToStmtTerms
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (ctor : ConstructorDecl) : CommandElabM (Array Term) := do
  match ctor.body with
  | `(term| do $[$elems:doElem]*) =>
      pure (← (translateDoElems fields constDecls immutableDecls externalDecls functions ctor.params #[] #[] elems)).1
  | _ => throwErrorAt ctor.body "constructor body must be a do block"

private def immutableInitStmtTerms
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (ctorParams : Array ParamDecl) : CommandElabM (Array Term) := do
  immutableDecls.zipIdx.mapM fun (imm, idx) => do
    let slotField := immutableStorageFieldDecl fields imm idx
    let valueExpr ← translatePureExpr fields constDecls #[] ctorParams #[] imm.body
    match imm.ty with
    | .uint256 | .int256 | .uint8 | .uint16 | .bytes32 | .bool =>
        `(Compiler.CompilationModel.Stmt.setStorage $(strTerm slotField.name) $valueExpr)
    | .address =>
        `(Compiler.CompilationModel.Stmt.setStorageAddr $(strTerm slotField.name) $valueExpr)
    | _ =>
        throwErrorAt imm.ident s!"immutable '{imm.name}' uses unsupported type"

def mkSuffixedIdent (base : Ident) (suffix : String) : CommandElabM Ident :=
  let rec appendSuffix : Name → Name
    | .anonymous => .str .anonymous suffix
    | .str p s => .str p (s ++ suffix)
    | .num p n => .str p (toString n ++ suffix)
  pure <| mkIdent <| appendSuffix base.getId

private def mkContractFnType (params : Array ParamDecl) (retTy : ValueType) : CommandElabM Term := do
  let mut ty ← `(Verity.Contract $(← contractValueTypeTerm retTy))
  for param in params.reverse do
    ty ← `(($(← contractValueTypeTerm param.ty)) → $ty)
  pure ty

private def mkTupleProjectionTerm (base : Term) (elemTys : List ValueType) (idx : Nat) : CommandElabM Term := do
  let rec go (tupleTerm : Term) (remaining : List ValueType) (targetIdx : Nat) : CommandElabM Term := do
    match remaining with
    | [] => throwError "tuple projection index out of bounds"
    | [_] =>
        if targetIdx == 0 then
          pure tupleTerm
        else
          throwError "tuple projection index out of bounds"
    | _ :: rest =>
        if targetIdx == 0 then
          `(Prod.fst $tupleTerm)
        else
          go (← `(Prod.snd $tupleTerm)) rest (targetIdx - 1)
  go base elemTys idx

private def injectTupleParamAliases (params : Array ParamDecl) (body : Term) : CommandElabM Term := do
  let mut wrappedBody := body
  for param in params.reverse do
    match param.ty with
    | .tuple elemTys =>
        let baseTerm : Term := mkIdent param.ident.getId
        for (_elemTy, idx) in (elemTys.toArray.zipIdx).reverse do
          let aliasName := mkIdent <| Name.str .anonymous s!"{param.name}_{idx}"
          let projection ← mkTupleProjectionTerm baseTerm elemTys idx
          wrappedBody ← `(term| let $aliasName := $projection; $wrappedBody)
    | _ => pure ()
  pure wrappedBody

private def typedInterfaceCallReturnType?
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM (Option ValueType) := do
  let some (target, methodName, _argTerms) := typedDotCallSyntax? stx
    | pure none
  let targetName ←
    match stripParens target with
    | `(term| $targetIdent:ident) => pure (toString targetIdent.getId)
    | _ => pure ""
  let some interfaceName := lookupInterfaceName? params locals targetName
    | pure none
  let externalName := interfaceExternalName interfaceName methodName
  let some ext := externalDecls.find? (fun ext => ext.name == externalName)
    | pure none
  match ext.returnTys.toList with
  | [retTy] => pure (some retTy)
  | _ => pure none

/-- True when `stx` is a typed interface call whose method is void (no returns
    clause). Used to drop statement-position void calls from the executable
    wrapper, where they have nothing to bind. -/
private def isVoidTypedInterfaceCall?
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (stx : Term) : CommandElabM Bool := do
  let some (target, methodName, _argTerms) := typedDotCallSyntax? stx
    | pure false
  let targetName ←
    match stripParens target with
    | `(term| $targetIdent:ident) => pure (toString targetIdent.getId)
    | _ => pure ""
  let some interfaceName := lookupInterfaceName? params locals targetName
    | pure false
  let externalName := interfaceExternalName interfaceName methodName
  let some ext := externalDecls.find? (fun ext => ext.name == externalName)
    | pure false
  pure ext.returnTys.isEmpty

mutual
private partial def rewriteForEachExecutableDoSeq
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (doSeq : DoSeq) : CommandElabM DoSeq := do
  match doSeq with
  | `(doSeq| $[$elems:doElem]*) =>
      let (elems, _) ← rewriteForEachExecutableDoElems externalDecls params locals elems
      `(doSeq| $[$elems:doElem]*)
  | _ => throwErrorAt doSeq "unsupported branch body; expected do-sequence"

private partial def rewriteForEachExecutableDoElems
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (elems : Array (TSyntax `doElem)) : CommandElabM (Array (TSyntax `doElem) × Array TypedLocal) := do
  let mut rewritten : Array (TSyntax `doElem) := #[]
  let mut currentLocals := locals
  for elem in elems do
    let (newElems, newLocals) ← rewriteForEachExecutableDoElem externalDecls params currentLocals elem
    rewritten := rewritten ++ newElems
    currentLocals := newLocals
  pure (rewritten, currentLocals)

private partial def rewriteForEachExecutableDoElem
    (externalDecls : Array ExternalDecl)
    (params : Array ParamDecl)
    (locals : Array TypedLocal)
    (elem : TSyntax `doElem) : CommandElabM (Array (TSyntax `doElem) × Array TypedLocal) := do
  match elem with
  | `(doElem| let $name:ident := $rhs:term) =>
      let varName := toString name.getId
      let interfaceName? ← interfaceNameOfTerm? params locals rhs
      let locals :=
        match interfaceName? with
        | some iface => locals.push (mkTypedLocal varName .address (some iface))
        | none => locals
      pure (#[elem], locals)
  | `(doElem| let $name:ident ← $rhs:term) =>
      match ← typedInterfaceCallReturnType? externalDecls params locals rhs with
      | some retTy =>
          let retTyTerm ← contractValueTypeTerm retTy
          pure (#[← `(doElem| let $name:ident := (panic! "typed interface calls are compiler-only in executable wrappers" : $retTyTerm))],
            locals.push (mkTypedLocal (toString name.getId) retTy))
      | none => pure (#[elem], locals)
  | `(doElem| let $pat:term ← $rhs:term) =>
      match tupleBinderNames? pat with
      | some _ =>
          match stripParens rhs with
          | `(term| tryExternalCall $name:term $_args:term) =>
              let extName := ← expectStringOrIdent name
              match externalDecls.find? (fun ext => ext.name == extName) with
              | some ext =>
                  match ext.returnTys.toList with
                  | [retTy] =>
                      let retTyTerm ← contractValueTypeTerm retTy
                      pure (#[← `(doElem| let $pat:term ← ($rhs : _root_.Verity.Contract (Bool × $retTyTerm)))], locals)
                  | _ => pure (#[elem], locals)
              | none => pure (#[elem], locals)
          | _ => pure (#[elem], locals)
      | none => pure (#[elem], locals)
  | `(doElem| forEach $name:term $_count:term $body:term) =>
      let loopIdent := mkIdent (Name.mkSimple (← expectStringOrIdent name))
      match stripParens body with
      | `(term| do $[$inner:doElem]*) =>
          let (inner, _) ← rewriteForEachExecutableDoElems externalDecls params locals inner
          pure (#[← `(doElem| let $loopIdent : Uint256 := 0)] ++ inner, locals)
      | _ => throwErrorAt body "forEach body must be a do block"
  | `(doElem| if $cond:term then $thenBranch:doSeq else $elseBranch:doSeq) =>
      let thenBranch ← rewriteForEachExecutableDoSeq externalDecls params locals thenBranch
      let elseBranch ← rewriteForEachExecutableDoSeq externalDecls params locals elseBranch
      pure (#[← `(doElem| if $cond then $thenBranch else $elseBranch)], locals)
  | `(doElem| tryCatch $attempt:term $handler:term) =>
      let tryCatchFn := Lean.mkIdentFrom attempt `_root_.Contracts.tryCatchWord
      match stripParens handler with
      | `(term| fun $name:ident => do $[$catchElems:doElem]*) =>
          let (catchElems, _) ← rewriteForEachExecutableDoElems externalDecls params locals catchElems
          pure (#[← `(doElem| $tryCatchFn:ident $attempt (fun $name => do $[$catchElems:doElem]*))], locals)
      | `(term| do $[$catchElems:doElem]*) =>
          let (catchElems, _) ← rewriteForEachExecutableDoElems externalDecls params locals catchElems
          pure (#[← `(doElem| $tryCatchFn:ident $attempt (fun _ => do $[$catchElems:doElem]*))], locals)
      | _ =>
          throwErrorAt handler
            "tryCatch handler must be `fun _ => do ...` or a direct `do ...` block"
  | `(doElem| unsafe $_reason:str do $body:doSeq) =>
      let body ← rewriteForEachExecutableDoSeq externalDecls params locals body
      pure (#[← `(doElem| do $body)], locals)
  | `(doElem| $stmt:term) =>
      -- a void typed interface call in statement position is compiler-only;
      -- drop it from the executable wrapper (it returns nothing to bind).
      if (← isVoidTypedInterfaceCall? externalDecls params locals stmt) then
        pure (#[← `(doElem| pure ())], locals)
      else
        pure (#[elem], locals)
  | other =>
      pure (#[other], locals)
end

private def rewriteForEachExecutableBody (externalDecls : Array ExternalDecl) (params : Array ParamDecl) (body : Term) : CommandElabM Term := do
  match body with
  | `(term| do $[$elems:doElem]*) =>
      let (elems, _) ← rewriteForEachExecutableDoElems externalDecls params #[] elems
      `(do $[$elems:doElem]*)
  | _ => pure body

private def mkContractFnValue (params : Array ParamDecl) (body : Term) : CommandElabM Term := do
  let mut value ← injectTupleParamAliases params body
  for param in params.reverse do
    let pid := param.ident
    value ← `(fun ($pid : $(← contractValueTypeTerm param.ty)) => $value)
  pure value

private def mkModelParamsTerm (params : Array ParamDecl) : CommandElabM Term := do
  let xs ← params.mapM fun p => do
    `(Compiler.CompilationModel.Param.mk $(strTerm p.name) $(← modelParamTypeTerm p.ty))
  `([ $[$xs],* ])

private def storageSlotInnerTypeTerm (ty : StorageType) : CommandElabM Term := do
  let rec mkStorageMappingTy (keys : List MappingKeyType) : CommandElabM Term := do
    match keys with
    | [] => `(Uint256)
    | keyTy :: rest =>
        `(($(← storageKeyTypeContractTerm keyTy) → $(← mkStorageMappingTy rest)))
  match ty with
    | .scalar .uint256 => `(Uint256)
    | .scalar .int256 => `(Uint256)
    | .scalar .uint8 => throwError "storage field cannot be Uint8; use Uint256 encoding"
    | .scalar .uint16 => throwError "storage field cannot be Uint16; use Uint256 encoding"
    | .scalar .address => `(Address)
    | .scalar .bytes32 => throwError "storage field cannot be Bytes32; use Uint256 encoding"
    | .scalar .bool => throwError "storage field cannot be Bool; use Uint256 (0/1) encoding"
    | .scalar .string => throwError "storage field cannot be String; use Uint256 encoding"
    | .scalar .bytes => throwError "storage field cannot be Bytes; use Uint256 encoding"
    | .scalar (.array _) => throwError "storage field cannot be Array; use mapping encodings"
    | .scalar (.fixedArray _ _) => throwError "storage field cannot be FixedArray; use mapping encodings"
    | .scalar (.tuple _) => throwError "storage field cannot be Tuple; use mapping encodings"
    | .scalar (.struct _ _) => throwError "storage field cannot be named struct; use mapping encodings"
    | .scalar .unit => throwError "storage field cannot be Unit"
    | .scalar (.newtype _ baseType) =>
        -- Newtypes erased to base type for storage (#1727 Step 3b)
        match baseType with
        | .uint256 => `(Uint256)
        | .address => `(Address)
        | _ => throwError "storage field with newtype base type not supported; use Uint256 or Address"
    | .scalar (.adt _ _) => `(Uint256)  -- ADTs stored as tag value in storage (#1727 Step 5b)
    | .dynamicArray .uint256 => `(List Uint256)
    | .dynamicArray .address => `(List Address)
    | .dynamicArray .bool => `(List Bool)
    | .dynamicArray .uint8 => throwError "storage dynamic arrays currently support only Uint256 elements on the macro path"
    | .dynamicArray .bytes32 => `(List Uint256)
    | .mappingAddressToUint256 => `(Address → Uint256)
    | .mapping2AddressToAddressToUint256 => `(Address → Address → Uint256)
    | .mappingUintToUint256 => `(Uint256 → Uint256)
    | .mappingChain keyTypes => mkStorageMappingTy keyTypes
    | .mappingStruct keyType _ => `(($(← storageKeyTypeContractTerm keyType) → Uint256))
    | .mappingStruct2 outerKey innerKey _ =>
        `(($(← storageKeyTypeContractTerm outerKey) → $(← storageKeyTypeContractTerm innerKey) → Uint256))

private def mkStorageDefCommand (field : StorageFieldDecl) : CommandElabM Cmd := do
  let storageTy ← storageSlotInnerTypeTerm field.ty
  let fid := field.ident
  `(command| def $fid : Verity.StorageSlot $storageTy := ⟨$(natTerm field.slotNum)⟩)

private def storageAccessorTypeName (parts : List String) : Ident :=
  mkIdent (Name.mkSimple (String.intercalate "_" parts ++ "_StorageSlots"))

private partial def storageAccessorTreeName : StorageAccessorTree → String
  | .leaf name .. => name
  | .node name _ => name

private partial def storageAccessorTypeCommands
    (path : List String) : StorageAccessorTree → CommandElabM (Array Cmd)
  | .leaf .. => pure #[]
  | .node _ children => do
      let mut cmds : Array Cmd := #[]
      for child in children do
        cmds := cmds ++ (← storageAccessorTypeCommands (path ++ [storageAccessorTreeName child]) child)
      let typeId := storageAccessorTypeName path
      let mut fieldIds : Array Ident := #[]
      let mut fieldTypes : Array Term := #[]
      for child in children do
        fieldIds := fieldIds.push (mkIdent (Name.mkSimple (storageAccessorTreeName child)))
        match child with
        | .leaf _ ty _ =>
            fieldTypes := fieldTypes.push (← `(Verity.StorageSlot $(← storageSlotInnerTypeTerm ty)))
        | .node name _ =>
            fieldTypes := fieldTypes.push (storageAccessorTypeName (path ++ [name]))
      cmds := cmds.push (← `(command| structure $typeId where
          $[$fieldIds:ident : $fieldTypes:term]*))
      pure cmds

private partial def storageAccessorValueTerm : StorageAccessorTree → CommandElabM Term
  | .leaf _ ty slotNum => do
      `((⟨$(natTerm slotNum)⟩ : Verity.StorageSlot $(← storageSlotInnerTypeTerm ty)))
  | .node _ children => do
      let fieldIds := children.map (fun child => mkIdent (Name.mkSimple (storageAccessorTreeName child)))
      let values ← children.mapM storageAccessorValueTerm
      `({ $[$fieldIds:ident := $values:term],* })

def mkStorageStructAccessorCommandsPublic
    (accessor : StorageStructAccessorDecl) : CommandElabM (Array Cmd) := do
  let typeCmds ← storageAccessorTypeCommands [accessor.name] accessor.tree
  let rootType := storageAccessorTypeName [accessor.name]
  let rootValue ← storageAccessorValueTerm accessor.tree
  let rootCmd ← `(command| def $(accessor.ident) : $rootType := $rootValue)
  pure (typeCmds.push rootCmd)

private def packedOptionTerm (packed : Option (Nat × Nat)) : CommandElabM Term := do
  match packed with
  | none => `(none)
  | some (offset, width) => `(some ($(natTerm offset), $(natTerm width)))

private def mkStructMemberReadBranches
    (fields : Array StorageFieldDecl)
    (nested : Bool)
    (fallbackTerm : Term) : CommandElabM Term := do
  let mut acc := fallbackTerm
  for field in fields.reverse do
    match nested, field.ty with
    | false, .mappingStruct _ members =>
        for member in members.reverse do
          let packedTerm ← packedOptionTerm member.packed
          acc ← `(if field == $(strTerm field.name) && member == $(strTerm member.name) then
              _root_.Contracts.structMemberAt $(natTerm field.slotNum) $(natTerm member.wordOffset)
                $packedTerm key
            else
              $acc)
    | true, .mappingStruct2 _ _ members =>
        for member in members.reverse do
          let packedTerm ← packedOptionTerm member.packed
          acc ← `(if field == $(strTerm field.name) && member == $(strTerm member.name) then
              _root_.Contracts.structMember2At $(natTerm field.slotNum) $(natTerm member.wordOffset)
                $packedTerm key1 key2
            else
              $acc)
    | _, _ => pure ()
  pure acc

private def mkStructMemberWriteBranches
    (fields : Array StorageFieldDecl)
    (nested : Bool)
    (fallbackTerm : Term) : CommandElabM Term := do
  let mut acc := fallbackTerm
  for field in fields.reverse do
    match nested, field.ty with
    | false, .mappingStruct _ members =>
        for member in members.reverse do
          let packedTerm ← packedOptionTerm member.packed
          acc ← `(if field == $(strTerm field.name) && member == $(strTerm member.name) then
              _root_.Contracts.setStructMemberAt $(natTerm field.slotNum) $(natTerm member.wordOffset)
                $packedTerm key value
            else
              $acc)
    | true, .mappingStruct2 _ _ members =>
        for member in members.reverse do
          let packedTerm ← packedOptionTerm member.packed
          acc ← `(if field == $(strTerm field.name) && member == $(strTerm member.name) then
              _root_.Contracts.setStructMember2At $(natTerm field.slotNum) $(natTerm member.wordOffset)
                $packedTerm key1 key2 value
            else
              $acc)
    | _, _ => pure ()
  pure acc

private def hasStructMapping (fields : Array StorageFieldDecl) : Bool :=
  fields.any fun field =>
    match field.ty with
    | .mappingStruct _ _ => true
    | _ => false

private def hasStructMapping2 (fields : Array StorageFieldDecl) : Bool :=
  fields.any fun field =>
    match field.ty with
    | .mappingStruct2 _ _ _ => true
    | _ => false

def mkExecutableStructMappingCommandsPublic (fields : Array StorageFieldDecl) :
    CommandElabM (Array Cmd) := do
  let mut cmds : Array Cmd := #[]
  if hasStructMapping fields then
    let readFallback : Term ← `(pure default)
    let writeFallback : Term ← `(pure ())
    let readBranches ← mkStructMemberReadBranches fields false readFallback
    let writeBranches ← mkStructMemberWriteBranches fields false writeFallback
    cmds := cmds.push (← `(command|
      def structMember {κ α : Type} [Inhabited α] [_root_.Contracts.StorageKey κ]
          [_root_.Contracts.StorageWord α] (field : String) (key : κ) (member : String) :
          Verity.Contract α :=
        $readBranches))
    cmds := cmds.push (← `(command|
      def setStructMember {κ α : Type} [_root_.Contracts.StorageKey κ]
          [_root_.Contracts.StorageWord α] (field : String) (key : κ) (member : String)
          (value : α) : Verity.Contract Unit :=
        $writeBranches))
  if hasStructMapping2 fields then
    let readFallback : Term ← `(pure default)
    let writeFallback : Term ← `(pure ())
    let readBranches ← mkStructMemberReadBranches fields true readFallback
    let writeBranches ← mkStructMemberWriteBranches fields true writeFallback
    cmds := cmds.push (← `(command|
      def structMember2 {κ₁ κ₂ α : Type} [Inhabited α] [_root_.Contracts.StorageKey κ₁]
          [_root_.Contracts.StorageKey κ₂] [_root_.Contracts.StorageWord α] (field : String)
          (key1 : κ₁) (key2 : κ₂) (member : String) : Verity.Contract α :=
        $readBranches))
    cmds := cmds.push (← `(command|
      def setStructMember2 {κ₁ κ₂ α : Type} [_root_.Contracts.StorageKey κ₁]
          [_root_.Contracts.StorageKey κ₂] [_root_.Contracts.StorageWord α] (field : String)
          (key1 : κ₁) (key2 : κ₂) (member : String) (value : α) : Verity.Contract Unit :=
        $writeBranches))
  pure cmds

private def mkModelFieldTerm (field : StorageFieldDecl) : CommandElabM Term := do
  let packedTerm ←
    match field.packedBits with
    | none => `(none)
    | some (offset, width) =>
        `(some { offset := $(natTerm offset), width := $(natTerm width) })
  `(Compiler.CompilationModel.Field.mk
      $(strTerm field.name)
      $(← modelFieldTypeTerm field.ty)
      (some $(natTerm field.slotNum))
      $packedTerm
      [])

private def mkModelErrorTerm (err : ErrorDecl) : CommandElabM Term := do
  let paramTerms ← err.params.mapM modelParamTypeTerm
  `(Compiler.CompilationModel.ErrorDef.mk
      $(strTerm err.name)
      [ $[$paramTerms],* ])

private def mkModelEventParamTerm (param : EventParamDecl) : CommandElabM Term := do
  let tyTerm ← modelParamTypeTerm param.ty
  let kindTerm ←
    if param.isIndexed then
      `(Compiler.CompilationModel.EventParamKind.indexed)
    else
      `(Compiler.CompilationModel.EventParamKind.unindexed)
  `(Compiler.CompilationModel.EventParam.mk
      $(strTerm param.name)
      $tyTerm
      $kindTerm)

private def mkModelEventTerm (ev : EventDecl) : CommandElabM Term := do
  let paramTerms ← ev.params.mapM mkModelEventParamTerm
  `(Compiler.CompilationModel.EventDef.mk
      $(strTerm ev.name)
      [ $[$paramTerms],* ])

private def mkModelExternalTerm (ext : ExternalDecl) : CommandElabM Term := do
  let paramTerms ← ext.params.mapM modelParamTypeTerm
  let returnTerms ← ext.returnTys.mapM modelParamTypeTerm
  let returnTypeTerm ←
    match ext.returnTys.toList with
    | [] => `(none)
    | [retTy] => `(some $(← modelParamTypeTerm retTy))
    | _ => `(none)
  let linkModeTerm ←
    match ext.linkMode with
    | .external => `(Compiler.CompilationModel.ForeignLinkMode.external)
    | .objectLinked => `(Compiler.CompilationModel.ForeignLinkMode.objectLinked)
    | .inline => `(Compiler.CompilationModel.ForeignLinkMode.inline)
    | .compilerRuntime => `(Compiler.CompilationModel.ForeignLinkMode.compilerRuntime)
  `( ({
      name := $(strTerm ext.name)
      params := [ $[$paramTerms],* ]
      returnType := $returnTypeTerm
      «returns» := [ $[$returnTerms],* ]
      proofStatus := Compiler.ProofStatus.assumed
      axiomNames := []
      linkMode := $linkModeTerm
    } : Compiler.CompilationModel.ExternalFunction) )

private def mkModelLocalObligationTerm (obligation : LocalObligationDecl) : CommandElabM Term := do
  let proofStatusTerm ←
    match obligation.proofStatus with
    | .proved => `(Compiler.ProofStatus.proved)
    | .assumed => `(Compiler.ProofStatus.assumed)
    | .unchecked => `(Compiler.ProofStatus.unchecked)
  `(Compiler.CompilationModel.LocalObligation.mk
      $(strTerm obligation.name)
      $(strTerm obligation.obligation)
      $proofStatusTerm)

private def mkAdtVariantTerm (variant : AdtVariantDecl) (tag : Nat) : CommandElabM Term := do
  let fieldTerms ← variant.fields.mapM fun p => do
    let tyTerm ← modelParamTypeTerm p.ty
    `(Compiler.CompilationModel.Param.mk $(strTerm p.name) $tyTerm)
  `(Compiler.CompilationModel.AdtVariant.mk
      $(strTerm variant.name)
      $(natTerm tag)
      [ $[$fieldTerms],* ])

private def mkAdtTypeDefTerm (adtDecl : AdtDecl) : CommandElabM Term := do
  let mut variantTerms : Array Term := #[]
  for (variant, idx) in adtDecl.variants.zipIdx do
    variantTerms := variantTerms.push (← mkAdtVariantTerm variant idx)
  `(Compiler.CompilationModel.AdtTypeDef.mk
      $(strTerm adtDecl.name)
      [ $[$variantTerms],* ])

private partial def collectQualifiedFunctionAppsFromSyntax (interfaceParamNames : Array String) (stx : Syntax) : Array Name :=
  let fromChildren : Array Name :=
    match stx with
    | .node _ _ args =>
        args.foldl (fun acc child => acc ++ collectQualifiedFunctionAppsFromSyntax interfaceParamNames child) #[]
    | _ => #[]
  match stx with
  | .node _ `Lean.Parser.Term.app args =>
      match args.getD 0 Syntax.missing with
      | .ident _ _ raw _ =>
          let components := nameComponents raw
          if isQualifiedFunctionName raw && (nameComponents raw).head? != some "Verity" &&
              !interfaceParamNames.contains (components.headD "") &&
              !startsWithLowercaseAscii (components.headD "") &&
              !raw.toString.endsWith ".mk" then
            fromChildren.push raw
          else
            fromChildren
      | _ => fromChildren
  | _ => fromChildren

private def uniqueNames (names : Array Name) : Array Name :=
  names.foldl
    (fun acc name => if acc.any (· == name) then acc else acc.push name)
    #[]

private def collectQualifiedFunctionAppsFromFunction (fn : FunctionDecl) : Array Name :=
  collectQualifiedFunctionAppsFromSyntax
    (fn.params.filterMap fun p => p.interfaceName?.map (fun _ => p.name))
    fn.body.raw

private def collectQualifiedFunctionAppsFromConstructor (ctor : ConstructorDecl) : Array Name :=
  collectQualifiedFunctionAppsFromSyntax #[] ctor.body.raw

private def mkQualifiedInternalFunctionTerm
    (usedNames : List String)
    (name : Name) : CommandElabM Term := do
  let modelIdent : Ident := mkIdent (qualifiedFunctionModelName name)
  `(({ $modelIdent with
        name := $(strTerm (qualifiedInternalHelperNameFromUsed usedNames name))
        isInternal := true } : Compiler.CompilationModel.FunctionSpec))

private def mkSpecCommand
    (contractName : String)
    (fields : Array StorageFieldDecl)
    (errorDecls : Array ErrorDecl)
    (eventDecls : Array EventDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (ctor : Option ConstructorDecl)
    (modifiers : Array ModifierDecl)
    (functions : Array FunctionDecl)
    (adtDecls : Array AdtDecl)
    (storageNamespace : Option Nat) : CommandElabM Cmd := do
  let immutableFields := immutableDecls.zipIdx.map (fun (imm, idx) => immutableStorageFieldDecl fields imm idx)
  let allFields := fields ++ immutableFields
  let fieldTerms ← allFields.mapM mkModelFieldTerm
  let errorTerms ← errorDecls.mapM mkModelErrorTerm
  let eventTerms ← eventDecls.mapM mkModelEventTerm
  let externalTerms ← externalDecls.mapM mkModelExternalTerm
  let constructorTerm ←
    match ctor, immutableDecls.isEmpty with
    | none, true => `(none)
    | some ctor, _ =>
        let ctorParams ← mkModelParamsTerm ctor.params
        let ctorPayable ← if ctor.isPayable then `(true) else `(false)
        let ctorLocalObligationTerms ← ctor.localObligations.mapM mkModelLocalObligationTerm
        let immutableInitTerms ← immutableInitStmtTerms fields constDecls immutableDecls ctor.params
        let ctorBodyTerms ← translateConstructorBodyToStmtTerms fields constDecls immutableDecls externalDecls functions ctor
        let ctorAllTerms := immutableInitTerms ++ ctorBodyTerms
        `(some {
          params := $ctorParams
          isPayable := $ctorPayable
          localObligations := [ $[$ctorLocalObligationTerms],* ]
          body := [ $[$ctorAllTerms],* ]
        })
    | none, false =>
        let immutableInitTerms ← immutableInitStmtTerms fields constDecls immutableDecls #[]
        `(some {
          params := []
          isPayable := false
          localObligations := []
          body := [ $[$immutableInitTerms],* ]
        })
  let publicFunctions := functions.filter (fun fn => !fn.isInternal)
  let functionModelIds ← publicFunctions.mapM fun fn => mkSuffixedIdent fn.ident "_model"
  let internalFunctionTerms ← functions.filterMapM fun fn => do
    if supportsInternalHelperSpec fn then
      let modelBodyName ← mkSuffixedIdent fn.ident "_modelBody"
      let modelParams ← mkModelParamsTerm fn.params
      let localObligationTerms ← fn.localObligations.mapM mkModelLocalObligationTerm
      let payableTerm ← if fn.isPayable then `(true) else `(false)
      let viewTerm ← if fn.isView then `(true) else `(false)
      let pureTerm ← if fn.isPure then `(true) else `(false)
      let noExternalCallsTerm ← if fn.noExternalCalls then `(true) else `(false)
      let allowPostInteractionWritesTerm ← if fn.allowPostInteractionWrites then `(true) else `(false)
      let nonReentrantLockTerm ← match fn.nonReentrantLock with
        | some lockIdent => `(some $(strTerm (toString lockIdent.getId)))
        | none => `(none)
      let ceiSafeTerm ← if fn.ceiSafe then `(true) else `(false)
      let requiresRoleTerm ← match fn.requiresRole with
        | some roleIdent => `(some $(strTerm (toString roleIdent.getId)))
        | none => `(none)
      let internalModifiesTerms : Array Term := fn.modifies.map fun ident => strTerm (toString ident.getId)
      let returnTypeTerm ← modelReturnTypeTerm fn.returnTy
      let returnsTerm ← modelReturnsTerm fn.returnTy
      pure <| some (← `( ({
        name := $(strTerm (internalHelperSpecNameFor fn))
        params := $modelParams
        returnType := $returnTypeTerm
        «returns» := $returnsTerm
        isPayable := $payableTerm
        isView := $viewTerm
        isPure := $pureTerm
        noExternalCalls := $noExternalCallsTerm
        allowPostInteractionWrites := $allowPostInteractionWritesTerm
        nonReentrantLock := $nonReentrantLockTerm
        ceiSafe := $ceiSafeTerm
        requiresRole := $requiresRoleTerm
        modifies := [ $[$internalModifiesTerms],* ]
        localObligations := [ $[$localObligationTerms],* ]
        body := $modelBodyName
        isInternal := true
      } : Compiler.CompilationModel.FunctionSpec) ))
    else
      pure none
  let modifierFunctionTerms ← modifiers.mapM fun modDecl => do
    let bodyTerms ←
      match modDecl.body with
      | `(term| do $[$elems:doElem]*) =>
          pure (← (translateDoElems fields constDecls immutableDecls externalDecls functions #[] #[] #[] elems)).1
      | _ => throwErrorAt modDecl.body "modifier body must be a do block"
    let bodyTerms := bodyTerms.push (← `(Compiler.CompilationModel.Stmt.stop))
    `( ({
        name := $(strTerm (modifierInternalName modDecl.name))
        params := []
        returnType := none
        «returns» := []
        isPayable := false
        isView := false
        isPure := false
        noExternalCalls := false
        allowPostInteractionWrites := false
        nonReentrantLock := none
        ceiSafe := false
        requiresRole := none
        modifies := []
        localObligations := []
        body := [ $[$bodyTerms],* ]
        isInternal := true
      } : Compiler.CompilationModel.FunctionSpec) )
  let qualifiedFunctionNames :=
    uniqueNames <|
      (functions.foldl (fun acc fn => acc ++ collectQualifiedFunctionAppsFromFunction fn) #[]) ++
      (match ctor with
      | some ctorDecl => collectQualifiedFunctionAppsFromConstructor ctorDecl
      | none => #[])
  let localInternalFunctionNames := (functions.map internalHelperSpecNameFor).toList
  let qualifiedInternalFunctionTerms ←
    qualifiedFunctionNames.mapM (mkQualifiedInternalFunctionTerm localInternalFunctionNames)
  let adtTypeTerms ← adtDecls.mapM mkAdtTypeDefTerm
  let functionModelTerms : Array Term := functionModelIds.map fun id => ⟨id.raw⟩
  let allFunctionTerms := functionModelTerms ++ internalFunctionTerms ++ modifierFunctionTerms ++ qualifiedInternalFunctionTerms
  let namespaceTerm ← match storageNamespace with
    | some ns => `(some $(natTerm ns))
    | none => `(none)
  `(command| def spec : Compiler.CompilationModel.CompilationModel := {
    name := $(strTerm contractName)
    fields := [ $[$fieldTerms],* ]
    «errors» := [ $[$errorTerms],* ]
    «events» := [ $[$eventTerms],* ]
    «constructor» := $constructorTerm
    functions := [ $[$allFunctionTerms],* ]
    «externals» := [ $[$externalTerms],* ]
    adtTypes := [ $[$adtTypeTerms],* ]
    storageNamespace := $namespaceTerm
  })

private def mkFindIdxFieldSimpCommands
    (contractIdent : Ident)
    (fields : Array StorageFieldDecl) : CommandElabM (Array Cmd) := do
  let contractName := toString contractIdent.getId
  let fieldTerms ← fields.mapM mkModelFieldTerm
  let fieldListTerm : Term ← `(([ $[$fieldTerms],* ] : List Compiler.CompilationModel.Field))
  let mut cmds : Array Cmd := #[]
  let mut idx := 0
  for field in fields do
    let baseName := s!"findIdx_{field.name}_{contractName}"
    let theoremName := mkIdent (Name.mkSimple baseName)
    let theoremNameDecide := mkIdent (Name.mkSimple (baseName ++ "_decide"))
    let idxTerm := natTerm idx
    let fieldNameTerm := strTerm field.name

    let eqCmd : Cmd ← `(command|
      @[simp] theorem $theoremName :
          List.findIdx? (fun x : Compiler.CompilationModel.Field => x.name == $fieldNameTerm)
            $fieldListTerm = some $idxTerm := by
        decide)
    cmds := cmds.push eqCmd

    let decideCmd : Cmd ← `(command|
      @[simp] theorem $theoremNameDecide :
          List.findIdx? (fun x : Compiler.CompilationModel.Field => decide (x.name = $fieldNameTerm))
            $fieldListTerm = some $idxTerm := by
        decide)
    cmds := cmds.push decideCmd
    idx := idx + 1
  pure cmds

private def mkFindIdxParamSimpCommandsForScope
    (contractName : String)
    (scopeName : String)
    (params : Array ParamDecl) : CommandElabM (Array Cmd) := do
  let paramNameTerms : Array Term := params.map (fun p => strTerm p.name)
  let paramListTerm : Term ← `(([ $[$paramNameTerms],* ] : List String))
  let mut cmds : Array Cmd := #[]
  let mut idx := 0
  for param in params do
    let baseName := s!"findIdx_param_{param.name}_{scopeName}_{contractName}"
    let theoremName := mkIdent (Name.mkSimple baseName)
    let theoremNameDecide := mkIdent (Name.mkSimple (baseName ++ "_decide"))
    let idxTerm := natTerm idx
    let paramNameTerm := strTerm param.name

    let eqCmd : Cmd ← `(command|
      @[simp] theorem $theoremName :
          List.findIdx? (fun x => x == $paramNameTerm)
            $paramListTerm = some $idxTerm := by
        decide)
    cmds := cmds.push eqCmd

    let decideCmd : Cmd ← `(command|
      @[simp] theorem $theoremNameDecide :
          List.findIdx? (fun x => decide (x = $paramNameTerm))
            $paramListTerm = some $idxTerm := by
        decide)
    cmds := cmds.push decideCmd
    idx := idx + 1
  pure cmds

private def mkFindIdxParamSimpCommands
    (contractIdent : Ident)
    (ctor : Option ConstructorDecl)
    (functions : Array FunctionDecl) : CommandElabM (Array Cmd) := do
  let contractName := toString contractIdent.getId
  let mut cmds : Array Cmd := #[]
  match ctor with
  | some ctorDecl =>
      let ctorCmds ← mkFindIdxParamSimpCommandsForScope contractName "constructor" ctorDecl.params
      cmds := cmds ++ ctorCmds
  | none => pure ()
  for fn in functions do
    let fnCmds ← mkFindIdxParamSimpCommandsForScope contractName (toString fn.ident.getId) fn.params
    cmds := cmds ++ fnCmds
  pure cmds

/-- Compute the storage namespace for a contract.
    `storageNamespace("Foo") = keccak256("Foo.storage.v0")` as a 256-bit Nat.
    The result can be used as a base offset so different contracts never collide
    in the shared 2^256 storage address space.
    (#1730, Axis 4 Step 4a) -/
def computeStorageNamespace (contractName : String) : Nat :=
  KeccakEngine.keccak256_str_nat s!"{contractName}.storage.v0"

/-- Compute a storage namespace from an explicit user-provided namespace key. -/
def computeStorageNamespaceKey (key : String) : Nat :=
  KeccakEngine.keccak256_str_nat key

private def natToBytes32BE (n : Nat) : ByteArray :=
  let bytes := (List.range 32).map fun idx =>
    UInt8.ofNat ((n / (256 ^ (31 - idx))) % 256)
  ⟨bytes.toArray⟩

/-- Compute an ERC-7201 namespace root:
    `keccak256(abi.encode(uint256(keccak256(key)) - 1)) & ~bytes32(uint256(0xff))`. -/
def computeERC7201StorageNamespaceKey (key : String) : Nat :=
  let keyHash := KeccakEngine.keccak256_str_nat key
  let encoded := natToBytes32BE (keyHash - 1)
  let slotHash := KeccakEngine.byteArrayToNatBE (KeccakEngine.keccak256 encoded)
  (slotHash / 256) * 256

private def namespaceOffsetFromSpec
    (contractName : Ident) (spec : TSyntax `verityNamespaceSpec) : CommandElabM Nat := do
  match spec with
  | `(verityNamespaceSpec| storage_namespace erc7201 $customKey:str) =>
      match customKey.raw.isStrLit? with
      | some key => pure (computeERC7201StorageNamespaceKey key)
      | none => throwErrorAt customKey "expected storage namespace string literal"
  | `(verityNamespaceSpec| storage_namespace $customKey:str) =>
      match customKey.raw.isStrLit? with
      | some key => pure (computeStorageNamespaceKey key)
      | none => throwErrorAt customKey "expected storage namespace string literal"
  | `(verityNamespaceSpec| storage_namespace) =>
      pure (computeStorageNamespace (toString contractName.getId))
  | _ =>
      throwErrorAt spec "unsupported storage namespace syntax"

private def namespaceOffsetFromStorageItem?
    (contractName : Ident) (item : TSyntax `verityStorageItem) : CommandElabM (Option Nat) := do
  match item with
  | `(verityStorageItem| storage_namespace erc7201 $customKey:str) =>
      match customKey.raw.isStrLit? with
      | some key => pure (some (computeERC7201StorageNamespaceKey key))
      | none => throwErrorAt customKey "expected storage namespace string literal"
  | `(verityStorageItem| storage_namespace $customKey:str) =>
      match customKey.raw.isStrLit? with
      | some key => pure (some (computeStorageNamespaceKey key))
      | none => throwErrorAt customKey "expected storage namespace string literal"
  | `(verityStorageItem| storage_namespace) =>
      pure (some (computeStorageNamespace (toString contractName.getId)))
  | _ => pure none

private def storageFieldFromItem?
    (item : TSyntax `verityStorageItem) : CommandElabM (Option (TSyntax `verityStorageField)) := do
  match item with
  | `(verityStorageItem| $name:ident : $ty:term := slot $slotNum:num) =>
      pure (some (← `(verityStorageField| $name:ident : $ty:term := slot $slotNum:num)))
  | _ => pure none

private partial def offsetStorageAccessorTree (offset : Nat) : StorageAccessorTree → StorageAccessorTree
  | .leaf name ty slotNum => .leaf name ty (slotNum + offset)
  | .node name children => .node name (children.map (offsetStorageAccessorTree offset))

structure ParsedContractSyntax where
  contractName : Ident
  newtypeDecls : Array NewtypeDecl
  structDecls : Array StructDecl
  adtDecls : Array AdtDecl
  fields : Array StorageFieldDecl
  storageStructAccessors : Array StorageStructAccessorDecl
  errorDecls : Array ErrorDecl
  eventDecls : Array EventDecl
  constDecls : Array ConstantDecl
  immutableDecls : Array ImmutableDecl
  externalDecls : Array ExternalDecl
  ctor : Option ConstructorDecl
  modifiers : Array ModifierDecl
  functions : Array FunctionDecl
  storageNamespace : Option Nat

def parseContractSyntax
    (stx : Syntax)
    : CommandElabM ParsedContractSyntax := do
  match stx with
  | `(command| verity_contract $contractName:ident where $[types $[$newtypeDecls:verityNewtype]*]? $[inductive $[$adtDecls:verityAdtDecl]*]? $[$nsSpec:verityNamespaceSpec]? storage $[$storageItems:verityStorageItem]* $[$structDecls:verityStructDecl]* $[errors $[$errorDecls:verityError]*]? $[event_defs $[$eventDecls:verityEvent]*]? $[constants $[$constantDecls:verityConstant]*]? $[immutables $[$immutableDecls:verityImmutable]*]? $[interfaces $[$interfaceDecls:verityInterface]*]? $[linked_externals $[$externalDecls:verityExternal]*]? $[$ctor:verityConstructor]? $[$entrypoints:veritySpecialEntrypoint]* $[$modifierDecls:verityModifier]* $[$functions:verityFunction]*) =>
      -- Parse newtypes first — they are needed by all downstream type resolution
      let parsedNewtypes ←
        match newtypeDecls with
        | some decls => decls.mapM parseNewtype
        | none => pure #[]
      -- Validate: no duplicate type names
      let mut seenNames : Array String := #[]
      for nt in parsedNewtypes do
        if seenNames.contains nt.name then
          throwErrorAt nt.ident s!"duplicate type name '{nt.name}'"
        seenNames := seenNames.push nt.name
      -- Validate: type names don't shadow built-in types
      let builtinTypeNames := #["Uint256", "Int256", "Uint8", "Address", "Bytes32", "Bool", "String", "Bytes", "Unit", "Array", "Tuple"]
      for nt in parsedNewtypes do
        if builtinTypeNames.contains nt.name then
          throwErrorAt nt.ident s!"type name '{nt.name}' shadows a built-in type"
      let mut parsedStructs : Array StructDecl := #[]
      for structStx in structDecls do
        let parsedStruct ← parseStructDecl parsedNewtypes parsedStructs structStx
        if seenNames.contains parsedStruct.name then
          throwErrorAt parsedStruct.ident s!"duplicate type name '{parsedStruct.name}'"
        if builtinTypeNames.contains parsedStruct.name then
          throwErrorAt parsedStruct.ident s!"struct name '{parsedStruct.name}' shadows a built-in type"
        seenNames := seenNames.push parsedStruct.name
        parsedStructs := parsedStructs.push parsedStruct
      -- Parse ADT declarations (#1727, Axis 1 Step 5a)
      let parsedAdts ←
        match adtDecls with
        | some decls => decls.mapM (parseAdtDecl parsedNewtypes)
        | none => pure #[]
      -- Validate: no duplicate ADT names
      for adtDecl in parsedAdts do
        if seenNames.contains adtDecl.name then
          throwErrorAt adtDecl.ident s!"duplicate type name '{adtDecl.name}'"
        seenNames := seenNames.push adtDecl.name
      -- Validate: ADT names don't shadow built-in types
      for adtDecl in parsedAdts do
        if builtinTypeNames.contains adtDecl.name then
          throwErrorAt adtDecl.ident s!"ADT name '{adtDecl.name}' shadows a built-in type"
      -- Compute the initial namespace offset (#1730, Axis 4 Steps 4b/4c).
      -- A legacy `storage_namespace` before `storage` applies to all following
      -- fields until overridden by an in-storage `storage_namespace` item.
      let namespaceOffset : Nat ←
        match nsSpec with
        | some spec => namespaceOffsetFromSpec contractName spec
        | none => pure 0
      let parsedErrors ←
        match errorDecls with
        | some decls => decls.mapM (parseError parsedNewtypes parsedStructs parsedAdts)
        | none => pure #[]
      let parsedEvents ←
        match eventDecls with
        | some decls => decls.mapM (parseEvent parsedNewtypes parsedStructs parsedAdts)
        | none => pure #[]
      let parsedConstants ←
        match constantDecls with
        | some decls => decls.mapM (parseConstant parsedNewtypes)
        | none => pure #[]
      let parsedImmutables ←
        match immutableDecls with
        | some decls => decls.mapM (parseImmutable parsedNewtypes)
        | none => pure #[]
      let parsedInterfaces ←
        match interfaceDecls with
        | some decls => decls.mapM (parseInterface parsedNewtypes parsedStructs parsedAdts)
        | none => pure #[]
      let seenTypeLocalNames := seenNames.map localDeclName
      let mut seenInterfaceNames : Array String := #[]
      for iface in parsedInterfaces do
        let ifaceLocalName := localDeclName iface.name
        if seenTypeLocalNames.contains ifaceLocalName then
          throwErrorAt iface.ident s!"interface name '{ifaceLocalName}' conflicts with an existing type name"
        if builtinTypeNames.contains ifaceLocalName then
          throwErrorAt iface.ident s!"interface name '{ifaceLocalName}' shadows a built-in type"
        if seenInterfaceNames.contains ifaceLocalName then
          throwErrorAt iface.ident s!"duplicate interface name '{ifaceLocalName}'"
        seenInterfaceNames := seenInterfaceNames.push ifaceLocalName
      let interfaceNames := parsedInterfaces.map (·.name)
      let parsedExternals ←
        match externalDecls with
        | some decls => decls.mapM (parseExternal parsedNewtypes parsedStructs parsedAdts)
        | none => pure #[]
      let parsedExternals := interfaceExternals parsedInterfaces ++ parsedExternals
      -- Apply namespace offsets to parsed storage fields (#1730).  In-storage
      -- `storage_namespace` items switch the active namespace for subsequent
      -- fields, allowing multiple ERC-7201 roots in one contract model.
      let mut parsedFields : Array StorageFieldDecl := #[]
      let mut parsedStorageStructAccessors : Array StorageStructAccessorDecl := #[]
      let mut currentNamespaceOffset := namespaceOffset
      let mut namespaceOpt : Option Nat :=
        if nsSpec.isSome then some namespaceOffset else none
      for item in storageItems do
        match (← namespaceOffsetFromStorageItem? contractName item) with
        | some offset =>
            currentNamespaceOffset := offset
            if namespaceOpt.isNone then
              namespaceOpt := some offset
        | none =>
            match (← parseStorageStructItem parsedNewtypes parsedStructs parsedAdts item) with
            | some (structFields, accessor) =>
                parsedFields := parsedFields ++
                  (structFields.map fun field => { field with slotNum := field.slotNum + currentNamespaceOffset })
                parsedStorageStructAccessors := parsedStorageStructAccessors.push
                  { accessor with tree := offsetStorageAccessorTree currentNamespaceOffset accessor.tree }
            | none =>
                match (← storageFieldFromItem? item) with
                | some fieldStx =>
                    let field ← parseStorageField parsedNewtypes parsedStructs parsedAdts fieldStx
                    parsedFields := parsedFields.push { field with slotNum := field.slotNum + currentNamespaceOffset }
                | none =>
                    throwErrorAt item "unsupported storage item"
      pure {
        contractName := contractName
        newtypeDecls := parsedNewtypes
        structDecls := parsedStructs
        adtDecls := parsedAdts
        fields := parsedFields
        storageStructAccessors := parsedStorageStructAccessors
        errorDecls := parsedErrors
        eventDecls := parsedEvents
        constDecls := parsedConstants
        immutableDecls := parsedImmutables
        externalDecls := parsedExternals
        ctor := (← ctor.mapM (parseConstructor parsedNewtypes parsedStructs parsedAdts))
        modifiers := (← modifierDecls.mapM parseModifier)
        functions :=
          assignOverloadInternalIdents
            (← monomorphizeHigherOrderHelpers
              ((← entrypoints.mapM parseSpecialEntrypoint) ++
                (← functions.mapM (parseFunction parsedNewtypes parsedStructs parsedAdts interfaceNames))))
        storageNamespace := namespaceOpt
      }
  | _ => throwErrorAt stx "invalid verity_contract declaration"

private def mkConstantDefCommand (constant : ConstantDecl) : CommandElabM Cmd := do
  `(command| def $constant.ident : $(← contractValueTypeTerm constant.ty) := $constant.body)

def mkStorageDefCommandPublic (field : StorageFieldDecl) : CommandElabM Cmd :=
  mkStorageDefCommand field

def mkConstantDefCommandPublic (constant : ConstantDecl) : CommandElabM Cmd :=
  mkConstantDefCommand constant

def mkStructDefCommandPublic (decl : StructDecl) : CommandElabM Cmd := do
  let structId := decl.ident
  let fieldIds := decl.fields.map (·.ident)
  let fieldTys ← decl.fields.mapM (fun field => contractValueTypeTerm field.ty)
  `(command| structure $structId where
      $[$fieldIds:ident : $fieldTys:term]*
      deriving Repr, Inhabited)

def mkStructEventArgInstanceCommandPublic (decl : StructDecl) : CommandElabM Cmd := do
  let structId := decl.ident
  `(command| instance : CoeTC $structId _root_.Contracts.EventArg where
      coe _ := _root_.Contracts.EventArg.word (pure (0 : _root_.Verity.Uint256)))

/-- Generate a `def storageNamespace : Nat := <keccak-value>` command for
    the current contract.  Uses the resolved namespace value from
    `parseContractSyntax` to respect custom `storage_namespace "key"`.
    (#1730, Axis 4 Step 4a) -/
def mkStorageNamespaceCommand (contractName : String) (resolvedNamespace : Option Nat := none) : CommandElabM Cmd := do
  let _ := contractName
  let ns := resolvedNamespace.getD 0
  let id : Ident := mkIdent (Name.mkSimple "storageNamespace")
  `(command| def $id : Nat := $(natTerm ns))

def validateConstantDeclsPublic (constDecls : Array ConstantDecl) : CommandElabM Unit := do
  for constant in constDecls do
    validateConstantBody constDecls constant.body [constant.name]
  validateConstantExprTypes constDecls

def validateGeneratedDefNamesPublic
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (functions : Array FunctionDecl) : CommandElabM Unit := do
  let reservedGeneratedNames : Array String := #["spec", "storageNamespace"]
  let mut generatedHelperNames : Array String := reservedGeneratedNames
  if hasStructMapping fields then
    generatedHelperNames := generatedHelperNames.push "structMember"
    generatedHelperNames := generatedHelperNames.push "setStructMember"
  if hasStructMapping2 fields then
    generatedHelperNames := generatedHelperNames.push "structMember2"
    generatedHelperNames := generatedHelperNames.push "setStructMember2"

  let mut storageNames : Array String := #[]
  for field in fields do
    if generatedHelperNames.contains field.name then
      throwErrorAt field.ident
        s!"storage field '{field.name}' conflicts with reserved generated declaration '{field.name}'"
    if storageNames.contains field.name then
      throwErrorAt field.ident s!"duplicate storage field declaration '{field.name}'"
    storageNames := storageNames.push field.name

  let mut constantNames : Array String := #[]
  for constant in constDecls do
    if generatedHelperNames.contains constant.name then
      throwErrorAt constant.ident
        s!"constant '{constant.name}' conflicts with reserved generated declaration '{constant.name}'"
    if storageNames.contains constant.name then
      throwErrorAt constant.ident
        s!"constant '{constant.name}' conflicts with a storage field of the same name"
    if constantNames.contains constant.name then
      throwErrorAt constant.ident
        s!"duplicate constant declaration '{constant.name}'"
    constantNames := constantNames.push constant.name

  let mut immutableNames : Array String := #[]
  for imm in immutableDecls do
    if generatedHelperNames.contains imm.name then
      throwErrorAt imm.ident
        s!"immutable '{imm.name}' conflicts with reserved generated declaration '{imm.name}'"
    immutableNames := immutableNames.push imm.name

  let mut functionNames : Array String := #[]
  let mut functionSignatures : Array String := #[]
  let mut functionAbiSignatures : Array String := #[]
  for fn in functions do
    let generatedFnName := toString fn.ident.getId
    let signature := functionSignatureKey fn
    let abiSignature := functionAbiSignatureKey fn
    if generatedHelperNames.contains fn.name then
      throwErrorAt fn.ident
        s!"function '{fn.name}' conflicts with reserved generated declaration '{fn.name}'"
    if storageNames.contains fn.name then
      throwErrorAt fn.ident
        s!"function '{fn.name}' conflicts with a storage field of the same name"
    if constantNames.contains fn.name then
      throwErrorAt fn.ident
        s!"function '{fn.name}' conflicts with a contract constant of the same name"
    if immutableNames.contains fn.name then
      throwErrorAt fn.ident
        s!"function '{fn.name}' conflicts with an immutable of the same name"
    if storageNames.contains generatedFnName then
      throwErrorAt fn.ident
        s!"function '{fn.name}' generates internal declaration '{generatedFnName}' that conflicts with a storage field of the same name"
    if constantNames.contains generatedFnName then
      throwErrorAt fn.ident
        s!"function '{fn.name}' generates internal declaration '{generatedFnName}' that conflicts with a contract constant of the same name"
    if immutableNames.contains generatedFnName then
      throwErrorAt fn.ident
        s!"function '{fn.name}' generates internal declaration '{generatedFnName}' that conflicts with an immutable of the same name"
    if generatedHelperNames.contains generatedFnName then
      throwErrorAt fn.ident
        s!"function '{fn.name}' generates internal declaration '{generatedFnName}' that conflicts with reserved generated declaration '{generatedFnName}'"
    if functionSignatures.contains signature then
      throwErrorAt fn.ident
        s!"duplicate function declaration '{signature}'"
    if functionAbiSignatures.contains abiSignature then
      throwErrorAt fn.ident
        s!"duplicate function ABI signature '{abiSignature}' after ABI erasure"
    if functionNames.contains generatedFnName then
      throwErrorAt fn.ident
        s!"function '{fn.name}' generates duplicate internal declaration '{generatedFnName}'"
    functionNames := functionNames.push generatedFnName
    functionSignatures := functionSignatures.push signature
    functionAbiSignatures := functionAbiSignatures.push abiSignature

    let helperNames :=
      #[ s!"{generatedFnName}_modelBody"
       , s!"{generatedFnName}_model"
       , s!"{generatedFnName}_bridge"
       , s!"{generatedFnName}_semantic_preservation"
       , s!"{generatedFnName}_is_view"
       , s!"{generatedFnName}_no_calls"
       , s!"{generatedFnName}_modifies"
       , s!"{generatedFnName}_frame"
       , s!"{generatedFnName}_frame_rfl"
       , s!"{generatedFnName}_effects"
       , s!"{generatedFnName}_cei_compliant"
       , s!"{generatedFnName}_nonreentrant"
       , s!"{generatedFnName}_cei_safe"
       , s!"{generatedFnName}_requires_role"
       ]
    for helperName in helperNames do
      if storageNames.contains helperName then
        throwErrorAt fn.ident
          s!"function '{fn.name}' generates helper '{helperName}' that conflicts with a storage field of the same name"
      if constantNames.contains helperName then
        throwErrorAt fn.ident
          s!"function '{fn.name}' generates helper '{helperName}' that conflicts with a contract constant of the same name"
      if immutableNames.contains helperName then
        throwErrorAt fn.ident
          s!"function '{fn.name}' generates helper '{helperName}' that conflicts with an immutable of the same name"
      if functionNames.contains helperName then
        throwErrorAt fn.ident
          s!"function '{fn.name}' generates helper '{helperName}' that conflicts with a function of the same name"
      if generatedHelperNames.contains helperName then
        throwErrorAt fn.ident
          s!"function '{fn.name}' generates duplicate helper declaration '{helperName}'"
      generatedHelperNames := generatedHelperNames.push helperName

def validateImmutableDeclsPublic
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (ctor : Option ConstructorDecl := none) : CommandElabM Unit := do
  let mut seenNames : Array String := #[]
  let ctorParams := ctor.map (·.params) |>.getD #[]
  for imm in immutableDecls do
    validateImmutableType imm
    let inferredTy ← inferPureExprType fields constDecls #[] #[] ctorParams #[] imm.body
    requireDeclaredValueType imm.body s!"immutable '{imm.name}'" imm.ty inferredTy
    if fields.any (fun field => field.name == imm.name) then
      throwErrorAt imm.ident
        s!"immutable '{imm.name}' conflicts with a storage field of the same name"
    if constDecls.any (fun constant => constant.name == imm.name) then
      throwErrorAt imm.ident
        s!"immutable '{imm.name}' conflicts with a contract constant of the same name"
    if seenNames.contains imm.name then
      throwErrorAt imm.ident
        s!"duplicate immutable declaration '{imm.name}'"
    seenNames := seenNames.push imm.name

def validateExternalDeclsPublic
    (externalDecls : Array ExternalDecl) : CommandElabM Unit := do
  let mut seenNames : Array String := #[]
  for ext in externalDecls do
    if seenNames.contains ext.name then
      throwErrorAt ext.ident
        s!"duplicate external declaration '{ext.name}'"
    -- Multi-return externals are allowed; the auto-revert expression form (externalCall)
    -- only supports single-return, but tryExternalCall supports any return count.
    -- (#1727, Axis 1 Step 5f)
    for paramTy in ext.params do
      validateExternalExecutableParamType ext.ident ext.name paramTy
    for returnTy in ext.returnTys do
      validateExternalExecutableType ext.ident ext.name returnTy "return"
    seenNames := seenNames.push ext.name

private def validateLocalObligationDecls
    (owner : String)
    (obligations : Array LocalObligationDecl) : CommandElabM Unit := do
  let mut seenNames : Array String := #[]
  for obligation in obligations do
    if obligation.obligation.isEmpty then
      throwErrorAt obligation.ident
        s!"{owner} local obligation '{obligation.name}' must not be empty"
    if seenNames.contains obligation.name then
      throwErrorAt obligation.ident
        s!"duplicate local obligation '{obligation.name}' on {owner}"
    seenNames := seenNames.push obligation.name

def validateFunctionDeclsPublic
    (fields : Array StorageFieldDecl)
    (errorDecls : Array ErrorDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (ctor : Option ConstructorDecl)
    (modifiers : Array ModifierDecl)
    (functions : Array FunctionDecl) : CommandElabM Unit := do
  match ctor with
  | some ctor =>
      for param in ctor.params do
        rejectExecutableBoundaryAdt param.ident s!"constructor parameter '{param.name}'" param.ty
      validateLocalObligationDecls "constructor" ctor.localObligations
      validateConstructorBodyExprTypes fields errorDecls constDecls immutableDecls externalDecls functions ctor
  | none => pure ()
  let modifierNames := modifiers.map (·.name)
  for modDecl in modifiers do
    match modDecl.body with
    | `(term| do $[$elems:doElem]*) =>
        let _ ← validateDoElemsExprTypes modDecl.name fields constDecls immutableDecls externalDecls errorDecls functions #[] #[] elems
        pure ()
    | _ => throwErrorAt modDecl.body "modifier body must be a do block"
  for fn in functions do
    for param in fn.params do
      rejectExecutableBoundaryAdt param.ident s!"function '{fn.name}' parameter '{param.name}'" param.ty
    rejectExecutableBoundaryAdt fn.ident s!"function '{fn.name}' return type" fn.returnTy
    if fn.isInternal then
      ensureSupportsInternalHelperSpec fn.ident.raw fn
    validateLocalObligationDecls s!"function '{fn.name}'" fn.localObligations
    for modifierIdent in fn.modifiers do
      let modifierName := toString modifierIdent.getId
      if !modifierNames.contains modifierName then
        throwErrorAt modifierIdent s!"function '{fn.name}' references unknown modifier '{modifierName}'"
    -- Validate modifies field names exist in the storage section
    for modField in fn.modifies do
      let modName := toString modField.getId
      let allFieldNames := fields.map (·.name)
      if !allFieldNames.contains modName then
        throwErrorAt modField s!"function '{fn.name}': modifies references unknown storage field '{modName}'; known fields: {allFieldNames.toList}"
    -- view functions must not use modifies (they already imply no writes)
    if fn.isView && !fn.modifies.isEmpty then
      throwErrorAt fn.ident s!"function '{fn.name}' is marked view and modifies(...); view already guarantees no state writes"
    if fn.isPure && !fn.modifies.isEmpty then
      throwErrorAt fn.ident s!"function '{fn.name}' is marked pure and modifies(...); pure already guarantees no state writes"
    -- Validate nonreentrant lock field references a valid storage field of scalar uint256 type
    match fn.nonReentrantLock with
    | some lockField =>
        let lockName := toString lockField.getId
        let allFieldNames := fields.map (·.name)
        match fields.find? (fun field => field.name == lockName) with
        | none =>
          throwErrorAt lockField s!"function '{fn.name}': nonreentrant references unknown storage field '{lockName}'; known fields: {allFieldNames.toList}"
        | some field =>
            match field.ty with
            | .scalar .uint256 => pure ()
            | _ =>
                throwErrorAt lockField s!"function '{fn.name}': nonreentrant lock field '{lockName}' must be a scalar Uint256 storage field"
    | none => pure ()
    -- cei_safe and allow_post_interaction_writes are mutually exclusive with nonreentrant
    if fn.ceiSafe && fn.allowPostInteractionWrites then
      throwErrorAt fn.ident s!"function '{fn.name}': cei_safe and allow_post_interaction_writes are mutually exclusive"
    if fn.nonReentrantLock.isSome && fn.allowPostInteractionWrites then
      throwErrorAt fn.ident s!"function '{fn.name}': nonreentrant and allow_post_interaction_writes are mutually exclusive"
    if fn.nonReentrantLock.isSome && fn.ceiSafe then
      throwErrorAt fn.ident s!"function '{fn.name}': nonreentrant and cei_safe are mutually exclusive"
    validateFunctionBodyExprTypes fields errorDecls constDecls immutableDecls externalDecls functions fn

def mkFunctionCommandsPublic
    (fields : Array StorageFieldDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (functions : Array FunctionDecl)
    (fn : FunctionDecl) : CommandElabM (Array Cmd) := do
  let fnType ← mkContractFnType fn.params fn.returnTy
  let fnRoleGuardedBody ← mkRoleGuardedBody fields fn
  let fnDecl := { fn with body := fnRoleGuardedBody }
  let fnGuardedBody ← mkInitGuardedBody fields fnDecl
  let fnBody ← mkImmutableBoundBody fields immutableDecls fn fnGuardedBody
  let fnExecutableBody ← rewriteForEachExecutableBody externalDecls fn.params fnBody
  let fnValue ← mkContractFnValue fn.params fnExecutableBody
  let modelBodyName ← mkSuffixedIdent fn.ident "_modelBody"
  let modelName ← mkSuffixedIdent fn.ident "_model"
  let stmtTerms ← translateBodyToStmtTerms fields constDecls immutableDecls externalDecls functions fn
  let modelParams ← mkModelParamsTerm fn.params
  let localObligationTerms ← fn.localObligations.mapM mkModelLocalObligationTerm
  let payableTerm ← if fn.isPayable then `(true) else `(false)
  let viewTerm ← if fn.isView then `(true) else `(false)
  let pureTerm ← if fn.isPure then `(true) else `(false)
  let noExternalCallsTerm ← if fn.noExternalCalls then `(true) else `(false)
  let allowPostInteractionWritesTerm ← if fn.allowPostInteractionWrites then `(true) else `(false)
  let nonReentrantLockTerm ← match fn.nonReentrantLock with
    | some lockIdent => `(some $(strTerm (toString lockIdent.getId)))
    | none => `(none)
  let ceiSafeTerm ← if fn.ceiSafe then `(true) else `(false)
  let requiresRoleTerm ← match fn.requiresRole with
    | some roleIdent => `(some $(strTerm (toString roleIdent.getId)))
    | none => `(none)
  let modifiesTerms : Array Term := fn.modifies.map fun ident => strTerm (toString ident.getId)
  let returnTypeTerm ← modelReturnTypeTerm fn.returnTy
  let returnsTerm ← modelReturnsTerm fn.returnTy

  let fnCmd : Cmd ← `(command| def $fn.ident : $fnType := $fnValue)
  let bodyCmd : Cmd ← `(command| def $modelBodyName : List Compiler.CompilationModel.Stmt := [ $[$stmtTerms],* ])
  let modelNameTerm :=
    if fn.isInternal then
      strTerm (internalHelperSpecNameFor fn)
    else
      strTerm fn.name
  let internalTerm ← if fn.isInternal then `(true) else `(false)
  let modelCmd : Cmd ← `(command| def $modelName : Compiler.CompilationModel.FunctionSpec := {
    name := $modelNameTerm
    params := $modelParams
    returnType := $returnTypeTerm
    «returns» := $returnsTerm
    isPayable := $payableTerm
    isView := $viewTerm
    isPure := $pureTerm
    noExternalCalls := $noExternalCallsTerm
    allowPostInteractionWrites := $allowPostInteractionWritesTerm
    nonReentrantLock := $nonReentrantLockTerm
    ceiSafe := $ceiSafeTerm
    requiresRole := $requiresRoleTerm
    modifies := [ $[$modifiesTerms],* ]
    localObligations := [ $[$localObligationTerms],* ]
    body := $modelBodyName
    isInternal := $internalTerm
  })
  pure #[fnCmd, bodyCmd, modelCmd]

def mkSpecCommandPublic
    (contractName : String)
    (fields : Array StorageFieldDecl)
    (errorDecls : Array ErrorDecl)
    (eventDecls : Array EventDecl)
    (constDecls : Array ConstantDecl)
    (immutableDecls : Array ImmutableDecl)
    (externalDecls : Array ExternalDecl)
    (ctor : Option ConstructorDecl)
    (modifiers : Array ModifierDecl)
    (functions : Array FunctionDecl)
    (adtDecls : Array AdtDecl)
    (storageNamespace : Option Nat) : CommandElabM Cmd :=
  mkSpecCommand contractName fields errorDecls eventDecls constDecls immutableDecls externalDecls ctor modifiers functions adtDecls storageNamespace

def mkFindIdxFieldSimpCommandsPublic
    (contractIdent : Ident)
    (fields : Array StorageFieldDecl) : CommandElabM (Array Cmd) :=
  mkFindIdxFieldSimpCommands contractIdent fields

def mkFindIdxParamSimpCommandsPublic
    (contractIdent : Ident)
    (ctor : Option ConstructorDecl)
    (functions : Array FunctionDecl) : CommandElabM (Array Cmd) :=
  mkFindIdxParamSimpCommands contractIdent ctor functions

/-- Public wrapper for `contractValueTypeTerm`, used by the semantic bridge
    theorem generator in `Bridge.lean` (Issue #998). -/
def contractValueTypeTermPublic (ty : ValueType) : CommandElabM Term :=
  contractValueTypeTerm ty

/-- Public wrapper for `strTerm`, used by the semantic bridge
    theorem generator in `Bridge.lean` (Issue #998). -/
def strTermPublic (s : String) : Term := strTerm s

/-- Public wrapper for `natTerm`, used by the semantic bridge
    theorem generator in `Bridge.lean` (Issue #998). -/
def natTermPublic (n : Nat) : Term := natTerm n

end Verity.Macro
